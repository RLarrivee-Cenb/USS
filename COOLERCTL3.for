C
	PROGRAM COOLERCTL3
C
C	(COOLER CONTROL STEP III AGGLOMERATOR)
C
C	THIS PROGRAM RUNS EVERY 2 SECONDS AND CONTROLS THE SPEED
C	OF THE COOLER DRIVE FOR LINES 6 & 7 AGGL
C
C	LINK COOLERCTL,COOLERTCTL/OPT,WESAPI_LIB:WESAPI/OPT
C
	IMPLICIT NONE
C
	INCLUDE 'WESAPI:SPD.DCL'
	INCLUDE 'WESAPI:SHC_defines.DCL'
	INCLUDE 'WESAPI:SHC_err.DCL'
C
	INTEGER*4	LIB$SYS_TRNLOG
	INTEGER*4 	status
	CHARACTER*20	highway

	CHARACTER*40    spd_filename
	INTEGER*2	spd_filename_len
	INTEGER*2	spd_fd
	INTEGER*2	access_type
	INTEGER*2	c_short_val
	CHARACTER*2	field_name
	CHARACTER*8	pt_name
	BYTE		extended_flag
	BYTE		gp_bit_num
	BYTE		inactive_flag
	BYTE		c_byte_val
	INTEGER*4	gp_sid
	INTEGER*2       get_pkd_group_val
	INTEGER*2       get_pkd_group_stat
	INTEGER*2       gp_force_stat
	INTEGER*2       gp_op_mask
	INTEGER*2       gp_val_mask
	INTEGER*2	AS(10)
	BYTE		gp_bitnum 
	INTEGER*2	digital_val
	INTEGER*2	digital_stat
	INTEGER*2	TIMEOUT
C
	REAL            put_analog_val
	INTEGER*2	PUT_DIGITAL_VAL
	REAL*4		VALU(10)
C
	INTEGER*4	LEN,POINT_QUALITY
	INTEGER*4	AO(2),DO(3),AI(10),DI(4)
C
	CHARACTER*23	DATETIME
	CHARACTER*8	TIME/'0 0:0:02'/
C
	INTEGER*4	GOOD(2),UFO_OPEN
	INTEGER*4	GPT(40),TIMERX(2),FTIMER(2),BAD(2)
	REAL*4		TEMP,CHECK,LAST(2)
	REAL*4		LFAIL(2)/0.95,0.95/
C
	INTEGER*4	CONTRL(2)
C
	REAL*4		PRGLSP(2),
	1		LVLPRN(2),
	1		LVOBAS(2),
	1		SPDSET(2),
	1		SPDPRN(2),
	1		OUTBAS(2),
	1		OUTPUT(2),
	1		LVSPI(2),
	1		LVSPD(2),
	1		LVSPP(2),
	1		SPOUTI(2),
	1		SPOUTD(2),
	1		SPOUTP(2),
	1		LVLHI(2),
	1		LVLLO(2),
	1		SPDHI(2),
	1		SPDLO(2),
	1		CNTLSP(2),
	1		CRTLSP(2),
	1		LVLPRO(2),
	1		SPDPRO(2),
	1		SKIP,
	1		SPDAO(2),
	1		AMPPRN(2),
	1		AMPPRO(2),
	1		BEDLVL(2),
	1		SPEED(2),
	1		SETPT(2),
	1		SPDOUT(2),
	1		AMPS(2),
	1		FAIL(2),
	1		AMPI(2),
	1		AMPP(2),
	1		AMPD(2),
	1		AMPSET(2),
	1		AMPHI(2),
	1		AMPLO(2),
	1		AMPBAS(2),AMPEDB(2),LVLEDB(2)
C
	INTEGER*4	I,J,K,L,M,N,ADDRESS(2),BIT,XXX(2)/4,5/
C
	INTEGER*4	SPT(2)/552,553/,
	1		CLRCX(2)/453,458/,
	1		CTRLCX(2)/830,831/
C
	CHARACTER*8	AOP(2)/'AV800011','AV800012'/
C
	CHARACTER*8	DOP(3)/'DV800004','DV800005','DV800003'/
C
	CHARACTER*8	AIP(10)/'AI812048','AI814049','AI812047','AI814048',
	1			'S012X441','S014X031','AO812002','AO814001',
	1			'AI812046','AI814047'/
C
	CHARACTER*8	DIP(4)/'DC812000','DC814000','DI804189','DI806189'/
C
	COMMON/AG3_GOODPTTBL/GPT
C
	EQUIVALENCE	(VALU(1),BEDLVL(1))
	EQUIVALENCE	(VALU(2),BEDLVL(2))
	EQUIVALENCE	(VALU(3),SPEED(1))
	EQUIVALENCE	(VALU(4),SPEED(2))
	EQUIVALENCE	(VALU(5),SETPT(1))
	EQUIVALENCE	(VALU(6),SETPT(2))
	EQUIVALENCE	(VALU(7),SPDOUT(1))
	EQUIVALENCE	(VALU(8),SPDOUT(2))
	EQUIVALENCE	(VALU(9),AMPS(1))
	EQUIVALENCE	(VALU(10),AMPS(2))
C
	EQUIVALENCE	(DI(1),CTRLCX(1))
	EQUIVALENCE	(DI(2),CTRLCX(2))
	EQUIVALENCE	(DI(3),CLRCX(1))
	EQUIVALENCE	(DI(4),CLRCX(2))
C
	EXTERNAL	UFO_OPEN
C
5	CONTINUE
C
C	OPEN CONNECTION TO AGGL 3 POINT DIRECTORY
C
        status = LIB$SYS_TRNLOG('WESAPI_PDIR_1',
     1			spd_filename_len,spd_filename,,,)
C
	spd_filename(spd_filename_len+1:) = CHAR(0)
C
        access_type = RUNTIME
C
        spd_fd = SPD_open_file(%ref(spd_filename), access_type)
C
	DO 15	J=1,2	!GET THE 2 AGGL 3 ANALOG OUTPUT SIDS
C
	        status = SPD_get_sid(spd_fd,%ref(AOP(J)//char(0)),AO(J),
     x		             gp_sid, gp_bit_num, extended_flag, inactive_flag)

C
15	CONTINUE
C
	DO 25 J=1,3  !GET COMPUTER ACKNOWLEDGE DO SID AND SAAB ALARM DOS SIDS
C
	        status = SPD_get_sid(spd_fd,%ref(DOP(J)//char(0)),DO(J),
     x		             gp_sid, gp_bit_num, extended_flag, inactive_flag)

C
25	CONTINUE
C
	DO 30	J=1,10	!GET THE 10 AGGL 3 PROCESS VARIABLES SIDS
C
	        status = SPD_get_sid(spd_fd,%ref(AIP(J)//char(0)),AI(J),
     x		             gp_sid, gp_bit_num, extended_flag, inactive_flag)

C
30	CONTINUE
C
	DO 31	J=1,4	!GET THE 4 AGGL 3 DIGITAL INPUT SIDS
C
	        status = SPD_get_sid(spd_fd,%ref(DIP(J)//char(0)),DI(J),
     x		             gp_sid, gp_bit_num, extended_flag, inactive_flag)

C
31	CONTINUE
C
C  Close the point directory connection
C
	status = SPD_close_file(spd_fd)
C
C  Open the connection to SHC memory for WESAPI access
	status = SHC_open_memory()
C
	IF (status .NE. SHC_OK) THEN
		IF (status .EQ. SHC_E_MEMOPEN)GOTO 88
C
		OPEN	(UNIT=22,FILE='CTL:COOLER3.ERR',STATUS='UNKNOWN',
	1		 ACCESS='APPEND')
C
		WRITE	(22,77)status
77		FORMAT	(' SHC open memory failure - error =',I4)
C
		CLOSE	(UNIT=22)
C
	   GO TO 99999
C
	ENDIF
C
88	CONTINUE
C
	DO 20 	I=1,2
		TIMERX(I)=0 	!RESET 30 SEC RUN TIMER
		FTIMER(I)=0     !RESET SAAB FAIL TIMER
		GOOD(I)=0       !RESET SAAB NOT FROZEN TIMER
		BAD(I)=0        !RESET SAAB IS FROZEN TIMER
20	CONTINUE
C
	CALL	SYS$BINTIM(TIME,ADDRESS)
	CALL	SYS$SCHDWK(,,ADDRESS,ADDRESS)
C
100	CONTINUE
C
	DO 150	I=1,10	!READ IN THE 10 PROCESS VALUES NEEDED FOR THIS PASS
C
		status = SHC_get_analog_val_stat( AI(I), valu(I), AS(I))
C
150	CONTINUE
C
	OPEN	(UNIT=1,FILE='CTL:COOLER3.DAT',STATUS='OLD',
	1	RECL=68,ACCESS='DIRECT',RECORDTYPE='FIXED',
	1	FORM='UNFORMATTED',SHARED,USEROPEN=UFO_OPEN)
C
	READ	(1,REC=1)CONTRL,PRGLSP,LVLPRN,LVOBAS,
	1		 SPDSET,SPDPRN,OUTBAS,
	1		 OUTPUT,LVSPI,
	1		 LVSPD,LVSPP,SPOUTI,SPOUTD,
	1		 SPOUTP,LVLHI,LVLLO,SPDHI,
	1		 SPDLO,CNTLSP,CRTLSP,LVLPRO,
	1		 SPDPRO,FAIL,AMPI,AMPP,AMPD,
	1		 AMPSET,AMPHI,AMPLO,
	1		 AMPBAS,AMPPRN,AMPPRO,AMPEDB,LVLEDB
C
	DO 1000 I=1,2
C
		LVLPRN(I)=BEDLVL(I)
		SPDPRN(I)=SPEED(I)
		CNTLSP(I)=SETPT(I)
		SPDAO(I)=SPDOUT(I)
		AMPPRN(I)=AMPS(I)
		LAST(I)=LVLPRO(I)
C
		CALL CHKGPT(SPT(I),BIT)		!CHECK GPT ON CONTROLLER SPT
C
		IF (BIT .NE. 1) THEN
			PRGLSP(I)=CRTLSP(I)	!USE OPERATOR SETPOINT
		ELSE
			PRGLSP(I)=CNTLSP(I)	!USE CONTROLLER SETPOINT
		END IF
C
		status = SHC_get_digital_val_stat(CTRLCX(I),gp_sid,gp_bitnum,
	1	digital_val, digital_stat)	!SEE IF COOLER ON COMP
C
		IF (digital_val .EQ. 1) THEN
			CONTRL(I)=JIBCLR(CONTRL(I),0)	!ON COMP
		ELSE 
			CONTRL(I)=JIBSET(CONTRL(I),0)	!OFF COMP
			OUTPUT(I)=SPDAO(I)		!MATCH CONTROLLER OUTPUT
			OUTBAS(I)=SPDAO(I)		!RESET OUTPUT BASE
			LVOBAS(I)=SPDPRN(I)		!RESET BED LEVEL BASE
			AMPBAS(I)=SPDPRN(I)		!RESET AMPS BASE
			GOTO 900
		END IF
C
		status = SHC_get_digital_val_stat(CLRCX(I),gp_sid,gp_bitnum,
	1	digital_val, digital_stat)	!CHECK IF COOLER RUNNING
C
		IF (digital_val .EQ. 0)THEN		!COOLER IS OFF
			TIMERX(I)=15		!SET UP RUN TIMER
			GOTO 1000
		END IF
C
C	BED LEVEL PID OUTPUT IS NEW COOLER SPEED FLOATING SETPOINT
C
		IF (TIMERX(I) .EQ. 0)GOTO 500
C
		TIMERX(I)=TIMERX(I)-1
		OUTBAS(I)=OUTPUT(I)	!RESET OUTPUT BASE
		LVOBAS(I)=SPDPRN(I)	!RESET BED LEVEL BASE
		AMPBAS(I)=SPDPRN(I)	!RESET AMPS BASE
		GOTO 900
C
500		CONTINUE
C
		IF (BJTEST(CONTRL(I),2))GOTO 800 !IF SAAB IS FROZE SKIP CHECK
C
		IF ((LVLPRN(I) .GE. FAIL(I)).OR.
	1	    (LVLPRN(I) .LT. LFAIL(I)))THEN	!SAAB IS BAD
			IF (BJTEST(CONTRL(I),1))THEN	!ARE WE ALREADY ON AMPS
				FTIMER(I)=0		!YES RESET TIMER
				GOTO 800		!GO CALCULATE
			ELSE
				FTIMER(I)=FTIMER(I)+1	!HAS SAAB BEEN BO
				IF (FTIMER(I) .GE. 2)THEN	!FOR 4 SECS
					CONTRL(I)=JIBSET(CONTRL(I),1)
					FTIMER(I)=0	!YES INDICATE AMPS
				END IF
				GOTO 800		!GO CALCULATE
			END IF
		ELSE					!SAAB LOOKS GOOD
			IF (BJTEST(CONTRL(I),1))THEN	!ARE WE ON AMPS?
				FTIMER(I)=FTIMER(I)+1	!YES HAS SAAB BEEN
				IF (FTIMER(I) .GE. 15)THEN	!OK FOR 30 SECS
					CONTRL(I)=JIBCLR(CONTRL(I),1)	!YES
					FTIMER(I)=0	!GO BACK TO BED LVL
				END IF
				GOTO 800		!GO CALCULATE
			ELSE
				FTIMER(I)=0		!RESET TIMER
				GOTO 800		!GO CALCULATE
			END IF
		END IF
C
800		CONTINUE
C
		IF (BJTEST(CONTRL(I),1))THEN	!USE BED LVL OR AMPS
			TEMP=AMPPRN(I)-AMPSET(I)
			IF (ABS(TEMP) .LT. AMPEDB(I))TEMP=0	!NO ERROR
			CHECK=(TEMP*AMPI(I))+AMPBAS(I)          
			CALL HILO(CHECK,AMPHI(I),AMPLO(I))
C			AMPBAS(I)=CHECK
C
			TEMP=TEMP*AMPP(I)
			CHECK=((AMPPRN(I)-AMPPRO(I))*AMPD(I))+TEMP+AMPBAS(I)
			CALL HILO(CHECK,AMPHI(I),AMPLO(I))
C			SPDSET(I)=CHECK			!NEW SPEED SETPOINT
C			LVOBAS(I)=AMPBAS(I)
		ELSE
			TEMP=LVLPRN(I)-PRGLSP(I)
			IF (ABS(TEMP) .LT. LVLEDB(I))TEMP=0	!NO ERROR
			CHECK=(TEMP*LVSPI(I))+LVOBAS(I)         
			CALL HILO(CHECK,LVLHI(I),LVLLO(I))
			LVOBAS(I)=CHECK
C
			TEMP=TEMP*LVSPP(I)
			CHECK=((LVLPRO(I)-LVLPRN(I))*LVSPD(I))+TEMP+LVOBAS(I)
			CALL HILO(CHECK,LVLHI(I),LVLLO(I))
			SPDSET(I)=CHECK			!NEW SPEED SETPOINT
			AMPBAS(I)=LVOBAS(I)
		END IF
C
C	SPEED SETPOINT TO OUTPUT IS STANDARD PID
C
		TEMP=SPDSET(I)-SPDPRN(I)
		CHECK=(TEMP*SPOUTI(I))+OUTBAS(I)
		CALL HILO(CHECK,SPDHI(I),SPDLO(I))
		OUTBAS(I)=CHECK
		TEMP=TEMP*SPOUTP(I)
		CHECK=((SPDPRO(I)-SPDPRN(I))*SPOUTD(I))+TEMP+OUTBAS(I)
		CALL HILO(CHECK,SPDHI(I),SPDLO(I))
		OUTPUT(I)=CHECK
C
900		CONTINUE
C
		LVLPRO(I)=LVLPRN(I)	!SAVE COOLER BED LVL LAST SCAN
		SPDPRO(I)=SPDPRN(I)	!SAVE COOLER SPEED LAST SCAN
		AMPPRO(I)=AMPPRN(I)	!SAVE COOLER AMPS LAST SCAN
C
		status = SHC_get_digital_val_stat(CTRLCX(I),gp_sid,gp_bitnum,
	1	digital_val, digital_stat)	!SEE IF COOLER ON COMP
C
		IF (digital_val .EQ. 0)GOTO 1000 !GO HERE IF OFF CONTROL
C
950		CONTINUE
C
		IF (LAST(I) .NE. LVLPRN(I))THEN	!SAAB HAS CHANGED
			BAD(I)=0	!RESET FROZEN COUNTER	
			GOOD(I)=GOOD(I)+1	!COUNT GOOD SCANS
			IF (GOOD(I) .GE. 7)THEN!GOOD FOR 14 SECS
				put_digital_val=0	! RESET SAAB ALARM
				status = SHC_put_point_quality(DO(I), 
	1					point_quality)
				status = SHC_put_digital_val( DO(I), 
	1					put_digital_val)
C
				CONTRL(I)=JIBCLR(CONTRL(I),2)	
				FTIMER(I)=15
				GOOD(I)=0
			END IF
		ELSE
			GOOD(I)=0	!RESET GOOD COUNTER	
			BAD(I)=BAD(I)+1	!COUNT BAD SCANS
			IF (BAD(I) .GE. 30)THEN	!BAD FOR 60 SECS
				put_digital_val=1	!ALARM CONTROL ROOM
				status = SHC_put_point_quality(DO(I), 
	1					point_quality)
				status = SHC_put_digital_val( DO(I), 
	1					put_digital_val)
C
				CONTRL(I)=JIBSET(CONTRL(I),2)	!SAAB FROZEN
				CONTRL(I)=JIBSET(CONTRL(I),1)	!USE AMPS
				BAD(I)=0
			END IF
		END IF
C
1000	CONTINUE
C
C	MAKE ALL PROCESS OUTPUTS (1 DO AND 2 AO'S)
C
	DO 2100	I=1,10
C
		TIMEOUT=IIBITS(AS(I),15,1)	!EXTRACT TIMED OUT BIT
C
		IF (TIMEOUT .EQ. 1)THEN		!IF ANY POINT IS TIMED OUT
C
			CALL	LIB$DATE_TIME(DATETIME)
C
			OPEN	(UNIT=22,FILE='CTL:COOLER3.ERR',
	1		STATUS='UNKNOWN',
	1		 ACCESS='APPEND')
C
			WRITE	(22,78)DATETIME
78			FORMAT	(' TIMED OUT POINTS DETECTED   ',A)
C
			CLOSE	(UNIT=22)
C
	   		GO TO 3000
C
		END IF
C
2100	CONTINUE
C
	IF (put_digital_val .EQ. 1)THEN	!PERFORM COMP ACKNOW USING DO
		put_digital_val=0	!IF RELAY IS OPEN THEN CLOSE IT
	ELSE
		put_digital_val=1	!IF RELAY IS CLOSED THEN OPEN IT
	END IF
C
	status = SHC_put_point_quality(DO(3), point_quality)
	status = SHC_put_digital_val( DO(3), put_digital_val)
C
	put_analog_val=OUTPUT(1)		!LINE 6 COOLER SPEED
	status = SHC_put_point_quality(AO(1), point_quality)
	status = SHC_put_analog_val(AO(1), put_analog_val)
C
	put_analog_val=OUTPUT(2)		!LINE 7 COOLER SPEED
	status = SHC_put_point_quality(AO(2), point_quality)
	status = SHC_put_analog_val(AO(2), put_analog_val)
C
3000	CONTINUE
C
	CRTLSP(1)=2.38
	CRTLSP(2)=2.38
C
	WRITE	(1,REC=1)CONTRL,PRGLSP,LVLPRN,LVOBAS,
	1		 SPDSET,SPDPRN,OUTBAS,
	1		 OUTPUT,LVSPI,
	1		 LVSPD,LVSPP,SPOUTI,SPOUTD,
	1		 SPOUTP,LVLHI,LVLLO,SPDHI,
	1		 SPDLO,CNTLSP,CRTLSP,LVLPRO,
	1		 SPDPRO,FAIL,AMPI,AMPP,AMPD,
	1		 AMPSET,AMPHI,AMPLO,
	1		 AMPBAS,AMPPRN,AMPPRO,AMPEDB,LVLEDB
C
	CLOSE	(UNIT=1)
C
	CALL	SYS$HIBER()
	GOTO 100
C
99999	CONTINUE
C
	CALL	EXIT
C
	END
C
	SUBROUTINE	CHKGPT(POINT,BIT)
C
C	SUBROUTINE WILL RETURN GOODPT STATUS OF SELECTED ANALOG
C
	INTEGER*4	BIT,POINT,I,J,GPT(40)
C
	COMMON/AG3_GOODPTTBL/GPT
C
	I=(POINT/32)+1		!WORD IN TABLE
	J=MOD(POINT,32)		!BIT IN WORD
	BIT=JIBITS(GPT(I),J,1)	!EXTRACT BIT
	RETURN
	END
C
	SUBROUTINE	HILO(VALUE,HI,LO)
C
C	SUBROUTINE WILL DO HI/LO LIMIT CHECKING FOR CALCULATED VALUES
C
	REAL*4		VALUE,HI,LO
C
	IF (VALUE .GT. HI)VALUE=HI
	IF (VALUE .LT. LO)VALUE=LO
	RETURN
	END
