	PROGRAM FANCTL3 
C
C	(FAN CONTROL STEP 3 AGGLOMERATOR)
C
C	THIS PROGRAM RUNS EVERY 5 SECONDS AND CONTROLS THE DAMPER
C	POSITIONS OF THE PREHEAT FANS FOR LINES 6 AND 7 AGGLOMERATOR
C
C	THIS PROGRAM WAS REVISED TO ADD LOGIC FOR CLOSING THE FAN
C	DAMPERS WHEN THE SCRUBBER TEMPERATURES BECOME TOO HIGH
C
C	FORT/NOOP/NOALIGN/WARN=NOALIGN/FLOAT=D_FLOAT FANCTL3
C
C	LINK FANCTL3,FANCTL3/OPT,WESAPI_LIB:WESAPI/OPT
C
	IMPLICIT NONE
C
	INCLUDE 'WESAPI:SPD.DCL'
	INCLUDE 'WESAPI:SHC_defines.DCL'
	INCLUDE 'WESAPI:SHC_err.DCL'
C
	INTEGER*4	LIB$SYS_TRNLOG
	INTEGER*4 	status
	CHARACTER*20	highway

	CHARACTER*40    spd_filename
	INTEGER*2	spd_filename_len
	INTEGER*2	spd_fd
	INTEGER*2	access_type
	INTEGER*2	c_short_val
	CHARACTER*2	field_name
	CHARACTER*8	pt_name
	BYTE		extended_flag
	BYTE		gp_bit_num
	BYTE		inactive_flag
	BYTE		c_byte_val
	INTEGER*4	gp_sid
	INTEGER*2       get_pkd_group_val
	INTEGER*2       get_pkd_group_stat
	INTEGER*2       gp_force_stat
	INTEGER*2       gp_op_mask
	INTEGER*2       gp_val_mask
C
	REAL            put_analog_val
	INTEGER*2	PUT_DIGITAL_VAL
C
	INTEGER*4	AO(4),DO
	INTEGER*4	LEN,POINT_QUALITY
C
	CHARACTER*8	TIME/'0 0:0:10'/
C
	INTEGER*4	CONTRL(2),UFO_OPEN
C
	REAL*4		GT1XPN(4),PHFPPN(2),IT1XPN(4),FA1XPN(4),CTEMPN(4),
	1		GT1XPO(4),PHFPPO(2),IT1XPO(4),FA1XPO(4),CTEMPO(4),
	1		GTPSET(2),OBGT1X(4),GTPRES(2),GTGAIN(2),GTLO(2),
	1		GTHI(2),C1XBAS(4),CRES(2),CGAIN(2),CDERIV(2),
	1		CLO(2),CHI(2),PFPSET(2),OBFP1X(4),PFPRES(2),
	1		FPGAIN(2),PFPDER(2),PHFPLO(2),PHFPHI(2),INTSET(2),
	1		OBIT1X(4),INTRES(2),ITGAIN(2),INTDER(2),INTLO(2),
	1		INTHI(2),FASET(2),OBFA1X(4),FARES(2),FAGAIN(2),
	1		FADER(2),FALO(2),FAHI(2),ASETPT(2),LTPHPN(2),
	1		OBAS1X(4),OUT1X(4)
	REAL*4		ANVAL(1000)
	REAL*4		OB,NB,PN,PO,RESET,GAIN,DERIV,LO,HI,SET,
	1		BASES(4),OUTPTS(4),NEWC,COBAS,TEMP,THERK/0.04/,
	1		SAVE,OUTX,CUTBCK/20/,RSTART/0/,CUT/5/,
	1		SETSAV
C
	INTEGER*4	TEMPX
	INTEGER*4	GPT(40)
	INTEGER*4	I,J,K,L,M,N,ADDRESS(2),BIT,BITX
C
	INTEGER*4	ONOFCX(2)/832,833/,	!ON/OFF COMP CXS
	1		GRATCX(2)/349,356/,	!GRATE DRIVE CXS
	1		FN1ACX(2)/375,379/,	!FAN DRIVE CXS
	1		FN1BCX(2)/377,380/,	!FAN DRIVE CXS
	1		SETPTX(2)/554,555/,	!CONTROLLER SETPTS
	1		GT1AA(2)/160,320/,	!GRATE TEMPS
	1		GT1BA(2)/161,321/,	!GRATE TEMPS
	1		PHFPA(2)/84,253/,	!FURNACE PRESSURE
	1		FA1AA(2)/100,211/,	!FAN AMPS
	1		FA1BA(2)/150,328/,	!FAN AMPS
	1		IT1AA(2)/102,270/,	!INLET TEMPS
	1		IT1BA(2)/152,330/,	!INLET TEMPS
	1		LTPHA(2)/900,901/,	!TONS AT GRATE
	1		DP1AA(2)/457,478/,	!DAMPER POSITIONS
	1		DP1BA(2)/458,479/,	!DAMPER POSITIONS
	1		SCRUB(2)/844,846/	!SCRUBBER TEMP IS HIGH
C
	INTEGER*4	CNTRLX(2)
	REAL*4		FANPN(2),HDPN(2),FANPO(2),HDPO(2),
	1		FMWT(2),MWTSET(2),MWTRES(2),HAMPS(2),ASOB(2),
	1		FANSET(2),OBFAN(2),FANRES(2),FNGAIN(2),FNDER(2),
	1		FANLO(2),FANHI(2),
	1		HDSET(2),OBHD(2),HDRES(2),HDGAIN(2),HDDER(2),
	1		HDLO(2),HDHI(2),
	1		DPSET(2),OBDP(2),DPRES(2),DPGAIN(2),DPLO(2),DPHI(2),
	1		DPCALC(2),CONSTC(2),AHDSET(2),OBAS3A(2),OUT3A(2)
C
	REAL*4		ACCUM,OBS,TEMPSP
	REAL*4		BX(2)/65.0,55.0/
	REAL*4		MX(2)/0.25,0.20/
C
	CHARACTER*8	AOP(4)/'AV800013','AV800014','AV800015',
	1		       'AV800016'/
C
	CHARACTER*8	DOP/'DV800006'/
C
	COMMON/AG3_ANVALTBL/ANVAL
	COMMON/PHFANFILE3/CONTRL,GT1XPN,PHFPPN,IT1XPN,FA1XPN,
	1		CTEMPN,GT1XPO,PHFPPO,IT1XPO,FA1XPO,
	1		CTEMPO,GTPSET,OBGT1X,GTPRES,GTGAIN,
	1		GTLO,GTHI,C1XBAS,CRES,CGAIN,CDERIV,
	1		CLO,CHI,PFPSET,OBFP1X,PFPRES,FPGAIN,
	1		PFPDER,PHFPLO,PHFPHI,INTSET,OBIT1X,
	1		INTRES,ITGAIN,INTDER,INTLO,INTHI,
	1		FASET,OBFA1X,FARES,FAGAIN,FADER,FALO,
	1		FAHI,ASETPT,LTPHPN,OBAS1X,OUT1X
C
	COMMON/GOODPTTBL/GPT
C
	EXTERNAL	UFO_OPEN
C
5	CONTINUE
C
C	OPEN CONNECTION TO AGGL 3 POINT DIRECTORY
C
        status = LIB$SYS_TRNLOG('WESAPI_PDIR_1',
     1			spd_filename_len,spd_filename,,,)
C
	spd_filename(spd_filename_len+1:) = CHAR(0)
C
        access_type = RUNTIME
C
        spd_fd = SPD_open_file(%ref(spd_filename), access_type)
C
	DO 15	J=1,4	!GET THE 4 AGGL 3 ANALOG OUTPUT SIDS
C
	        status = SPD_get_sid(spd_fd,%ref(AOP(J)//char(0)),AO(J),
     x		             gp_sid, gp_bit_num, extended_flag, inactive_flag)

C
15	CONTINUE
C
C		GET COMPUTER ACKNOWLEDGE DO
C
	status = SPD_get_sid(spd_fd,%ref(DOP//char(0)),DO,
	1		gp_sid, gp_bit_num, extended_flag, inactive_flag)

C
C  Close the point directory connection
C
	status = SPD_close_file(spd_fd)
C
C  Open the connection to SHC memory for WESAPI access
	status = SHC_open_memory()
C
	IF (status .NE. SHC_OK) THEN
		IF (status .EQ. SHC_E_MEMOPEN)GOTO 88
C
		OPEN	(UNIT=22,FILE='CTL:FANCTL3.ERR',STATUS='UNKNOWN',
	1		 ACCESS='APPEND')
C
		WRITE	(22,77)status
77		FORMAT	(' SHC open memory failure - error =',I4)
C
		CLOSE	(UNIT=22)
C
	   GO TO 99999
C
	ENDIF
C
88	CONTINUE
C
	OPEN	(UNIT=1,FILE='CTL:PHFAN3.DAT',STATUS='OLD',
	1	RECL=126,RECORDTYPE='FIXED',FORM='UNFORMATTED',
	1	SHARED,ACCESS='DIRECT',USEROPEN=UFO_OPEN)
C
	READ	(1,REC=1)CONTRL,GT1XPN,PHFPPN,IT1XPN,FA1XPN,
	1		CTEMPN,GT1XPO,PHFPPO,IT1XPO,FA1XPO,
	1		CTEMPO,GTPSET,OBGT1X,GTPRES,GTGAIN,
	1		GTLO,GTHI,C1XBAS,CRES,CGAIN,CDERIV,
	1		CLO,CHI,PFPSET,OBFP1X,PFPRES,FPGAIN,
	1		PFPDER,PHFPLO,PHFPHI,INTSET,OBIT1X,
	1		INTRES,ITGAIN,INTDER,INTLO,INTHI,
	1		FASET,OBFA1X,FARES,FAGAIN,FADER,FALO,
	1		FAHI,ASETPT,LTPHPN,OBAS1X,OUT1X
C
	CLOSE	(UNIT=1)
C
	CALL	SYS$BINTIM(TIME,ADDRESS)
	CALL	SYS$SCHDWK(,,ADDRESS,ADDRESS)
C
100	CONTINUE
C
	DO 2000	I=1,2		!LINE INDEX 1=L6...2=L7
		CALL	CHKCX(ONOFCX(I),BIT)	!SEE IF ON COMP
		IF (BIT .EQ. 1)THEN
			CONTRL(I)=JIBCLR(CONTRL(I),23)	!ON COMP
		ELSE
			CONTRL(I)=JIBSET(CONTRL(I),23)	!OFF COMP
		END IF
C
		GTPSET(I)=ANVAL(SETPTX(I))	!GET CNTRLLR SETPNT
		IF ((GTPSET(I) .LT. 850).OR.(GTPSET(I) .GT. 1000))THEN
			CONTRL(I)=JIBSET(CONTRL(I),8)	!SETPNT IS BAD
		ELSE
			CONTRL(I)=JIBCLR(CONTRL(I),8)	!SETPNT IS GOOD
		END IF
C
		GT1XPO(I)=GT1XPN(I)		!SWAP NEW AND OLD 
		GT1XPO(I+2)=GT1XPN(I+2)		!PROCESS READINGS
		PHFPPO(I)=PHFPPN(I)
		FA1XPO(I)=FA1XPN(I)
		FA1XPO(I+2)=FA1XPN(I+2)
		IT1XPO(I)=IT1XPN(I)
  		IT1XPO(I+2)=IT1XPN(I+2)
		CTEMPO(I)=CTEMPN(I)
		CTEMPO(I+2)=CTEMPN(I+2)
C
		GT1XPN(I)=ANVAL(GT1AA(I))	!READ NEW PROCESS VALUES
		GT1XPN(I+2)=ANVAL(GT1BA(I))
		PHFPPN(I)=ANVAL(PHFPA(I))
		FA1XPN(I)=ANVAL(FA1AA(I))
		FA1XPN(I+2)=ANVAL(FA1BA(I))	
		IT1XPN(I)=ANVAL(IT1AA(I))
		IT1XPN(I+2)=ANVAL(IT1BA(I))
		LTPHPN(I)=ANVAL(LTPHA(I))
C
		CALL	CHKCX(GRATCX(I),BIT)	!CHECK IF GRATE RUNNING
		IF (BIT .EQ. 1) THEN
			CONTRL(I)=JIBCLR(CONTRL(I),22)!GRATE ON	
		ELSE	
			CONTRL(I)=JIBSET(CONTRL(I),22)!GRATE OFF
		END IF
C
		BIT=JIBITS(CONTRL(I),23,1)	!IS LINE OFF CONTROL
		IF (BIT .EQ. 1)CONTRL(I)=JIBSET(CONTRL(I),21)	!YES...
C				USE DAMPER POSITIONS WHEN WE COME ON

		OBAS1X(I)=ANVAL(DP1AA(I))	!LOAD DAMPER POSITIONS
		OUT1X(I)=ANVAL(DP1AA(I))
		OBAS1X(I+2)=ANVAL(DP1BA(I))
		OUT1X(I+2)=ANVAL(DP1BA(I))
C
	DO 500 J=1,2
		K=(J-1)*2+I
		TEMP=GT1XPN(K)-GT1XPO(K)	!PERFORM THERMOCOUPLE CALC
		TEMP=TEMP*TEMP			!ON GRATE TEMPERATURES
		IF (GT1XPN(K) .GT. GT1XPO(K))THEN
			CTEMPN(K)=GT1XPN(K)+(TEMP*THERK)
		ELSE
			CTEMPN(K)=GT1XPN(K)-(TEMP*THERK)
		END IF
500 	CONTINUE
C
		CALL	CHKCX(FN1ACX(I),BIT)	!SEE IF 1A FAN RUNNING
		IF (BIT .EQ. 1)THEN
			CONTRL(I)=JIBCLR(CONTRL(I),11)
		ELSE
			CONTRL(I)=JIBSET(CONTRL(I),11)
		END IF
C
		CALL	CHKCX(FN1BCX(I),BIT)	!SEE IF 1B FAN RUNNING
		IF (BIT .EQ. 1)THEN
			CONTRL(I)=JIBCLR(CONTRL(I),12)
		ELSE	
			CONTRL(I)=JIBSET(CONTRL(I),12)
		END IF
C
		CALL	CHKCX(SCRUB(I),BIT)	!SEE IF SCRUBBER TEMP IS HI
		IF (BIT .EQ. 1)THEN !YES SCRUB TEMP IS HIGH
			CONTRL(I)=JIBSET(CONTRL(I),24)
		ELSE	
			CONTRL(I)=JIBCLR(CONTRL(I),24) !NO SCRUBBER TEMP OK
		END IF
C
		DO 550 N=1,8
			CONTRL(I)=JIBCLR(CONTRL(I),N-1)	!CLEAR OUTPUT INDIC BITS
550		CONTINUE
C
	DO 1000 J=1,2
		K=(J-1)*2+I
		BIT=JIBITS(CONTRL(I),23,1)	!ARE WE OFF COMPUTER
		IF (BIT .EQ. 1)GOTO 1000	!YES SKIP CALC
C
		BIT=JIBITS(CONTRL(I),22,1)	!IS GRATE OFF
600		CONTINUE
C
		IF (BIT .EQ. 1)THEN
			OUT1X(K)=OUT1X(K)-CUTBCK	!YES CLOSE DAMPER
			IF (OUT1X(K) .LT. 0)OUT1X(K)=0	!UNTIL ZERO
			OBGT1X(K)=RSTART	!RESET BASES
			OBIT1X(K)=RSTART
			OBFA1X(K)=RSTART
			OBFP1X(K)=RSTART
			GOTO 1000
		END IF
C
		BIT=JIBITS(CONTRL(I),J+10,1)	!IS FAN RUNNING
		IF (BIT .EQ. 1)THEN
			GOTO 600		!NO GO CLOSE DAMPER
		END IF
C
700		CONTINUE
C
		BIT=JIBITS(CONTRL(I),24,1)	!IS SCRUB TEMP HI
		IF (BIT .EQ. 1)THEN
			GOTO 600		!YES GO CLOSE DAMPER
		END IF
C
		IF (LTPHPN(I) .LT.200)THEN	!IF TONS < 200 
			OUT1X(K)=OUT1X(K)-CUT	!CUT OUTPUT BY 5%
			IF (OUT1X(K) .LT. 0)OUT1X(K)=0
			GOTO 1000
		END IF
C
C	HERE WE CLACULATE PID EQUATION FOR CONSTANT C
C
		BIT=JIBITS(CONTRL(I),21,1)	!IS BASE USABLE
		BITX=BIT			!SAVE INDICATION
		IF(BIT. EQ. 1)THEN		!OR DO WE USE DAMPER POS
			OB=(MX(I)*LTPHPN(I)-BX(I))-OBAS1X(K)
		ELSE
			OB=C1XBAS(K)
		END IF
C
		BIT=JIBITS(CONTRL(I),8,1)	!CHECK WHICH TEMP
		IF (BIT .EQ. 1) THEN		!SETPOINT TO USE
			SET=ASETPT(I)
		ELSE
			SET=GTPSET(I)
		END IF
C
		PN=CTEMPN(K)
		RESET=CRES(I)
		LO=CLO(I)
		HI=CHI(I)
C
		CALL	NBASE(OB,SET,PN,RESET,NB,HI,LO)	!CALC NEW BASE
C
		BASES(1)=NB
		COBAS=NB
C
		IF (BITX .EQ. 0)C1XBAS(K)=COBAS
C
		PN=CTEMPN(K)
		PO=CTEMPO(K)
C
		GAIN=CGAIN(I)
C
		DERIV=CDERIV(I)
C
		CALL	NOUTPT(NB,SET,PN,PO,GAIN,DERIV,OUTX,HI,LO)
C		          CALCULATE NEW OUTPUT
		NEWC=OUTX	!NEW OUTPUT FOR CONSTANT C
C
C	CALCULATE FURNACE PRESSURE BASE 
C
		SET=PFPSET(I)
		PN=PHFPPN(I)
		RESET=PFPRES(I)
C
		IF (BITX .EQ. 1)THEN
			OB=OBAS1X(K)
		ELSE	
			OB=OBFP1X(K)
		END IF
C
		LO=PHFPLO(I)
		HI=PHFPHI(I)
		CALL	NBASE(OB,SET,PN,RESET,NB,HI,LO)
		BASES(2)=NB
C
C	CALCULATE INLET TEMPERATURE BASE
C
		SET=INTSET(I)
		PN=IT1XPN(K)
		RESET=INTRES(I)
C
		IF (BITX .EQ. 1)THEN
			OB=OBAS1X(K)
		ELSE
			OB=OBIT1X(K)
		END IF
C
		LO=INTLO(I)
		HI=INTHI(I)
		CALL	NBASE(OB,SET,PN,RESET,NB,HI,LO)
		BASES(3)=NB
C
C	CALCULATE FAN AMPS BASE
C
		SET=FASET(I)
		RESET=FARES(I)
		PN=FA1XPN(K)
C
		IF (BITX .EQ. 1)THEN
			OB=OBAS1X(K)
		ELSE
			OB=OBFA1X(K)
		END IF
C
		LO=FALO(I)
		HI=FAHI(I)
		CALL	NBASE(OB,SET,PN,RESET,NB,HI,LO)
		BASES(4)=NB
C
C	CALCULATE REMAINDER OF GRATE TEMPERATURE OUTPUT
C
		TEMP=((LTPHPN(I)*MX(I))-BX(I))-NEWC-OBAS1X(K)
		SAVE=TEMP*GTPRES(I)
C
		IF (BITX .EQ. 1)THEN
			OB=OBAS1X(K)
		ELSE
			OB=OBGT1X(K)
		END IF
C
		BASES(1)=OB+SAVE
		IF (BASES(1) .GT. GTHI(I)) BASES(1)=GTHI(I)
		IF (BASES(1) .LT. GTLO(I)) BASES(1)=GTLO(I)
		OUTPTS(1)=GTGAIN(I)*TEMP+BASES(1)
		IF (OUTPTS(1) .GT. GTHI(I)) OUTPTS(1)=GTHI(I)
		IF (OUTPTS(1) .LT. GTLO(I)) OUTPTS(1)=GTLO(I)	
C
C	CALCULATE FURNACE PRESSURE OUTPUT
C
		PN=PHFPPN(I)
		PO=PHFPPO(I)
		SET=PFPSET(I)
		GAIN=FPGAIN(I)
		DERIV=PFPDER(I)
		NB=BASES(2)
		LO=PHFPLO(I)
		HI=PHFPHI(I)
		CALL	NOUTPT(NB,SET,PN,PO,GAIN,DERIV,OUTX,HI,LO)
		OUTPTS(2)=OUTX
C
C	CALCULATE INLET TEMPERATURE OUTPUT
C
		PN=IT1XPN(K)
		PO=IT1XPO(K)
		SET=INTSET(I)
		GAIN=ITGAIN(I)
		DERIV=INTDER(I)
		NB=BASES(3)
		LO=INTLO(I)
		HI=INTHI(I)
		CALL	NOUTPT(NB,SET,PN,PO,GAIN,DERIV,OUTX,HI,LO)
		OUTPTS(3)=OUTX
C
C	CALCULATE FAN AMPS OUTPUT
C
		PN=FA1XPN(K)
		PO=FA1XPO(K)
		SET=FASET(I)
		GAIN=FAGAIN(I)
		DERIV=FADER(I)
		NB=BASES(4)
		LO=FALO(I)
		HI=FAHI(I)
		CALL	NOUTPT(NB,SET,PN,PO,GAIN,DERIV,OUTX,HI,LO)
		OUTPTS(4)=OUTX
C
C	LOCATE THE LOWEST OF THE FOUR OUTPUTS
C
		TEMP=AMIN1(OUTPTS(1),OUTPTS(2),OUTPTS(3),OUTPTS(4))
		DO 800 L=1,4
			IF (TEMP .EQ. OUTPTS(L))GOTO 900
800		CONTINUE
900		CONTINUE
C
		OUT1X(K)=OUTPTS(L)	!OUTPUT THIS VALUE TO DAMPER
		M=((J-1)*4+L)-1
		CONTRL(I)=JIBSET(CONTRL(I),M)	!INDICATE INPUT RESP.
		OBGT1X(K)=BASES(L)	!RESET BASES FOR ALL LOOPS
		OBFP1X(K)=BASES(L)
		OBIT1X(K)=BASES(L)
		OBFA1X(K)=BASES(L)
		IF (L .NE. 1)GOTO 1000	!IF NOT CONTROLLING FROM GRATE TEMP
		IF (OUT1X(K) .LT. GTHI(I))C1XBAS(K)=COBAS
C
1000	CONTINUE
		BIT=JIBITS(CONTRL(I),23,1)
		IF (BIT .EQ. 0)CONTRL(I)=JIBCLR(CONTRL(I),21)
C
2000	CONTINUE
C
	IF (put_digital_val .EQ. 1)THEN	!PERFORM COMPUTER ACKNOWLEDGE
		put_digital_val=0		!IF RELAY OPEN THEN CLOSE IT
	ELSE
		put_digital_val=1		!IF RELAY CLOSED THEN OPEN IT
	END IF
C
	status = SHC_put_point_quality(DO, point_quality)
	status = SHC_put_digital_val( DO, put_digital_val)
C
4000	CONTINUE
C
	put_analog_val=OUT1X(1)	!LINE 4 1A DAMPER POSITION
	status = SHC_put_point_quality(AO(1), point_quality)
	status = SHC_put_analog_val(AO(1), put_analog_val)
C
	put_analog_val=OUT1X(3)	!LINE 4 1B DAMPER POSITION
	status = SHC_put_point_quality(AO(2), point_quality)
	status = SHC_put_analog_val(AO(2), put_analog_val)
C
	put_analog_val=OUT1X(2)	!LINE 5 1A DAMPER POSITION
	status = SHC_put_point_quality(AO(3), point_quality)
	status = SHC_put_analog_val(AO(3), put_analog_val)
C
	put_analog_val=OUT1X(4)	!LINE 5 1B DAMPER POSITION
	status = SHC_put_point_quality(AO(4), point_quality)
	status = SHC_put_analog_val(AO(4), put_analog_val)
C
5000	CONTINUE
C
	CALL	SYS$HIBER()
	GOTO 100
C
99999	CONTINUE
C
	CALL	EXIT
C
	END
C
	SUBROUTINE	CHKCX(POINT,BIT)
C
C	SUBROUTINE WILL RETURN THE STATUS OF A CONTACT (0 OR 1)
C
	INTEGER*4	BIT,POINT,I,J,CXSTS(100)
C
	COMMON/CONTACTSAG3/CXSTS
C
	I=(POINT/32)+1		!WORD IN TABLE
	J=MOD(POINT,32)		!BIT IN WORD
	BIT=JIBITS(CXSTS(I),J,1)!EXTRACT BIT
	RETURN
	END
C
	SUBROUTINE	CHKGPT(POINT,BIT)
C
C	SUBROUTINE WILL RETURN GOODPT STATUS OF SELECTED ANALOG
C
	INTEGER*4	BIT,POINT,I,J,GPT(40)
C
	COMMON/AG3_GOODPTTBL/GPT
C
	I=(POINT/32)+1		!WORD IN TABLE
	J=MOD(POINT,32)		!BIT IN WORD
	BIT=JIBITS(GPT(I),J,1)	!EXTRACT BIT
	RETURN
	END
C
	SUBROUTINE	NBASE(OB,SET,PN,RESET,NB,HI,LO)
C
C	SUBROUTINE WILL CALCULATE NEW BASE FOR PROCESS VARIABLE
C
	IMPLICIT NONE
C
	REAL*4		OB,SET,PN,RESET,NB,HI,LO
C
	NB=(SET-PN)*RESET+OB
  	IF (NB .GT. HI)NB=HI
	IF (NB .LT. LO)NB=LO
C
	RETURN
	END
C
	SUBROUTINE	NOUTPT(NB,SET,PN,PO,GAIN,DERIV,OUTX,HI,LO)
C
C	SUBROUTINE WILL CALCULATE NEW OUTPUT FOR PROCESS VARIABLE
C
	IMPLICIT NONE
C
	REAL*4		NB,SET,PN,PO,GAIN,DERIV,OUTX,HI,LO
C
	OUTX=((SET-PN)*GAIN)+((PO-PN)*DERIV)+NB
	IF (OUTX .GT. HI)OUTX=HI
	IF (OUTX .LT. LO)OUTX=LO
C
	RETURN
	END
