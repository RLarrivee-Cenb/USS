C
	PROGRAM		TARGET
C
C	THIS PROGRAM RUNS EVERY 12 MINUTES AND MANAGES THE SIO2
C	TARGET CALCULATIONS FOR THE STEP 2 AND STEP 3 AGLLOMERATORS
C
C	FORT/NOOP/NOALIGN/WARN=NOALIGN/FLOAT=D_FLOAT TARGET
C
C	LINK TARGET,TARGET/OPT,WESAPI_LIB:WESAPI/OPT
C
	IMPLICIT NONE
C
	INCLUDE '($SSDEF)'
	INCLUDE '($IODEF)'
C
	INCLUDE 'WESAPI:SPD.DCL'
	INCLUDE 'WESAPI:SHC_defines.DCL'
	INCLUDE 'WESAPI:SHC_err.DCL'
C
	INTEGER*4	LIB$SYS_TRNLOG
	INTEGER*4 	status
	CHARACTER*20	highway

	CHARACTER*2	field_name
	BYTE		c_byte_val
	CHARACTER*40    spd_filename
	INTEGER*2	spd_filename_len
	INTEGER*2	spd_fd
	INTEGER*2	access_type
	CHARACTER*8	pt_name
	BYTE		extended_flag
	BYTE		gp_bit_num
	BYTE		inactive_flag
	BYTE		FM
	INTEGER*4	gp_sid
C
	CHARACTER*8	PNT(12)/'AD400064','AD400065','AD400066',
	1		       'AD400067','AD400068','AD400069',
	1		       'AD400070','AD400071','AD400094',
	1		       'AD400095','AD400096','AD400097'/
C
	REAL            put_analog_val
C
C	INTEGER*2	AS(8)
C
	INTEGER*4	SID(12),LEN,POINT_QUALITY
C
	STRUCTURE	/TS/
C
		CHARACTER*23	DATX	!DATE AND TIME
		REAL*4		R10
		REAL*4		LP	
		REAL*4		FC
		REAL*4		FCT
		REAL*4		ACOMP
		REAL*4		T
		REAL*4		TF
		REAL*4		LINES
		REAL*4		APTON
		REAL*4		BENT
C
	END STRUCTURE
C
	RECORD/TS/TS(2),TSX
C
	REAL*4	PC,K1,K2,K3,K4
	REAL*4	CNC_ANVAL(1900),FCAKS(2),ANVAL(1200)
	REAL*4	TIM,DELAY,ACCUM,OBS,AVGS(2)
	REAL*4	S2(200),S2TPH(200),S3(200),S3TPH(200)
	REAL*4	ANVALX(1000),FFS
	REAL*4	BENT1,BENT3,TONS1,TONS3,BC
	REAL*4	FCTRJS(2)/3.70,3.75/
C
	REAL*8		BT2(9),BT3(9),FLF(2),TX2,TX3
C
	INTEGER*4	I,J,K,L,N,M
	INTEGER*4	TIMX,UFO_OPEN
	INTEGER*4	ADDRESS(2),MIN
	INTEGER*4	T170(2)/1171,1172/
	INTEGER*4	SCOMP(2)/1305,1306/
	INTEGER*4	AO(2)/64,65/
	INTEGER*4	AOFC(2)/66,67/,
	1		AOR10(2)/68,69/,
	1		AOLP(2)/70,71/,
	1		AOFT(2)/94,95/,
	1		AOFCT(2)/96,97/
	INTEGER*4	TARX(2)/1698,1699/
	INTEGER*4	CGPT(60)
C
	CHARACTER*16	FILEC(2)/'ANA:TARGETC2.DAT',
	1		     'ANA:TARGETC3.DAT'/
	CHARACTER*13	FILET(2)/'ANA:TSIL2.DAT',
	1		     'ANA:TSIL3.DAT'/
	CHARACTER*12	DATEX
	CHARACTER*23	DATZ
	CHARACTER*9	TIME/'0 0:12:00'/
	CHARACTER*14	FILE/'ANA:TARGET.DAT'/
	CHARACTER*17	LOGF/'LOGS:CNC_HOUR.DAT'/
C
	COMMON/AG3_ANVALTBL/ANVALX
	COMMON/CNC_ANVALTBL/CNC_ANVAL	!CONC ANALOG PULSE VALUE TABLE
	COMMON/ANVALTBL/ANVAL		!AGGL ANALOG PULSE VALUE TABLE
	COMMON/CNC_GOODPTTBL/CGPT
C
	EXTERNAL	UFO_OPEN
C
C  Use the wesapi highway number to form the name of the wesapi logical that
C  names the highway 0 point directory file and get file name from the logical
        status = LIB$SYS_TRNLOG('WESAPI_PDIR_4',
     1			spd_filename_len,spd_filename,,,)

C  Put a NULL character (0) at the end of the file name string.  This is
C  necessary because the spd library functions expect string arguments
C  to be NULL terminated strings.
C
	spd_filename(spd_filename_len+1:) = CHAR(0)

C  Call the spd open file function to get access to the point directory file
C
        access_type = RUNTIME
        spd_fd = SPD_open_file(%ref(spd_filename), access_type)
C
	DO 10	J=1,12	!GET THE 8 ANALOG OUTPUT SIDS  SIDS
C
C  Call the get sid function.  The point name argument must have a NULL (0)
C  at the end.
C
	        status = SPD_get_sid(spd_fd, %ref(PNT(J)//char(0)), SID(J),
     x		             gp_sid, gp_bit_num, extended_flag, inactive_flag)

C
10	CONTINUE
C
C  Close the point directory connection
C
	status = SPD_close_file(spd_fd)
C
C
C  Open the connection to SHC memory for WESAPI access
	status = SHC_open_memory()
C
	IF (status .NE. SHC_OK) THEN
		IF (status .EQ. SHC_E_MEMOPEN)GOTO 88
C
		OPEN	(UNIT=22,FILE='ANA:TARGET.ERR',STATUS='UNKNOWN',
	1		 ACCESS='APPEND')
C
		WRITE	(22,77)status
77		FORMAT	(' SHC open memory failure - error =',I4)
C
		CLOSE	(UNIT=22)
C
	   GO TO 99999
C
	ENDIF
C
88	CONTINUE
C
	AO(1)=SID(1)
	AO(2)=SID(2)
	AOFC(1)=SID(3)
	AOFC(2)=SID(4)
	AOR10(1)=SID(5)
	AOR10(2)=SID(6)
	AOLP(1)=SID(7)
	AOLP(2)=SID(8)
	AOFT(1)=SID(9)
	AOFT(2)=SID(10)
	AOFCT(1)=SID(11)
	AOFCT(2)=SID(12)
C
	DO 20 	I=1,2	!FIRST TIME THRU MAKE OUTPUTS
C
		OPEN	(UNIT=2,FILE=FILET(I),STATUS='OLD',SHARED,
	1		RECL=16,RECORDTYPE='FIXED',FORM='UNFORMATTED',
	1		ACCESS='DIRECT',USEROPEN=UFO_OPEN)
C
		READ	(2,REC=1)TSX	!READ A TSIL RECORD
C
		CLOSE	(UNIT=2)
C
		put_analog_val=TSX.T	!SEND NEW TARGET TO WDPF
		status = SHC_put_point_quality( AO(I), point_quality)
		status = SHC_put_analog_val( AO(I), put_analog_val)
C
		put_analog_val=TSX.FC	!SEND NEW F CAKE TO WDPF
		status = SHC_put_analog_val( AOFC(I), put_analog_val)
C
		put_analog_val=TSX.R10	!SEND NEW R10 TO WDPF
		status = SHC_put_analog_val( AOR10(I), put_analog_val)
C
		put_analog_val=TSX.LP	!SEND NEW LINE PEL TO WDPF
		status = SHC_put_analog_val( AOLP(I), put_analog_val)
C
		put_analog_val=TSX.TF	!SEND NEW FLOATING TARGET TO WDPF
		status = SHC_put_analog_val( AOFT(I), put_analog_val)
C
	c_byte_val=2
	field_name='FM'
	status = SHC_change_byte_attribute( AOFT(I), 
	1		%REF(field_name),c_byte_val)
C
		put_analog_val=TSX.FCT	!SEND NEW FILTER CAKE TARGET TO WDPF
		status = SHC_put_analog_val( AOFCT(I), put_analog_val)
C
	c_byte_val=2
	field_name='FM'
	status = SHC_change_byte_attribute( AOFCT(I), 
	1		%REF(field_name),c_byte_val)
C
20	CONTINUE
C
	TIM=SECNDS(0.0)	!GET TIME IN SECONDS
	TIMX=TIM	!CONVERT TO INTEGER
C
	DELAY=720-MOD(TIMX,720)	!SECONDS REMAINING TILL NEXT 12 MINUTES
C
	CALL	LIB$WAIT(DELAY)	!DELAY TILL NEXT 12 MINUTE TURN ON
C
	CALL	SYS$BINTIM(TIME,ADDRESS)
	CALL	SYS$SCHDWK(,,ADDRESS,ADDRESS)	!SCHEDULE WAKEUP IN 12 MINUTES
C
100	CONTINUE
C
	CALL	LIB$DATE_TIME(DATZ)
C
	OPEN	(UNIT=1,FILE=FILE,STATUS='OLD',SHARED,
	1	RECL=800,FORM='UNFORMATTED',RECORDTYPE='FIXED',
	1	ACCESS='DIRECT',USEROPEN=UFO_OPEN)
C
	READ	(1,REC=1)S2,S2TPH,S3,S3TPH	!BRING IN 12 MINUTE SAMPLES
C
	DO 200	I=199,1,-1
C
		S2(I+1)=S2(I)	!SHUFFLE TABLE DOWN TO MAKE ROOM FOR NEW
		S2TPH(I+1)=S2TPH(I)	!12 MINUTE ENTRIES
		S3(I+1)=S3(I)
		S3TPH(I+1)=S3TPH(I)
C
200	CONTINUE
C
	S2(1)=CNC_ANVAL(1305)	!S2 COMPOSITE SIO2
	S2TPH(1)=ANVAL(694)	!S2 170 LTPH  (IF FLUX THEN 1171 FROM CNC)
	S3(1)=CNC_ANVAL(1306)	!S3 COMPOSITE SIO2
	S3TPH(1)=ANVALX(572)	!S3 170 LTPH  (IF FLUX THEN 1172 FROM CNC)
C
	WRITE	(1,REC=1)S2,S2TPH,S3,S3TPH	!WRITE OUT 12 MIN SAMPLE FILE
C
	CLOSE	(UNIT=1)
C
C	HERE WE WILL BUILD THE RECORD FOR THE TSIL FILE ..........
C	A RECORD IS WRITTEN TO THIS FILE ONCE AN HOUR ON THE HOUR
C
	READ	(DATZ(16:17),'(I2)')MIN	!CONVERT CURRENT MINUTES TO INT
C
	OPEN	(UNIT=2,FILE='ANA:FCAKE.DAT',STATUS='NEW',
	1	RECL=2,RECORDTYPE='FIXED',FORM='UNFORMATTED',
	1	ACCESS='DIRECT',SHARED,USEROPEN=UFO_OPEN)
C
	READ	(2,REC=1)FCAKS	!BRING IN FILTER CAKE SIO2S
C
	CLOSE	(UNIT=2)
C
	OPEN	(UNIT=2,FILE='MET_P:WJBBTX.DAT',STATUS='OLD',
	1	SHARED,USEROPEN=UFO_OPEN)
C
	READ	(2,11)DATEX,BT2,BT3,FLF,TX2,TX3	!GET NUMBERS FROM MAKI
11	FORMAT	(A,20F7.2,2F7.1)
C
	CLOSE	(UNIT=2)
C
		put_analog_val=FCAKS(1)	!SEND NEW STEP 2 F CAKE TO WDPF
		status = SHC_put_analog_val( AOFC(1), put_analog_val)
C
		put_analog_val=FCAKS(2)	!SEND NEW STEP 3 F CAKE TO WDPF
		status = SHC_put_analog_val( AOFC(2), put_analog_val)
C
		IF (BT2(8) .NE. 0)THEN
		put_analog_val=BT2(8)	!SEND NEW STEP 2 LINE PEL TO WDPF
		status = SHC_put_analog_val( AOLP(1), put_analog_val)
		END IF
C
		IF (BT3(8) .NE. 0)THEN
		put_analog_val=BT3(8)	!SEND NEW STEP 3 LINE PEL TO WDPF
		status = SHC_put_analog_val( AOLP(2), put_analog_val)
		END IF
C
	IF (MIN .GE. 2)GOTO 9999	!THIS IS NOT ON THE HOUR
C
	OPEN	(UNIT=4,FILE='ANA:FFF.DAT',STATUS='OLD',
	1	RECL=1,RECORDTYPE='FIXED',FORM='UNFORMATTED',
	1	ACCESS='DIRECT',SHARED,USEROPEN=UFO_OPEN)
C
	DO 444	J=63,1,-1
C
		READ	(4,REC=J)FFS  	!SHUFFLE DOWN FLOAT FEED TABLE
		WRITE	(4,REC=J+1)FFS
C
444	CONTINUE
C
	FFS=CNC_ANVAL(1267)	!GRAB CURRENT FLOAT FEED SILICA
	WRITE	(4,REC=1)FFS	!PUT IT AT TOP OF TABLE
	CLOSE	(UNIT=4)
C
	TS(1).LP=BT2(8)	!STEP 2 AGGLOM LINE PELLET SIO2
C
	TS(2).LP=BT3(8)	!STEP 3 AGGLOM LINE PELLET SIO2
C
	BENT1=0
	BENT3=0
	TONS1=0
	TONS3=0
C
	DO 1000	I=1,2	!BUILD THE TSIL RECORD FOR EACH STEP
C
		TS(I).DATX=DATZ	!TIME OF RECORD
C
		IF (I .EQ. 1)THEN
C
		TS(I).LINES=ANVAL(694)/135	!NO. OF LINES TO STEP 1-2
C
		OPEN	(UNIT=2,FILE='LOGS:HOUR.DAT',FORM='UNFORMATTED',
	1	 	SHARED,ACCESS='DIRECT',STATUS='OLD',
	1	 	RECL=2,RECORDTYPE='FIXED',USEROPEN=UFO_OPEN)
C
		READ	(2,REC=876)ACCUM,OBS
		BENT1=BENT1+ACCUM	!LINE 3 BENTONITE
		READ	(2,REC=362)ACCUM,OBS
		BENT1=BENT1+ACCUM	!LINE 4 BENTONITE
		READ	(2,REC=363)ACCUM,OBS
		BENT1=BENT1+ACCUM	!LINE 5 BENTONITE
		READ	(2,REC=867)ACCUM,OBS
		TONS1=TONS1+ACCUM	!LINE 3 PELLETS
		READ	(2,REC=339)ACCUM,OBS
		TONS1=TONS1+ACCUM	!LINE 4 PELLETS
		READ	(2,REC=340)ACCUM,OBS
		TONS1=TONS1+ACCUM	!LINE 5 PELLETS
C
		CLOSE	(UNIT=2)
C
		IF (TONS1 .LE. 0)THEN
			TS(I).BENT=0
		ELSE
			TS(I).BENT=BENT1/TONS1 !BENT CONSUMPTION STEP 12
		END IF
C
		ELSE
C
		TS(I).LINES=ANVALX(572)/135 	!NO. OF LINES TO STEP 3
C
		OPEN	(UNIT=2,FILE='LOGS:AG3_HOUR.DAT',FORM='UNFORMATTED',
	1	 	SHARED,ACCESS='DIRECT',STATUS='OLD',
	1	 	RECL=2,RECORDTYPE='FIXED',USEROPEN=UFO_OPEN)
C
		READ	(2,REC=803)ACCUM,OBS
		BENT3=BENT3+ACCUM	!LINE 6 BENTONITE
		READ	(2,REC=804)ACCUM,OBS
		BENT3=BENT3+ACCUM	!LINE 7 BENTONITE
		READ	(2,REC=564)ACCUM,OBS
		TONS3=TONS3+ACCUM	!LINE 6 PELLETS
		READ	(2,REC=565)ACCUM,OBS
		TONS3=TONS3+ACCUM	!LINE 7 PELLETS
C
		CLOSE	(UNIT=2)
C
		IF (TONS3 .LE. 0)THEN
			TS(I).BENT=0
		ELSE
			TS(I).BENT=BENT3/TONS3 !BENT CONSUMPTION STEP 3
		END IF
C
		END IF
C
		OPEN	(UNIT=2,FILE=LOGF,STATUS='OLD',SHARED,
	1		RECL=2,FORM='UNFORMATTED',RECORDTYPE='FIXED',
	1		USEROPEN=UFO_OPEN,ACCESS='DIRECT')
C
		READ	(2,REC=SCOMP(I))ACCUM,OBS	!LAST HOUR SIO2 COMP
C
		IF (OBS .EQ. 0)THEN 
			TS(I).ACOMP=0
		ELSE
			TS(I).ACOMP=ACCUM/OBS	!LAST HOUR AVG STEP COMP SIO2
		END IF
C
		CLOSE	(UNIT=2)
C
		TS(I).FC=FCAKS(I)	!LAST FILTER CAKE INPUT FROM LAB
C
		TS(I).APTON=CNC_ANVAL(1386)	!AMINE LBS/TON TOTAL PLANT
C		TS(I).AEFF=CNC_ANVAL(1697)	!AMINE EFFICIENCY TOTAL PLANT
C
		AVGS(1)=0
		AVGS(2)=0
C
		DO 400	J=1,200	!CALCULATE R10 (AVG SIO2 LAST 10000 TONS)
C
			IF (I .EQ. 1)AVGS(2)=AVGS(2)+(S2TPH(J)/5) !SUM TONS
			IF (I .EQ. 2)AVGS(2)=AVGS(2)+(S3TPH(J)/5) !SUM TONS
C
			IF (I .EQ. 1)AVGS(1)=AVGS(1)+(S2(J)*(S2TPH(J)/5)) !SIO2
			IF (I .EQ. 2)AVGS(1)=AVGS(1)+(S3(J)*(S3TPH(J)/5)) !SIO2
C
			IF (AVGS(2) .GE. 10000)GOTO 500	!WE HIT 10000 TONS
C
400		CONTINUE
C
500		CONTINUE
C
		IF (AVGS(2) .EQ. 0)THEN
			TS(I).R10=0
		ELSE
			TS(I).R10=AVGS(1)/AVGS(2)	!R10 FOR THIS STEP
		END IF
C
		OPEN	(UNIT=2,FILE=FILEC(I),STATUS='OLD',SHARED,
	1		RECL=5,RECORDTYPE='FIXED',FORM='UNFORMATTED',
	1		ACCESS='DIRECT',USEROPEN=UFO_OPEN)
C
		READ	(2,REC=1)PC,K1,K2,K3,K4	!TARGET CONSTANTS
C
		CLOSE	(UNIT=2)
C
		OPEN	(UNIT=2,FILE=FILET(I),STATUS='OLD',SHARED,
	1		RECL=16,RECORDTYPE='FIXED',FORM='UNFORMATTED',
	1		ACCESS='DIRECT',USEROPEN=UFO_OPEN)
C
		AVGS(1)=0
		AVGS(2)=0
C
		DO 600	J=1,63
C
			READ	(2,REC=J)TSX	!READ A TSIL RECORD
C
			IF (TSX.LP .GT. 6.0)GOTO 600
C
			IF (TSX.LP .NE. 0)THEN
				AVGS(1)=AVGS(1)+(TSX.LP-TSX.FC)
				AVGS(2)=AVGS(2)+1
			END IF
C
600		CONTINUE
C
		IF (AVGS(2) .EQ. 0)THEN
			AVGS(1)=0
		ELSE
			AVGS(1)=AVGS(1)/AVGS(2)	!AVG DIFF LP - FC
		END IF
C
		TS(I).FCT=PC-AVGS(1)	!FILTER CAKE TARGET
C		TS(I).FCT=FCTRJS(I)	!FIX FCT PER BOB STRUKEL 2-25-97
C
		READ	(2,REC=1)TSX	!BRING IN LAST HOUR
C
		IF (TSX.TF .EQ. 0)TSX.TF=3.60	!INITIALIZE FLOATING TARGET
C
		TS(I).TF=TSX.TF+
	1	K1*(TS(I).R10-TSX.TF-TS(I).FC+TS(I).FCT) !FLOATING TARGET
C
		IF (TS(I).BENT .GT. 25)TS(I).BENT=0
C
		IF ((TSX.BENT .EQ. 0).OR.(TS(I).BENT .EQ. 0))THEN
			BC=0
		ELSE
			BC=(TSX.BENT-TS(I).BENT)*0.031
		END IF
C
		TS(I).TF=TS(I).TF+BC	!MAKE BENT ADJUSTMENT
C
		IF (TS(I).LP .EQ. 0)TS(I).LP=TSX.LP !IF PEL SIO2 ZERO USE LAST
C
		TS(I).T=TS(I).TF+((TSX.T-TS(I).R10)*K2)
	1		+((TS(I).FCT-TS(I).FC)*K3)
	1		+((PC-TS(I).LP)*K4)	!NEW TARGET
C
		DO 700	J=63,1,-1
C
			READ	(2,REC=J)TSX	!SHUFFLE STIL FILE DOWN
			WRITE	(2,REC=J+1)TSX
C
700		CONTINUE
C
		WRITE	(2,REC=1)TS(I)	!INSERT NEW RECORD
C
		CLOSE	(UNIT=2)
C
		put_analog_val=TS(I).T	!SEND NEW TARGET TO WDPF
		status = SHC_put_point_quality( AO(I), point_quality)
		status = SHC_put_analog_val( AO(I), put_analog_val)
C
		put_analog_val=TS(I).FC	!SEND NEW F CAKE TO WDPF
		status = SHC_put_analog_val( AOFC(I), put_analog_val)
C
		put_analog_val=TS(I).R10	!SEND NEW R10 TO WDPF
		status = SHC_put_analog_val( AOR10(I), put_analog_val)
C
		put_analog_val=TS(I).LP	!SEND NEW LINE PEL TO WDPF
		status = SHC_put_analog_val( AOLP(I), put_analog_val)
C
		put_analog_val=TS(I).TF	!SEND NEW FLOATING TARGET TO WDPF
		status = SHC_put_analog_val( AOFT(I), put_analog_val)
C
		put_analog_val=TS(I).FCT  !SEND NEW FILTER CAKE TARGET TO WDPF
		status = SHC_put_analog_val( AOFCT(I), put_analog_val)
C
		CNC_ANVAL(TARX(I))=TS(I).T	!PUT TARGET IN ANVAL TABLE
C
		M=(TARX(I)/32)+1		!CALCULATE GOODPOINT WORD
		N=MOD(TARX(I),32)		!CALCULATE GOODPOINT BIT
		CGPT(M)=JIBSET(CGPT(M),N)       !SET GOODPT FOR TARGET SIO2
C
1000	CONTINUE
C
9999	CONTINUE
C
	CALL	SYS$HIBER()
	GOTO 100
C
99999	CONTINUE
C
	CALL	EXIT
C
	END
