CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C	CONFIDENTIAL
C	Property of United States Steel Corporation
C	Copyright 2007 United States Steel Corporation
C	All rights reserved.
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
	PROGRAM		DCSCAN	!DUST COLLECTOR SCANNER
C
C	FORT/ALIGN=NONE/WARN=NOALIGN/FLOAT=D_FLOAT DCSCAN
C
C	LINK DCSCAN,DCSCAN/OPT
C
C	THIS PROGRAM RUNS EVERY 15 MINUTES AND MONITORS
C	ALL USS MINNTAC DUST COLLECTORS TO PERFORM THE FOLLOWING:
C
C	1. LOOK AT EACH DUST COLLECTOR AT 02:00 AM EACH DAY. THE
C	   INTERLOCKING EMISSION UNITS ARE TO BE TAKEN INTO 
C	   CONSIDERATION.  IF THE INTERLOCKING EMISSION UNITS ARE 
C	   ALL OFF AT 02:00 AM THEN THE DUST COLLECTOR WILL BE LOOKED
C	   AT EVERY 15 MINUTES UNTIL ONE OR MORE OF THE INTERLOCKING
C	   EMISSION UNITS ARE ON.  IF THAT OCCURS THEN THE FIRST 15
C	   MINUTE READING WILL BE THE READING FOR THE DAY FOR THAT 
C	   DUST COLLECTOR.  IF NONE OF THE INTERLOCKING EMISSION UNITS
C	   ARE ON FOR THE REST OF THE DAY, THEN THE DUST COLLECTOR 
C	   WILL BE CONSIDERED IN COMPLIANCE FOR THE DAY.  THESE RULES
C	   WERE ESTABLISHED BY RAY POTTS ON 4-27-2004.
C
C	REVISION HISTORY
C	
C	ADDED 247-03-1 WET SCRUBBER PRS 2048
C
C
C	CHANGED BEGINNING SCAN START TIME FROM 2:00AM TO 12:00NOON PER BRAD
C	GERLACH'S REQUEST
C	IM484920 BILL BABICH
C
	IMPLICIT NONE
C
	STRUCTURE /DUST_COLL/	!DUST COLLECTOR
C
		CHARACTER*12	EN	!EQUIPMENT NUMBER
		CHARACTER*2	TYPE	!DUST COLLECTOR TYPE
		CHARACTER*6	PLANT	!PLANT LOCATION
C
		INTEGER*4	ONSCAN	!SCAN FLAG
		INTEGER*4	MR(2)	!MOTOR RUN CX'S
		INTEGER*4	FLOW	!H2O FLOW TAG
		INTEGER*4	PRESS	!PRESS TAG
		INTEGER*4	TAGS(6)	!SERVICE TAGS
C
		REAL*8	FLL		!H2O FLOW LOW LIMIT
		REAL*8	FHL		!H2O FLOW HIGH LIMIT
		REAL*8	PLL		!PRESS LOW LIMIT
		REAL*8	PHL		!PRESS HIGH LIMIT
C
	END STRUCTURE
C
	RECORD/DUST_COLL/DC
C
	STRUCTURE /DUST_DATA/	!DUST COLLECTOR
C
		REAL*4		FLOW		!H2O FLOW READING
		REAL*4		PRES		!PRESSURE READING
		CHARACTER*23   	FDATEX		!FLOW READING DATE
		CHARACTER*23   	PDATEX		!PRES READING DATE
		INTEGER*4	F_FLAG		!FLOW FLAG
		INTEGER*4	P_FLAG		!PRES FLAG
C
	END STRUCTURE
C
	RECORD/DUST_DATA/DCX,DCZ,DC15
C
	INTEGER*4	ADRS(2),DAYFLG,MCX
	INTEGER*4	UFO_OPEN,I,J,K,L,M,N,CX,TAG
	INTEGER*4	BIT,POINT,MR1,MR2,PNT,MIN,SEC
	INTEGER*4	CXSTS_CRS(100)
	INTEGER*4	CXSTS_CNC(100)
	INTEGER*4	CXSTS(100)
	INTEGER*4	CXSTS_AG3(100)
	INTEGER*4	FLAG(4),RND,RNDX,STRFLG
C
	REAL*4		ANVAL_CRS(1000)
	REAL*4		ANVAL_CNC(1900)
	REAL*4		ANVAL(1200)
	REAL*4		ANVAL_AG3(1000)
	REAL*4		VALU,SECF,RSECS
C
	CHARACTER*9	TIM/'0 0:15:00'/	!15 MIN RUN TIME
	CHARACTER*23	DATETIME
	CHARACTER*5	STRTM/'02:00'/	!START OF MONITOR TIME
	CHARACTER*5	DAYTM/'22:30'/	!END OF DAY TIME
	CHARACTER	TIME*5		!STORAGE FOR TIME OF DAY
	CHARACTER*21	DFILE/'ANA:DCDD-MMM-YYYY.DAT'/
C
	EXTERNAL	UFO_OPEN
C
	COMMON/CRS_ANVALTBL/ANVAL_CRS
	COMMON/CNC_ANVALTBL/ANVAL_CNC
	COMMON/ANVALTBL/ANVAL
	COMMON/AG3_ANVALTBL/ANVAL_AG3
C
	COMMON/CONTACTSCRS/CXSTS_CRS
	COMMON/CONTACTSCNC/CXSTS_CNC
	COMMON/CONTACTS/CXSTS
	COMMON/CONTACTSAG3/CXSTS_AG3
C
	DCZ.FDATEX='                       '
	DCZ.PDATEX='                       '
C
	CALL	LIB$DATE_TIME(DATETIME)	!GET CURRENT TIME
	READ	(DATETIME(16:17),'(I2)')MIN	!CONVERT MINUTES
	READ	(DATETIME(19:20),'(I2)')SEC	!CONVERT SECONDS
	I=MIN/15
	I=(I*15)-1
	IF (MIN .GE. 15)MIN=MIN-I-1
	IF (MIN .LT. 15)MIN=MIN+1
	SEC=60-SEC
	SEC=SEC+((15-MIN)*60)
	SECF=SEC	!FLOATING POINT SECONDS
	CALL	LIB$WAIT(SECF)	!WAIT TILL BEGINNING OF NEXT 15 MIN INTERVAL
C
	CALL	SYS$BINTIM(TIM,ADRS)
	CALL	SYS$SCHDWK(,,ADRS,ADRS)	!SCHEDULE 15 MIN WAKEUP
C
100	CONTINUE
C
	RSECS=SECNDS(0.0)	!GET SECONDS SINCE MIDNIGHT
	IF ((RSECS .GE. 43200).AND.(RSECS .LE. 81000))THEN
		STRFLG=1	!LOOK BETWEEN 12:00 AND 22:30
	ELSE
		STRFLG=0

	END IF
C
	OPEN	(UNIT=1,FILE='ANA:WGS.DAT',STATUS='OLD',
	1	ACCESS='DIRECT',RECL=5,RECORDTYPE='FIXED',
	1	FORM='UNFORMATTED',SHARED,USEROPEN=UFO_OPEN)
C
	READ	(1,REC=1)FLAG	!READ IN INDICATIONS FOR WASTE GAS SCRUBBERS
C
	CLOSE	(UNIT=1)
C
	CALL	LIB$DATE_TIME(DATETIME)	!GET CURRENT TIME
	TIME=DATETIME(13:17)		!ISOLATE HOURS AND MINUTES
C
	OPEN	(UNIT=3,FILE='ANA:DCPHR.DAT',FORM='UNFORMATTED',
	1	ACCESS='DIRECT',STATUS='OLD',RECL=24,SHARED,
	1	RECORDTYPE='FIXED',USEROPEN=UFO_OPEN)
C
	OPEN	(UNIT=2,FILE='ANA:DCDATA.DAT',STATUS='OLD',
	1	ACCESS='DIRECT',RECL=16,RECORDTYPE='FIXED',
	1	FORM='UNFORMATTED',SHARED,USEROPEN=UFO_OPEN)
C
	DO 666	I=1,150 	!PROCESS ALL DUST COLLECTORS
C
		READ	(3,REC=I)DC	!BRING IN A DUST COLLECTOR
		IF (DC.ONSCAN .EQ. 0)GOTO 666	!IF OFF SCAN THEN SKIP
		READ	(2,REC=I)DCX	!BRING IN DC DATA
		MR1=0
		MR2=0
C
		POINT=DC.MR(1)		!FIRST MOTOR RUN CX
		M=(POINT/32)+1		!WORD IN CX TABLE
		N=MOD(POINT,32)		!BIT IN WORD
		IF (DC.PLANT(1:2) .EQ. 'CC')MR1=JIBITS(CXSTS_CRS(M),N,1)
		IF (DC.PLANT(1:2) .EQ. 'FC')MR1=JIBITS(CXSTS_CRS(M),N,1)
		IF (DC.PLANT(1:4) .EQ. 'CONC')MR1=JIBITS(CXSTS_CNC(M),N,1)
		IF (DC.PLANT(1:5) .EQ. 'AGGL2')MR1=JIBITS(CXSTS(M),N,1)
		IF (DC.PLANT(1:5) .EQ. 'AGGL3')MR1=JIBITS(CXSTS_AG3(M),N,1)
C
	IF (DC.MR(2) .NE. 0)THEN	!IF DC HAS 2ND MOTOR RUN CX CHECK IT
		POINT=DC.MR(2)		!SECOND MOTOR RUN CX
		M=(POINT/32)+1		!WORD IN CX TABLE
		N=MOD(POINT,32)		!BIT IN WORD
		IF (DC.PLANT(1:2) .EQ. 'CC')MR2=JIBITS(CXSTS_CRS(M),N,1)
		IF (DC.PLANT(1:2) .EQ. 'FC')MR2=JIBITS(CXSTS_CRS(M),N,1)
		IF (DC.PLANT(1:4) .EQ. 'CONC')MR2=JIBITS(CXSTS_CNC(M),N,1)
		IF (DC.PLANT(1:5) .EQ. 'AGGL2')MR2=JIBITS(CXSTS(M),N,1)
		IF (DC.PLANT(1:5) .EQ. 'AGGL3')MR2=JIBITS(CXSTS_AG3(M),N,1)
	END IF
C
		MCX=0
		IF (MR1 .EQ. 1)MCX=1
		IF (MR2 .EQ. 1)MCX=1
C
		IF (STRFLG .EQ. 0)GOTO 665	!NOT 12:00 NOON YET
C
		IF (DC.FLOW .GT. 0)THEN	!DOES DC HAVE A WATER FLOW
			IF (DCX.F_FLAG .EQ. 0)GOTO 222	!FIND FLOW
			IF (DCX.F_FLAG .EQ. -1)GOTO 665	!DC OFF EMS ON
 			IF (DCX.F_FLAG .EQ. 2)GOTO 665	!DC ON BAD READING
 			IF (DCX.F_FLAG .EQ. 1)GOTO 665	!DC ON GOOD READING
 		END IF
		IF (DC.PRESS .GT. 0)THEN	!DOES DC HAVE A DP CELL
			IF (DCX.P_FLAG .EQ. 0)GOTO 222	!FIND PRES
			IF (DCX.P_FLAG .EQ. -1)GOTO 665	!DC OFF EMS ON
			IF (DCX.P_FLAG .EQ. 2)GOTO 665	!DC ON BAD READING
			IF (DCX.P_FLAG .EQ. 1)GOTO 665	!DC ON GOOD READING
		END IF
C
		GOTO 665	!CONTINUE
C
222	CONTINUE
C
C		WE MUST CHECK TO SEE IF IT HAS
C		ANY EMISSION UNITS THAT ARE RUNNING... IF IT HAS AN EMISSION
C		UNIT THAT IS RUNNING WE MUST RECORD ITS READINGS... IF IT HAS
C		NO EMISSION UNITS THEN WE WILL RECORD ITS READINGS
C
		BIT=0
C
		IF (I .EQ. 129)THEN	!PROCESS 247-03-1 WGS
			IF (FLAG(1) .GE. 540)THEN !HAS LINE RUN FOR 90 MIN
				BIT=1       !YES
				GOTO 557    !GO RECORD
			ELSE
				GOTO 665    !NO SKIP
			END IF
		END IF
C
		IF (I .EQ. 100)THEN	!PROCESS 247-04-1 WGS
			IF (FLAG(2) .GE. 540)THEN !HAS LINE RUN FOR 90 MIN
				BIT=1       !YES
				GOTO 557    !GO RECORD
			ELSE
				GOTO 665    !NO SKIP
			END IF
		END IF
C
		IF (I .EQ. 108)THEN	!PROCESS 247-05-1 WGS
			IF (FLAG(3) .GE. 540)THEN !HAS LINE RUN FOR 90 MIN
				BIT=1       !YES
				GOTO 557    !GO RECORD
			ELSE
				GOTO 665    !NO SKIP
			END IF
		END IF
C
		IF (I .EQ. 119)THEN	!PROCESS 247-06-1 WGS
			IF (FLAG(4) .GE. 540)THEN !HAS LINE RUN FOR 90 MIN
				BIT=1       !YES
				GOTO 557    !GO RECORD
			ELSE
				GOTO 665    !NO SKIP
			END IF
		END IF
C
		IF (I .EQ. 125)THEN	!PROCESS 247-07-1 WGS
			IF (FLAG(5) .GE. 540)THEN !HAS LINE RUN FOR 90 MIN
				BIT=1       !YES
				GOTO 557    !GO RECORD
			ELSE
				GOTO 665    !NO SKIP
			END IF
		END IF
C
		TAG=0
		BIT=0
C
		DO 555	J=1,6	!CHECK ALL SIX SERVICE TAGS
			IF (DC.TAGS(J) .EQ. 0)GOTO 555
			TAG=1	!FOUND A SERVICE TAG ...CHECK IT
			PNT=DC.TAGS(J)
			M=(PNT/32)+1		!WORD IN CX TABLE
			N=MOD(PNT,32)		!BIT IN WORD
			IF (DC.PLANT(1:2) .EQ. 'CC')
	1			CX=JIBITS(CXSTS_CRS(M),N,1)
			IF (DC.PLANT(1:2) .EQ. 'FC')
	1			CX=JIBITS(CXSTS_CRS(M),N,1)
			IF (DC.PLANT(1:4) .EQ. 'CONC')
	1			CX=JIBITS(CXSTS_CNC(M),N,1)
			IF (DC.PLANT(1:5) .EQ. 'AGGL2')
	1			CX=JIBITS(CXSTS(M),N,1)
			IF (DC.PLANT(1:5) .EQ. 'AGGL3')
	1			CX=JIBITS(CXSTS_AG3(M),N,1)
C
			IF (CX .EQ. 1)THEN
				BIT=1	!SERVICE TAG IS RUNNING
				GOTO 556	!READY TO TAKE READINGS
			END IF
C
555		CONTINUE
C
556	CONTINUE
C
	IF (MCX .EQ. 0)THEN	!IF DC IS OFF
		IF (TAG .EQ. 0)GOTO 665	!AND NO EMISSION UNITS TO CHECK
	END IF
C
	IF (MCX .EQ. 0)THEN	!IF DC IS OFF
		IF (BIT .EQ. 1)THEN	!AND EMISSION UNITS ON
			IF (DC.FLOW .NE. 0)THEN
				DCX.F_FLAG=-1
				DCX.FDATEX=DATETIME
			END IF
			IF (DC.PRESS .NE. 0)THEN
				DCX.P_FLAG=-1
				DCX.PDATEX=DATETIME
			END IF
			GOTO 663  !GO SAVE & RECORD 15 MINUTE READINGS
		END IF
	END IF
C
	IF ((MCX .EQ. 1).AND.(TAG .EQ. 1))THEN !IF DC IS ON AND HAS AN EU
		IF (BIT .EQ. 0)GOTO 665	!EU IS OFF SO SKIP TAKING READING
	END IF
C
	IF ((MCX .EQ. 0).AND.(TAG .EQ. 1))THEN !IF DC IS OFF AND HAS AN EU
		IF (BIT .EQ. 0)GOTO 665	!EU IS OFF SO SKIP TAKING READING
	END IF
C
557	CONTINUE
C
	IF ((BIT .EQ. 1).OR.(TAG .EQ. 0))THEN	!WE NEED TO RECORD
		IF (DC.FLOW .NE. 0)THEN	!WE HAVE A FLOW METER
			IF (DCX.F_FLAG .EQ. 1)GOTO 777	!ALREADY HAVE OK VALU
			PNT=DC.FLOW
			IF (DC.PLANT(1:2) .EQ. 'CC')
	1			VALU=ANVAL_CRS(PNT)
			IF (DC.PLANT(1:2) .EQ. 'FC')
	1			VALU=ANVAL_CRS(PNT)
			IF (DC.PLANT(1:4) .EQ. 'CONC')
	1			VALU=ANVAL_CNC(PNT)
			IF (DC.PLANT(1:5) .EQ. 'AGGL2')
	1			VALU=ANVAL(PNT)
			IF (DC.PLANT(1:5) .EQ. 'AGGL3')
	1			VALU=ANVAL_AG3(PNT)
C
			DCX.FLOW=VALU		!RECORD FLOW
			DCX.FDATEX=DATETIME	!RECORD TIME AND DATE
C
			RND=VALU	!ROUND FLOW TO
			RNDX=VALU*10	!NEAREST GPM (WHOLE NUMBER)
			IF ((RNDX-(RND*10)) .GE. 5)RND=RND+1
			VALU=RND
C
			IF ((VALU .LT. DC.FLL).OR.
	1		   (VALU .GT. DC.FHL))THEN	!BAD FLOW
				DCX.F_FLAG=2	!BAD FLOW
				GOTO 777
			END IF
C
			DCX.F_FLAG=1		!IND WE FOUND GOOD VALUE
C
		END IF
C
777	CONTINUE
C
		IF (DC.PRESS .NE. 0)THEN	!WE HAVE A PRES XMTR
			IF (DCX.P_FLAG .EQ. 1)GOTO 663	!ALREADY HAVE OK VALU
			PNT=DC.PRESS
			IF (DC.PLANT(1:2) .EQ. 'CC')
	1			VALU=ANVAL_CRS(PNT)
			IF (DC.PLANT(1:2) .EQ. 'FC')
	1			VALU=ANVAL_CRS(PNT)
			IF (DC.PLANT(1:4) .EQ. 'CONC')
	1			VALU=ANVAL_CNC(PNT)
			IF (DC.PLANT(1:5) .EQ. 'AGGL2')
	1			VALU=ANVAL(PNT)
			IF (DC.PLANT(1:5) .EQ. 'AGGL3')
	1			VALU=ANVAL_AG3(PNT)
C
			DCX.PRES=VALU		!RECORD PRESS
			DCX.PDATEX=DATETIME	!RECORD TIME AND DATE
C
			RND=VALU*10	!ROUND PRESSURE TO THE
			RNDX=VALU*100	!NEAREST TENTH (.1) OF AN INCH
			IF ((RNDX-(RND*10)) .GE. 5)RND=RND+1
			VALU=RND
			VALU=VALU/10
C
			IF ((VALU .LT. DC.PLL).OR.
	1		   (VALU .GT. DC.PHL))THEN	!BAD PRESS
				DCX.P_FLAG=2	!BAD PRESSURE
				GOTO 663
			END IF
C
			DCX.P_FLAG=1		!IND WE FOUND GOOD VALUE
		END IF
C
	ELSE	!DC IS RUNNING BUT EMISSION UNITS ARE OFF
C
		GOTO 663	!THEN SKIP IT
	END IF
C
663	CONTINUE
C
	WRITE	(2,REC=I)DCX	!SAVE DC DATA
C
665	CONTINUE
C
666	CONTINUE
C
	CLOSE	(UNIT=3)	!CLOSE PHRASE FILE
C
	IF (TIME .EQ. DAYTM)THEN	!IF END OF DAY
		IF (DAYFLG .EQ. 0)THEN	!IF DAY HAS NOT BEEN SAVED
			DFILE(7:17)=DATETIME(1:11) !CREATE NEW FILE NAME
			IF (DFILE(7:7) .EQ. ' ')DFILE(7:7)='0'
C
			OPEN	(UNIT=3,FILE=DFILE,STATUS='NEW',
	1			ACCESS='DIRECT',RECL=16,RECORDTYPE='FIXED',
	1			FORM='UNFORMATTED')
C
			DO 778	I=1,150 	!PROCESS ALL DUST COLLS
C
				READ	(2,REC=I)DCX	!BRING IN DC DATA
				WRITE	(3,REC=I)DCX	!SAVE IT
				WRITE	(2,REC=I)DCZ	!ZERO OUT FILE
778			CONTINUE
C
			DAYFLG=1
		END IF
	ELSE
		DAYFLG=0		!RESET END OF DAY FLAG
	END IF
C
	CLOSE	(UNIT=2)
	CLOSE	(UNIT=3)
C
999	CONTINUE
C
	CALL	SYS$HIBER()	!DELAY TILL WAKEUP
C
	GOTO 100
C
	END
C
