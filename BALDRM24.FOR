C
	PROGRAM	BALDRM24
C
	IMPLICIT NONE
C
C
C	LINK BALDRM,BALDRM/OPT
C
C	THIS PROGRAM MONITORS ALL 24 BALLING DRUM CIRCUITS FOR AGGLOMERATOR
C	LINES 3,4,5,6 AND 7AND PERFORMS THE FOLLOWING FUNCTIONS:
C
C	1.  CREATES A 24 HOUR (DAILY) AVERAGE FOR EACH 027 BELT SCALE
C	2.  CREATES A 24 HOUR (DAILY) AVERAGE FOR EACH 030 BELT SCALE
C	3.  CREATES A 24 HOUR (DAILY) AVERAGE FOR EACH 030 BELT PULSE
C
C	THESE CALCULATIONS ARE ONLY PERFORMED ONLY AFTER BOTH BELTS 
C	HAVE BEEN RUNNING FORWARD CONTINUOUSLY FOR 15 MINUTES. ANY
C	CHANGE IN THIS CONDITION CAUSES THE 15 MINUTE TIMER TO BE RESET.
C
C	AT THE END OF EACH DAY THE RESULTS ARE TRANSFERRED TO THE 
C	PROD2 ORACLE DATABASE AND PLACED IN THE TABLE aggl_scale_monitor_daily
C
	STRUCTURE	/BDRUM/
C
		CHARACTER*23	DATX	!DATE AND TIME
		CHARACTER*10	EQUIP(24)	!EQUIPMENT NUMBER
		REAL*4		B27AVG(24)	!027 BELT SCALE AVERAGE LTPH
		REAL*4		B30AVG(24)	!030 BELT SCALE AVERAGE LTPH
		REAL*4		B30PA(24)    !030 BELT SCALE PULSE LTPH
		REAL*4		COMPAR(24)    !027...030 SCALE COMPARISON %
		REAL*4		PULSCK(24)    !030 ANALOG/PULSE COMPARISON %
C
	END STRUCTURE
C
	RECORD/BDRUM/BDRUM
C
	STRUCTURE	/BDRUMA/
C
		REAL*4		T15MIN(24)	!15 MINUTE TIMER
		REAL*4		B27A(24)	!027 BELT SCALE ACCUMULATION
		REAL*4		B27O(24)	!027 BELT SCALE OBSERVATION
		REAL*4		B30A(24)	!030 BELT SCALE ACCUMULATION
		REAL*4		B30O(24)	!030 BELT SCALE OBSERVATION
		REAL*4		B30PA(24)    	!030 BELT SCALE PULSE ACCUM
		REAL*4		B30PO(24)    	!030 BELT SCALE PULSE OBS
C
	END STRUCTURE
C
	RECORD/BDRUMA/BDRUMA
C
	INTEGER*4	I,J,K,L,M,N
	INTEGER*4	UFO_OPEN
	INTEGER*4	ADRS(2),BIT27,POINT,BIT30
	INTEGER*4	B27MCX(24)/1191,1192,1193,1194,
	1			   300,301,302,303,304,
	1			   507,508,509,510,511,
	1			   44,45,46,47,48,
	1			   49,50,51,52,53/
	INTEGER*4	B30MCX(24)/1005,1006,1007,1008,
	1			   1578,1579,1580,1581,1582,
	1			   1583,1584,1585,1586,1587,
	1			   74,77,78,80,83,
	1			   86,89,90,92,94/
	INTEGER*4	B27SP(24)/881,882,883,884,
	1			  565,566,567,568,569,
	1			  599,600,601,602,603,
	1			  594,595,596,597,598,
	1			  599,600,601,602,603/
	INTEGER*4	B27AI(24)/710,716,722,728,
	1			  53,54,55,56,57,
	1			  169,170,171,172,173,
	1			  1,6,11,16,21,
	1			  180,184,188,192,196/
	INTEGER*4	B30AI(24)/709,715,721,727,
	1			  67,68,69,70,71,
	1			  183,184,185,186,187,
	1			  2,7,12,17,22,
	1			  181,185,189,193,197/
	INTEGER*4	B30PA(24)/760,762,764,766,
	1			  516,517,518,519,520,
	1			  521,522,523,524,525,
	1			  516,517,518,519,520,
	1			  536,537,538,539,540/
C
	INTEGER*4	CXSTS(100)
	INTEGER*4	CXSTS_AG3(100)
C
	REAL*4		ANVAL(1200)
	REAL*4		ANVAL_AG3(1000)
	REAL*4		SP,PCT,LTPH
C
	CHARACTER*9	TIM/'0 0:00:10'/	!10 SEC RUN TIME
	CHARACTER*23	DATETIME
	CHARACTER*5	TIME,DAYTIM
	CHARACTER*10	EQUIP(24)/'230-03-1  ','230-03-2  ','230-03-3  ',
	1			  '230-03-4  ','230-04-1  ','230-04-2  ',
	1			  '230-04-3  ','230-04-4  ','230-04-5  ',
	1			  '230-05-1  ','230-05-2  ','230-05-3  ',
	1			  '230-05-4  ','230-05-5  ','230-06-1  ',
	1			  '230-06-2  ','230-06-3  ','230-06-4  ',
	1			  '230-06-5  ','230-07-1  ','230-07-2  ',
	1			  '230-07-3  ','230-07-4  ','230-07-5  '/
C
	COMMON/ANVALTBL/ANVAL		!ANALOG PULSE VALUE TABLE
	COMMON/AG3_ANVALTBL/ANVAL_AG3
	COMMON/CONTACTS/CXSTS		!CONTACT STATUS TABLE
	COMMON/CONTACTSAG3/CXSTS_AG3
C
	EXTERNAL	UFO_OPEN
C
	CALL	SYS$BINTIM(TIM,ADRS)
	CALL	SYS$SCHDWK(,,ADRS,ADRS)	!SCHEDULE 10 SEC WAKEUP
	DAYTIM='     '
C
100	CONTINUE
C
	CALL	LIB$DATE_TIME(DATETIME)	!GET CURRENT TIME
	TIME=DATETIME(13:17)		!ISOLATE HOURS AND MINUTES
	IF (TIME(1:1) .EQ. ' ')TIME(1:1)='0'
C
	OPEN	(UNIT=1,FILE='ANA:BDRUMA24.DAT',STATUS='OLD',
	1	RECL=168,RECORDTYPE='FIXED',FORM='UNFORMATTED',
	1	ACCESS='DIRECT',SHARED,USEROPEN=UFO_OPEN)
C
	READ	(1,REC=1)BDRUMA	!BRING IN ACTIVE RESULTS
C
	DO	500 I=1,24	!PROCESS ALL BALL MILLS
C
		BIT27=0
		POINT=B27MCX(I)	!027 BELT M-CX
		M=(POINT/32)+1	!WORD IN CX TABLE
		N=MOD(POINT,32)	!BIT IN WORD
		IF (I .LE. 14)THEN	!IF AGGL STEP12
			BIT27=JIBITS(CXSTS(M),N,1)
		ELSE
			BIT27=JIBITS(CXSTS_AG3(M),N,1)
		END IF
C
		BIT30=0
		POINT=B30MCX(I)	!030 BELT M-CX
		M=(POINT/32)+1	!WORD IN CX TABLE
		N=MOD(POINT,32)	!BIT IN WORD
		IF (I .LE. 14)THEN	!IF AGGL STEP12
			BIT30=JIBITS(CXSTS(M),N,1)
		ELSE
			BIT30=JIBITS(CXSTS_AG3(M),N,1)
		END IF
C
		BIT30=BIT30+BIT27
C
		IF (BIT30 .NE. 2)THEN	!IF BOTH BELTS NOT RUNNING FORWARD
			BDRUMA.T15MIN(I)=0	!ZERO 15 MIN TIMER
			GOTO 500
		END IF
C
		IF (I .LE. 14)THEN
			SP=ANVAL(B27SP(I))	!027 BELT SETPOINT
			LTPH=ANVAL(B27AI(I))	!027 BELT LTPH
		ELSE
			SP=ANVAL_AG3(B27SP(I))	!027 BELT SETPOINT
			LTPH=ANVAL_AG3(B27AI(I))	!027 BELT LTPH
		END IF
C
		PCT=(SP/LTPH)*100   !CHECK IF LTPH ARE AT LEAST 60% OF SP
		IF (PCT .LT. 60)THEN            !NO
			BDRUMA.T15MIN(I)=0	!ZERO 15 MIN TIMER
			GOTO 500
		END IF
C
		IF (BDRUMA.T15MIN(I) .LT. 100)THEN
			BDRUMA.T15MIN(I)=BDRUMA.T15MIN(I)+1	!BUMP TIMER
		END IF
C
		IF (BDRUMA.T15MIN(I) .LT. 90)GOTO 500	!NEED 15 CONT. MINS
C
		IF (I .LE. 14)THEN
			BDRUMA.B27A(I)=BDRUMA.B27A(I)+ANVAL(B27AI(I))
			BDRUMA.B27O(I)=BDRUMA.B27O(I)+1
			BDRUMA.B30A(I)=BDRUMA.B30A(I)+ANVAL(B30AI(I))
			BDRUMA.B30O(I)=BDRUMA.B30O(I)+1
			BDRUMA.B30PA(I)=BDRUMA.B30PA(I)+ANVAL(B30PA(I))*120
			BDRUMA.B30PO(I)=BDRUMA.B30PO(I)+1
		ELSE
			BDRUMA.B27A(I)=BDRUMA.B27A(I)+ANVAL_AG3(B27AI(I))
			BDRUMA.B27O(I)=BDRUMA.B27O(I)+1
			BDRUMA.B30A(I)=BDRUMA.B30A(I)+ANVAL_AG3(B30AI(I))
			BDRUMA.B30O(I)=BDRUMA.B30O(I)+1
			BDRUMA.B30PA(I)=BDRUMA.B30PA(I)+ANVAL_AG3(B30PA(I))*120
			BDRUMA.B30PO(I)=BDRUMA.B30PO(I)+1
		END IF
C
500	CONTINUE
C
	WRITE	(1,REC=1)BDRUMA	!SAVE ACTIVE RESULTS
C
	CLOSE	(UNIT=1)
C
	IF (TIME .EQ. '22:30')THEN
		IF (TIME .EQ. DAYTIM)GOTO 900	!PROCESSED THIS DAY BEFORE
		BDRUM.DATX=DATETIME	!CURRENT TIME
		GOTO 600
	END IF
C
	DAYTIM='     '  !RESET END OF SHIFT
	GOTO 900	!NOT END OF SHIFT
C
600	CONTINUE
C
	DAYTIM=TIME	!SAVE SO WE WON'T REPEAT END OF SHIFT
C
	DO 800	I=1,24	!PROCESS END OF DAY
C
		IF (BDRUMA.B27O(I) .EQ. 0)THEN
			BDRUM.B27AVG(I)=0
		ELSE
			BDRUM.B27AVG(I)=BDRUMA.B27A(I)/BDRUMA.B27O(I)
		END IF
C
		IF (BDRUMA.B30O(I) .EQ. 0)THEN
			BDRUM.B30AVG(I)=0
		ELSE
			BDRUM.B30AVG(I)=BDRUMA.B30A(I)/BDRUMA.B30O(I)
		END IF
C
		IF (BDRUMA.B30PO(I) .EQ. 0)THEN
			BDRUM.B30PA(I)=0
		ELSE
			BDRUM.B30PA(I)=BDRUMA.B30PA(I)/BDRUMA.B30PO(I)
		END IF
C
		IF (BDRUM.B27AVG(I) .EQ. 0)THEN
			BDRUM.COMPAR(I)=0
		ELSE
			BDRUM.COMPAR(I)=((BDRUM.B27AVG(I)
	1		-BDRUM.B30AVG(I))/BDRUM.B27AVG(I))*100
		END IF
C
		IF (BDRUM.B30AVG(I) .EQ. 0)THEN
			BDRUM.PULSCK(I)=0
		ELSE
			BDRUM.PULSCK(I)=((BDRUM.B30AVG(I)
	1		-BDRUM.B30PA(I))/BDRUM.B30AVG(I))*100
		END IF
C
		BDRUM.EQUIP(I)=EQUIP(I)	!EQUIPMENT NUMBER
C
		BDRUMA.B27A(I)=0	!ZERO OUT ACTIVE FOR START OF SHIFT
		BDRUMA.B27O(I)=0
		BDRUMA.B30A(I)=0
		BDRUMA.B30O(I)=0
		BDRUMA.B30PA(I)=0
		BDRUMA.B30PO(I)=0
C
800	CONTINUE
C
	OPEN	(UNIT=1,FILE='ANA:BDRUM24.DAT',STATUS='OLD',
	1	RECL=187,RECORDTYPE='FIXED',FORM='UNFORMATTED',
	1	ACCESS='DIRECT',SHARED,USEROPEN=UFO_OPEN)
C
	WRITE	(1,REC=1)BDRUM	!SAVE DAILY RESULTS
C
	CLOSE	(UNIT=1)
C
	OPEN	(UNIT=1,FILE='ANA:BDRUMA24.DAT',STATUS='OLD',
	1	RECL=168,RECORDTYPE='FIXED',FORM='UNFORMATTED',
	1	ACCESS='DIRECT',SHARED,USEROPEN=UFO_OPEN)
C
	WRITE	(1,REC=1)BDRUMA	!ZERO ACTIVE RESULTS
C
	CLOSE	(UNIT=1)
C
900	CONTINUE
C
	CALL	SYS$HIBER()	!DELAY TILL WAKEUP
C
	GOTO 100
C
	END
C
