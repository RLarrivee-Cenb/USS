C
	PROGRAM	EXPTNK		!EXPANSION TANK
C
C	LINK EXPTNK,EXPTNK/OPT,WESAPI_LIB:WESAPI/OPT
C
C	PROGRAM MONITORS EXPANSION TANK LEVELS FOR UTILITIES
C	PLANT AND ALARMS IF DETECTING SYSTEM WATER LOSS
C
	IMPLICIT NONE
C
	INCLUDE '($SSDEF)'
	INCLUDE '($IODEF)'
C
	INCLUDE 'WESAPI:SPD.DCL'
	INCLUDE 'WESAPI:SHC_defines.DCL'
	INCLUDE 'WESAPI:SHC_err.DCL'
C
	INTEGER*4	LIB$SYS_TRNLOG
	INTEGER*4 	STATUS
	CHARACTER*20	HIGHWAY

	CHARACTER*40    SPD_FILENAME
	INTEGER*2	SPD_FILENAME_LEN
	INTEGER*2	SPD_FD
	INTEGER*2	ACCESS_TYPE
	CHARACTER*8	PT_NAME
	BYTE		EXTENDED_FLAG
	BYTE		GP_BIT_NUM
	BYTE		INACTIVE_FLAG
	INTEGER*4	GP_SID
C
	INTEGER*2	PUT_DIGITAL_VAL(2)
	INTEGER*4	SID(2),POINT_QUALITY
C
	CHARACTER*8	PNT(2)/'DD500000','DD500001'/
	CHARACTER*6	NAMEX/'EXPTNK'/
C
	INTEGER*4	SCNTR,		!SHIFT COUNTER (0,1,2)
	1		HCNTR,		!HOUR COUNTER (0,1,2,3,4,5,6,7)
	1		SECS,
	1		PM1030/81000/,	!10:30 PM IN SECONDS
	1		PM1430/52200/,	!02:30 PM IN SECONDS
	1		AM0630/23400/,	!06:30 AM IN SECONDS
	1		SHFT(3)/22,14,6/
	INTEGER*4	INTHR,EOFD
	INTEGER*4	HRX/3600/	!SECONDS IN ONE HOUR
	INTEGER*4	I,J,K,L,M,N
	INTEGER*4	DATA(2)		!3 MINUTE PERIODS LEFT IN HALF HOUR
	INTEGER*4	MIN3X/180/	!SECONDS IN 3 MINUTES
	INTEGER*4	TEMP,MINA,MINQ,UFO_OPEN
	INTEGER*4	SAVE,ACNTR(2),GPT(20)
	INTEGER*4	ADRS(2),FLAG,LVL(2)/17,150/
	INTEGER*4	STUFF(10)/412,413,414,415,416,
	1			  417,418,419,420,421/
C	INTEGER*4	SYS$CANWAK
C
	REAL*4		RSECS,ANVAL(500)
	REAL*4		MINAL(2),MINOBS(2),AGALS(2)
	REAL*4		MINLVL(2),SLEN(2)/10,9/,
	1		DIA(2)/10,10/,TLEN(2)/40,45/,
	1		SEGH,LIMIT(2)/-60,-60/
	REAL*4		TEMPX,START,END,DIFF,AT(2),SMCF(2)
	REAL*4		DD,MD,GALS
	REAL*4		LASTHR(2),LAST8(2),PRELVL(2),LAST24(2)
	REAL*4		VALU
C
	CHARACTER*8	TIM/'0 0:0:10'/
	CHARACTER*8	PN(2)/'AI501018','AI503042'/,
	1		STEP(2)/'STEP I  ','STEP III'/
	CHARACTER*23	DATETIME
	CHARACTER*16	FILEX/'LOGS:UTL_DAY.DAT'/
C
	EXTERNAL	UFO_OPEN
C
	COMMON/UTL_ANVALTBL/ANVAL
	COMMON/UTL_GOODPTTBL/GPT	!ANALOG PULSE GOODPOINT STATUS
C
C  Use the wesapi highway number to form the name of the wesapi logical that
C  names the highway 0 point directory file and get file name from the logical
        STATUS = LIB$SYS_TRNLOG('WESAPI_PDIR_2',
     1			SPD_FILENAME_LEN,SPD_FILENAME,,,)

C  Put a NULL character (0) at the end of the file name string.  This is
C  necessary because the spd library functions expect string arguments
C  to be NULL terminated strings.
C
	SPD_FILENAME(SPD_FILENAME_LEN+1:) = CHAR(0)

C  Call the spd open file function to get access to the point directory file
C
        ACCESS_TYPE = RUNTIME
        SPD_FD = SPD_open_file(%ref(SPD_FILENAME), ACCESS_TYPE)
C
	DO 100	J=1,2	!GET THE TWO DIGITAL OUTPUT SIDS
C
C  Call the get sid function.  The point name argument must have a NULL (0)
C  at the end.
C
	        STATUS = SPD_get_sid(SPD_FD, %ref(PNT(J)//char(0)), SID(J),
     x		             GP_SID, GP_BIT_NUM, EXTENDED_FLAG, INACTIVE_FLAG)

C
100	CONTINUE
C  Close the point directory connection
C
	STATUS = SPD_close_file(SPD_FD)
C
C  Open the connection to SHC memory for WESAPI access
	status = SHC_open_memory()
C
	IF (status .NE. SHC_OK) THEN
		IF (status .EQ. SHC_E_MEMOPEN)GOTO 88
C
		OPEN	(UNIT=22,FILE='GP:EXPTNK.ERR',STATUS='UNKNOWN',
	1		 ACCESS='APPEND')
C
		WRITE	(22,77)status
77		FORMAT	(' SHC open memory failure - error =',I4)
C
		CLOSE	(UNIT=22)
C
	   GO TO 99999
C
	ENDIF
C
88	CONTINUE
C
150	CONTINUE
C
	EOFD=0	!ZERO END OF DAY FLAG
C
	OPEN	(UNIT=3,FILE='GP:EXPTNK.DAT',STATUS='OLD',
	1	ACCESS='DIRECT',FORM='UNFORMATTED',RECL=23,
	1	RECORDTYPE='FIXED')
C
	READ	(3,REC=1)DATA,FLAG,ACNTR,HCNTR,SCNTR,MINAL,MINOBS,
	1		 MINLVL,PRELVL,LASTHR,AGALS,LAST8,LAST24
C
	RSECS=SECNDS(0.0)			!CALCULATE AND SET UP
	SECS=RSECS				!THE OLD SCANNER COUNTERS
C
	IF (SECS .LT. AM0630)THEN	!IF BEFORE 630 AM
		SCNTR=2			!INDICATE MIDNIGHT SHIFT
		GOTO 200
	END IF
C
	IF (SECS .LT. PM1430)THEN	!IF BEFORE 0230 PM
		SCNTR=1			!INDICATE DAY SHIFT
		GOTO 200
	END IF
C
	IF (SECS .LT. PM1030)THEN	!IF BEFORE 1030 PM
		SCNTR=0			!INDICATE AFTERNOON SHIFT
		GOTO 200
	END IF
C
	SCNTR=2				!MUST BE AFTER 1030 PM 
C
200	CONTINUE
C
	INTHR=SECS/HRX	!CURRENT TIME DIVIDED BY ONE HOUR
	I=SCNTR+1          	!CALCULATE CORRECT HOUR COUNTER
	HCNTR=SHFT(I)-INTHR     !RELATIVE TO THIS SHIFT
	IF (HCNTR .GT. 0)THEN
		GOTO 300	
	ELSE	
		IF (INTHR .GE. 23)THEN	
			HCNTR=7 	!IF HOUR IS 23 HCNTR=7
		ELSE	
			HCNTR=0 	!IF HOUR IS 22 HCNTR=0
		END IF
	END IF
C
300	CONTINUE
C
	TEMP=MOD(SECS,HRX)	!MINUTES OF TIME
	MINQ=TEMP/MIN3X		!NUMBER OF 3 MINUTE PERIODS
	MINA=MOD(TEMP,MIN3X)	!MINUTES LESS THAN 3
C
	IF (MINQ .LT. 10)THEN	!IF MINUTES 29 OR LESS
		DATA(1)=10-MINQ	!3 MINUTE PERIODS LEFT TILL HALF HOUR
		GOTO 400	
	ELSE			!IF MINUTES 30 OR GREATER
		SAVE=MINQ-10
		DATA(1)=20-SAVE	!3 MINUTE PERIODS LEFT TILL HALF HOUR
		HCNTR=HCNTR-1	!PAST HALF HOUR SO ADJUST HOUR COUNTER
		IF (HCNTR .LT. 0)HCNTR=7
	END IF
C
400	CONTINUE
C
	DATA(2)=(MIN3X-MINA)/10	!10 SECOND SCANS LEFT IN REMAINDER OF PERIOD
	IF (DATA(2) .GT. 0)DATA(2)=DATA(2)-1
C
	MINAL(1)=0
	MINAL(2)=0
	MINOBS(1)=0
	MINOBS(2)=0
	ACNTR(1)=0
	ACNTR(2)=0
	AGALS(1)=0
	AGALS(2)=0
	FLAG=-1
C
C	HERE WE WILL WRITE DATA FILE OUT FOR FIRST PASS
C
	WRITE	(3,REC=1)DATA,FLAG,ACNTR,HCNTR,SCNTR,MINAL,MINOBS,
	1		 MINLVL,PRELVL,LASTHR,AGALS,LAST8,LAST24
C
	CLOSE	(UNIT=3)
C
	CALL	SYS$BINTIM(TIM,ADRS)
	CALL	SYS$SCHDWK(,,ADRS,ADRS)	!SCHEDULE 10 SEC WAKEUP
C
500	CONTINUE
C
C	HERE WE WILL READ IN DATA FILE FOR THIS RUN
C
	OPEN	(UNIT=3,FILE='GP:EXPTNK.DAT',STATUS='OLD',
	1	ACCESS='DIRECT',FORM='UNFORMATTED',RECL=23,
	1	RECORDTYPE='FIXED',USEROPEN=UFO_OPEN)
C
	READ	(3,REC=1)DATA,FLAG,ACNTR,HCNTR,SCNTR,MINAL,MINOBS,
	1		 MINLVL,PRELVL,LASTHR,AGALS,LAST8,LAST24
C
	DO 600	I=1,2
		MINAL(I)=MINAL(I)+ANVAL(LVL(I))	!ACCUM FOR AVG THIS TANK LEVEL
		MINOBS(I)=MINOBS(I)+1		!BUMP OBSERVATIONS
600	CONTINUE
C
	DATA(2)=DATA(2)-1	!DECREMENT 3 MINUTE COUNTER
C
	IF (DATA(2) .LT. 0)THEN	!IF END OF 3 MINUTE PERIOD
C
		DATA(2)=17	!RESET 3 MINUTE COUNTER
C
		DO 700	I=1,2
C
			PUT_DIGITAL_VAL(I)=0	!RESET CONTROL ROOM ALARM
C
C
C  			Call the put quality function.  
           		STATUS = SHC_put_point_quality(SID(I), POINT_QUALITY)
C
C  			Call the put digital value function.  
           		STATUS = SHC_put_digital_val(SID(I),
	1				PUT_DIGITAL_VAL(I))
C
			IF (MINOBS(I) .EQ. 0)THEN
				MINLVL(I)=0
			ELSE
				MINLVL(I)=MINAL(I)/MINOBS(I)	!AVG LEVEL
			END IF
C
			SEGH=(MINLVL(I)/100)*SLEN(I)	!SEGMENT HEIGHT IN TANK
C
C	CALCULATE CUBIC FEET OF TANK THAT CONTAINS WATER AND CONVERT 
C	TO GALLONS
C
			TEMPX=DIA(I)*DIA(I)*0.017
			TEMPX=TEMPX+(SEGH*DIA(I)*1.7)
			TEMPX=TEMPX-(SEGH*SEGH)
			IF (TEMPX .LT. 0)TEMPX=0
C
			TEMPX=SQRT(TEMPX)
C
			MINLVL(I)=TEMPX*SEGH*TLEN(I)*7.4805	!AVG GALS 
C
			IF (FLAG .EQ. -1)THEN	!IS THIS FIRST PERIOD 
				LASTHR(I)=MINLVL(I)	!YES INITIALIZE HOUR
				GOTO 690	!SKIP ALARM CHECK
			ELSE
				SAVE=MINLVL(I)-PRELVL(I)  !HAS TANK LVL DROPPED
				IF (SAVE .GE. 0)GOTO 680  !FROM LAST LVL 
				IF (SAVE .GT. LIMIT(I))GOTO 680 !SKIP ALARM
				AGALS(I)=AGALS(I)+SAVE !ACCUM ALARM RATE
				ACNTR(I)=ACNTR(I)+1	!BUMP ALARM COUNTER
				IF (ACNTR(I) .LT. 2)GOTO 690 !NOT YET 2 ALARMS
C
				GALS=(AGALS(I)/2)*20 !CALC LOSS IN GALS/HR
C
C	LOAD UP ALARM MESSAGE AND OUTPUT
C
				CALL	LIB$DATE_TIME(DATETIME)
C
				OPEN	(UNIT=1,FILE='LTA065:',STATUS='UNKNOWN',
	1		 	CARRIAGECONTROL='LIST')
C
			WRITE	(1,650,ERR=655)PN(I),STEP(I),DATETIME,GALS
650				FORMAT	(' ',1X,A,1X,A,1X,
	1				 'EXPANSION TANK LEVEL ALARM ',A,1X,
	1				 'LOSS RATE',F8.1,1X,'GPH'/)
C
655	CONTINUE
C
				CLOSE	(UNIT=1)
C
				PUT_DIGITAL_VAL(I)=1	!ALARM CONTROL ROOM
C
C  				Call the put quality function.  
           			STATUS = SHC_put_point_quality(SID(I), 
	1				POINT_QUALITY)
C
C  				Call the put digital value function.  
           			STATUS = SHC_put_digital_val(SID(I),
	1				PUT_DIGITAL_VAL(I))
C
			END IF
C
680	CONTINUE
C
				ACNTR(I)=0 	!RESET ALARM COUNTER
				AGALS(I)=0
C
690	CONTINUE
C
				PRELVL(I)=MINLVL(I)	!SWAP FOR NEXT CHECK
C
700		CONTINUE
C
		FLAG=0	!RESET START FLAG
C
		MINAL(1)=0
		MINAL(2)=0
		MINOBS(1)=0
		MINOBS(2)=0
C
		DATA(1)=DATA(1)-1	!DECREMENT HOUR COUNTER
C
		IF (DATA(1) .GE. 0)THEN	!IS IT END OF HOUR
			GOTO 1000	!NO
		ELSE
			CALL	LIB$DATE_TIME(DATETIME)
C
			OPEN	(UNIT=1,FILE='LTA065:',STATUS='UNKNOWN',
	1		CARRIAGECONTROL='LIST')
C
			WRITE	(1,710,ERR=900)DATETIME
710			FORMAT	(/' ',1X,'HOURLY EXPANSION TANK LEVEL REPORT  ',
	1			A,/,' ',11X,'START',8X,'END',2X,
	1			'GAIN/LOSS'/)
C
			DIFF=MINLVL(1)-LASTHR(1)
C
			WRITE	(1,720,ERR=900)LASTHR(1),MINLVL(1),DIFF
720			FORMAT	(' ',1X,'I/II',3F11.1)
C
			DIFF=MINLVL(2)-LASTHR(2)
C
			WRITE	(1,730,ERR=900)LASTHR(2),MINLVL(2),DIFF
730			FORMAT	(' ',1X,'III ',3F11.1)
C
			LASTHR(1)=MINLVL(1)	!SAVE START OF
			LASTHR(2)=MINLVL(2)	!HOUR TANK LEVELS
C
			DATA(1)=19	!RESET HOUR COUNTER
C
		END IF
C
		HCNTR=HCNTR-1		!DECREMENT HOUR COUNTER
C
		IF (HCNTR .GE. 0)THEN	!IS IT END OF SHIFT
			GOTO 900	!NO
		ELSE
			CALL	LIB$DATE_TIME(DATETIME)
C
			WRITE	(1,740,ERR=900)DATETIME
740			FORMAT	(/' ',1X,'8 HOUR EXPANSION TANK LEVEL REPORT  ',
	1			A,/,' ',11X,'START',8X,'END',2X,
	1			'GAIN/LOSS'/)
C
			DIFF=MINLVL(1)-LAST8(1)
C
			WRITE	(1,720,ERR=900)LAST8(1),MINLVL(1),DIFF
C
			DIFF=MINLVL(2)-LAST8(2)
C
			WRITE	(1,730,ERR=900)LAST8(2),MINLVL(2),DIFF
C
			LAST8(1)=MINLVL(1)	!SAVE START OF
			LAST8(2)=MINLVL(2)	!8 HOUR TANK LEVELS
C
			HCNTR=7	!RESET SHIFT COUNTER
C
		END IF
C
		SCNTR=SCNTR-1	!DECREMENT  DAY COUNTER
C
		IF (SCNTR .GE. 0)THEN	!IS IT END OF DAY
			GOTO 900	!NO
		ELSE
			CALL	LIB$DATE_TIME(DATETIME)
C
			EOFD=1	!INDICATE END OF DAY
C
			WRITE	(1,750,ERR=900)DATETIME
750			FORMAT	(/' ',1X,'24 HR  EXPANSION TANK LEVEL REPORT  ',
	1			A,/,' ',11X,'START',8X,'END',2X,
	1			'GAIN/LOSS'/)
C
			DIFF=MINLVL(1)-LAST24(1)
C
			WRITE	(1,720,ERR=900)LAST24(1),MINLVL(1),DIFF
C
			DIFF=MINLVL(2)-LAST24(2)
C
			WRITE	(1,730,ERR=900)LAST24(2),MINLVL(2),DIFF
C
			LAST24(1)=MINLVL(1)	!SAVE START OF
			LAST24(2)=MINLVL(2)	!DAY TANK LEVELS
C
			SCNTR=2	!RESET DAY COUNTER
C
		OPEN	(UNIT=2,FILE=FILEX,FORM='UNFORMATTED',
	1	 	SHARED,ACCESS='DIRECT',STATUS='OLD',
	1	 	RECL=2,RECORDTYPE='FIXED',USEROPEN=UFO_OPEN)
C
		END IF
C
		READ	(2,REC=60)AT	!24 AVG TEMPERATURE
		READ	(2,REC=240)SMCF	!24 GAS CONSUMPTION
C
		CLOSE	(UNIT=2)
C
		IF (AT(2) .EQ. 0)THEN
			AT(1)=0
		ELSE
			AT(1)=AT(1)/AT(2)	!CALC AVG TEMPERATURE
		END IF
C
		DD=65-AT(1)			!CALC DEGREE DAYS
C
		IF (DD .EQ. 0)THEN
			MD=0
		ELSE
			MD=SMCF(1)/DD		!CALC SMCF PER DEG DAY
		END IF
C
		WRITE	(1,760,ERR=900)AT(1),DD,SMCF(1),MD
760		FORMAT	(/' ',1X,'AVERAGE TEMPERATURE',F9.1,
	1		 /' ',1X,'DEGREE DAY         ',F9.1,
	1		 /' ',1X,'TOTAL SMCF CONSUMED',F9.1,
	1		 /' ',1X,'SMCF/DEGREE DAY    ',F9.1/)
C
		END IF
C
900	CONTINUE
C
	CLOSE	(UNIT=1)	!CLOSE LOGGER
C
1000	CONTINUE
C
C	WRITE FILE OUT FOR NEXT RUN
C
	WRITE	(3,REC=1)DATA,FLAG,ACNTR,HCNTR,SCNTR,MINAL,MINOBS,
	1		 MINLVL,PRELVL,LASTHR,AGALS,LAST8,LAST24
C
	CLOSE	(UNIT=3)
C
	OPEN	(UNIT=9,FILE='UTIL_REMOTE:REMOTE.DAT',
	1	STATUS='UNKNOWN',
	1	ACCESS='DIRECT',FORM='UNFORMATTED',RECL=1,SHARED,
	1	USEROPEN=UFO_OPEN,RECORDTYPE='FIXED')
C
	DO 1200 I=1,10
C
		READ	(9,REC=I)VALU	!READ IN REMOTE POINTS
		ANVAL(STUFF(I))=VALU  	!PUT IN ANVAL TABLE FOR SCANNER
		M=(STUFF(I)/32)+1
		N=MOD(STUFF(I),32)        !SET GOODPOINT FOR ALL TEN
		GPT(M)=JIBSET(GPT(M),N)	  !POINTS
C
1200	CONTINUE
C
	CLOSE	(UNIT=9)
C
	CALL	SYS$HIBER()
C
	IF (EOFD .EQ. 1)THEN	!IF END OF DAY
C
		CALL	SYS$CANWAK(,NAMEX)
C
		GOTO 150	!RESET TIMERS
C
	END IF
C
	GOTO 500
C
99999	CONTINUE
C
	CALL	EXIT
C
	END
