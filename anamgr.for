C
	PROGRAM ANAMGR
C
C	THIS PROGRAM MANAGES THE ANALOG DATABASE
C	FOR THE ANASCN PROGRAM.
C
	IMPLICIT NONE
C
	INTEGER*4	PBID,VDID,KBID,VD(13)
C
	INTEGER*4	RW(13)/1,2,2,2,2,3,7,5,2,2,2,2,4/
	INTEGER*4	CL(13)/10,12,13,10,10,15,
	1		       15,13,12,30,10,8,60/
	INTEGER*4	RWX(13)/2,4,4,4,4,9,
	1			9,9,9,18,18,18,21/
	INTEGER*4	CLX(13)/31,2,21,41,56,2,
	1		        21,41,56,2,41,56,2/
	INTEGER*4	CHK(12)/1,1,1,1,1,2,6,4,1,1,1,1/
	INTEGER*4	SROW/10/,SCOL/36/,BORDER/1/
	INTEGER*4	ROWX/7/,COLX/23/,VIDEO/2/
	INTEGER*4	DELTAR/8/,DELTAC/16/
	INTEGER*4	PT,ET,SC,RC,ACPT(6),INTC(4)
	INTEGER*4	POINT,I,J,K,L,M,N
	INTEGER*4	DELR/1/,DELC/0/,VALU
C
	INTEGER*2	RARROW/277/,LARROW/276/,PF1KEY/256/,
	1		KEYSTROKE,UARROW/274/,DARROW/275/
C
	REAL*4		RLC,LL,HL,VALUX
C
	CHARACTER*8	PN
	CHARACTER*6	EU,INTCX(4)
	CHARACTER*30	ED
	CHARACTER*4	ACPTX(6),RCX(6)
	CHARACTER*1	PTX,ETX,SCX,CHARX
	CHARACTER*5	LABEL/'PNT  '/
	CHARACTER*2	LABL/'C '/
	CHARACTER*9	HLX,LLX
	CHARACTER*12	RLCX
	CHARACTER*30	INPUT
C
	CHARACTER*4	POINTX
C
	EXTERNAL	UFO_OPEN
C
	OPEN	(UNIT=2,FILE='ANA:ANAMOD.DAT',FORM='UNFORMATTED',
	1	 ACCESS='DIRECT',STATUS='OLD',RECL=20,SHARED,
	1	 RECORDTYPE='FIXED',USEROPEN=UFO_OPEN)
C
	OPEN	(UNIT=3,FILE='ANA:ANAPHR.DAT',FORM='FORMATTED',
	1	 ACCESS='DIRECT',STATUS='OLD',RECL=44,SHARED,
	1	 RECORDTYPE='FIXED',USEROPEN=UFO_OPEN)
C
	CALL	SMG$CREATE_PASTEBOARD(PBID,,,)
	CALL	SMG$CREATE_VIRTUAL_KEYBOARD(KBID)
C
	CALL	SMG$CREATE_VIRTUAL_DISPLAY(SROW,
	1		SCOL,VDID,BORDER,,)
C
50	CONTINUE
C
	CALL	SMG$CREATE_PASTEBOARD(PBID,,,)
C
	CALL	SMG$PASTE_VIRTUAL_DISPLAY(VDID,PBID,ROWX,COLX,)
C
	CALL	SMG$PUT_CHARS(VDID,'ANALOG SCANNER',2,11)
	CALL	SMG$PUT_CHARS(VDID,'DATABASE MANAGER',3,10)
	CALL	SMG$PUT_CHARS(VDID,'TO VIEW OR MODIFY AN EXISTING',
	1			5,3)
	CALL	SMG$PUT_CHARS(VDID,'POINT PLEASE INPUT POINT NUMBER',
	1			6,3)
	CALL	SMG$PUT_CHARS(VDID,'AND PUSH <RET>',7,3)
100	CONTINUE
	CALL	SMG$PUT_CHARS(VDID,'    ',9,17,,VIDEO)
C
	CALL	SMG$HOME_CURSOR(VDID,)
	CALL	SMG$SET_CURSOR_REL(VDID,DELTAR,DELTAC)
	CALL	SMG$READ_STRING(KBID,INPUT,,,,,,,,VDID,,VIDEO,)
C
	IF (INPUT(1:4) .EQ. 'EXIT')GOTO 99999
C
	CALL	OTS$CVT_TI_L(INPUT,POINT,,%VAL(1))
	IF ((POINT .LT.1).OR.(POINT .GT. 1200))THEN
		CALL	SMG$PUT_CHARS(VDID,'INVALID POINT...TRY AGAIN',
	1			10,5)
		GOTO 100
	END IF
C
	READ	(2,REC=POINT)PT,ET,SC,RC,LL,HL,ACPT,INTC,RLC
	READ	(3,150,REC=POINT)PN,ED,EU
150	FORMAT	(A,A,A)
C
	CALL	SMG$UNPASTE_VIRTUAL_DISPLAY(VDID,PBID)
C
	DO 200 I=1,13
		CALL	SMG$CREATE_VIRTUAL_DISPLAY(RW(I),CL(I),
	1		VD(I),,,)
200	CONTINUE
C
	CALL	OTS$CVT_L_TU(POINT,POINTX)
	CALL	OTS$CVT_L_TU(ET,ETX)
	CALL	OTS$CVT_L_TU(PT,PTX)
	CALL	OTS$CVT_L_TU(RC,RCX)
	CALL	OTS$CVT_L_TU(SC,SCX)
	WRITE	(HLX,'(F9.2)')HL
	WRITE	(LLX,'(F9.2)')LL
	WRITE	(RLCX,'(F12.4)')RLC
C
	DO 220 I=1,6
		IF (ACPT(I) .EQ. 0)THEN
			ACPTX(I)='    '
			GOTO 220
		END IF
C
		CALL	OTS$CVT_L_TU(ACPT(I),ACPTX(I))
220	CONTINUE
C
	DO 230 I=1,4
		IF (INTC(I) .EQ. 0)THEN
			INTCX(I)='      '
			GOTO 230
		END IF
C
		CALL	OTS$CVT_L_TU(INTC(I),INTCX(I))
230	CONTINUE
C
	CALL	SMG$PUT_CHARS(VD(1),'POINT')
	CALL	SMG$PUT_CHARS(VD(1),POINTX,1,7)
C
	CALL	SMG$PUT_CHARS(VD(2),'POINT TYPE')
	CALL	SMG$PUT_CHARS(VD(2),PTX,2,1,,VIDEO)
	CALL	SMG$PUT_CHARS(VD(2),'(0,1,2,3)',2,3)
C
	CALL	SMG$PUT_CHARS(VD(3),'EQUATION TYPE')
	CALL	SMG$PUT_CHARS(VD(3),ETX,2,1,,VIDEO)
	CALL	SMG$PUT_CHARS(VD(3),'(0 THRU 8)',2,3)
C
	CALL	SMG$PUT_CHARS(VD(4),'SCAN RATE')
	CALL	SMG$PUT_CHARS(VD(4),SCX,2,1,,VIDEO)
	CALL	SMG$PUT_CHARS(VD(4),'(0,1)',2,3)
C
	CALL	SMG$PUT_CHARS(VD(5),'RELATED CX')
	CALL	SMG$PUT_CHARS(VD(5),RCX,2,1,,VIDEO)
C
	CALL	SMG$PUT_CHARS(VD(6),'ALARM LIMITS')
	CALL	SMG$PUT_CHARS(VD(6),LLX,2,1,,VIDEO)
	CALL	SMG$PUT_CHARS(VD(6),'LO',2,11)
	CALL	SMG$PUT_CHARS(VD(6),HLX,3,1,,VIDEO)
	CALL	SMG$PUT_CHARS(VD(6),'HI',3,11)
C
	CALL	SMG$PUT_CHARS(VD(7),'ANA/CX POINTS')
	CALL	SMG$PUT_CHARS(VD(8),'INTEGER CONST')
C
	CALL	SMG$PUT_CHARS(VD(9),'REAL CONST')
	CALL	SMG$PUT_CHARS(VD(9),RLCX,2,1,,VIDEO)
C
	CALL	SMG$PUT_CHARS(VD(10),'ENGLISH DESCRIPTION')
	CALL	SMG$PUT_CHARS(VD(10),ED,2,1,,VIDEO)
C
	CALL	SMG$PUT_CHARS(VD(11),'ENG UNITS')
	CALL	SMG$PUT_CHARS(VD(11),EU,2,1,,VIDEO)
C
	CALL	SMG$PUT_CHARS(VD(12),'WDPF ID')
	CALL	SMG$PUT_CHARS(VD(12),PN,2,1,,VIDEO)
C
	DO 250 I=1,6
		CALL	SMG$PUT_CHARS(VD(7),ACPTX(I),I+1,1,,VIDEO)
		CALL	OTS$CVT_L_TU(I,LABEL(5:5))
		CALL	SMG$PUT_CHARS(VD(7),LABEL,I+1,6)
250	CONTINUE
C
	DO 260	I=1,4
		CALL	SMG$PUT_CHARS(VD(8),INTCX(I),I+1,1,,VIDEO)
		CALL	OTS$CVT_L_TU(I,LABL(2:2))
		CALL	SMG$PUT_CHARS(VD(8),LABL,I+1,8)
260	CONTINUE
C
	CALL	SMG$PUT_CHARS(VD(13),'LEFT  ARROW - PREVIOUS FIELD',
	1				1,1)
	CALL	SMG$PUT_CHARS(VD(13),'PF1 - CHANGE POINT',1,35)
	CALL	SMG$PUT_CHARS(VD(13),'RIGHT ARROW - NEXT FIELD',
	1				2,1)
	CALL	SMG$PUT_CHARS(VD(13),'UP    ARROW - UP WITHIN FIELD',
	1				3,1)
	CALL	SMG$PUT_CHARS(VD(13),'DOWN  ARROW - DOWN WITHIN FIELD',
	1				4,1)
C
	DO 300	I=1,13
		CALL	SMG$PASTE_VIRTUAL_DISPLAY(VD(I),
	1		 PBID,RWX(I),CLX(I),)
300	CONTINUE
C
	I=2
400	CONTINUE
	IF (I .GT. 12)I=2
	IF (I .LT. 2)I=12
C
425	CONTINUE
C
	CALL	SMG$HOME_CURSOR(VD(I),)
	CALL	SMG$SET_CURSOR_REL(VD(I),DELR,DELC)
	INPUT='                    '
	CALL	SMG$READ_KEYSTROKE(KBID,KEYSTROKE)
	IF (KEYSTROKE .EQ. RARROW)THEN
		I=I+1
		DELR=1
		DELC=0
		GOTO 400
	END IF
C
	IF (KEYSTROKE .EQ. LARROW)THEN
		I=I-1
		DELR=1
		DELC=0
		GOTO 400
	END IF
C
	IF (KEYSTROKE .EQ. DARROW)THEN
		IF (CHK(I) .EQ. 1)GOTO 425
		DELR=DELR+1
		IF (DELR .GT. CHK(I))DELR=1
		GOTO 425
	END IF
C
	IF (KEYSTROKE .EQ. UARROW)THEN
		IF (CHK(I) .EQ. 1)GOTO 425
		DELR=DELR-1
		IF (DELR .LT. 1)DELR=1
		GOTO 425
	END IF
C				
	IF (KEYSTROKE .EQ. PF1KEY)THEN
		CALL	SMG$DELETE_PASTEBOARD(PBID)
		GOTO 50
	END IF
C
	CHARX=CHAR(KEYSTROKE)
	INPUT(1:1)=CHARX
	INPUT(2:30)='                             '
	CALL	SMG$READ_STRING(KBID,INPUT(2:30),CHARX,,,,,,,VD(I),,VIDEO,)
C
	IF (I .EQ. 12)THEN
		PN=INPUT(1:8)
		CALL	SMG$PUT_CHARS(VD(12),PN,2,1,,VIDEO)
		GOTO 10000
	END IF
C
	IF (I .EQ. 11)THEN
		EU=INPUT(1:6)
		CALL	SMG$PUT_CHARS(VD(11),EU,2,1,,VIDEO)
		GOTO 10000
	END IF
C
	IF (I .EQ. 10)THEN
		ED=INPUT
		CALL	SMG$PUT_CHARS(VD(10),ED,2,1,,VIDEO)
		GOTO 10000
	END IF
C
	IF (I .EQ. 2)THEN
		CALL	OTS$CVT_TI_L(INPUT,VALU,,%VAL(1))
		PT=VALU
		CALL	OTS$CVT_L_TU(PT,PTX)
		CALL	SMG$PUT_CHARS(VD(2),PTX,2,1,,VIDEO)
		GOTO 11000
	END IF
C
	IF (I .EQ. 3)THEN
		CALL	OTS$CVT_TI_L(INPUT,VALU,,%VAL(1))
		ET=VALU
		CALL	OTS$CVT_L_TU(ET,ETX)
		CALL	SMG$PUT_CHARS(VD(3),ETX,2,1,,VIDEO)
		GOTO 11000
	END IF
C
	IF (I .EQ. 4)THEN
		CALL	OTS$CVT_TI_L(INPUT,VALU,,%VAL(1))
		SC=VALU
		CALL	OTS$CVT_L_TU(SC,SCX)
		CALL	SMG$PUT_CHARS(VD(4),SCX,2,1,,VIDEO)
		GOTO 11000
	END IF
C
	IF (I .EQ. 5)THEN
		CALL	OTS$CVT_TI_L(INPUT,VALU,,%VAL(1))
		RC=VALU
		CALL	OTS$CVT_L_TU(RC,RCX)
		CALL	SMG$PUT_CHARS(VD(5),RCX,2,1,,VIDEO)
		GOTO 11000
	END IF
C
	IF (I .EQ. 6)THEN
		READ (INPUT,'(BN,F10.0)')VALUX
		IF (DELR .EQ. 1)THEN
			DELR=2
			LL=VALUX
			WRITE	(LLX,'(F9.2)')LL
			CALL	SMG$PUT_CHARS(VD(6),LLX,2,1,,VIDEO)
			GOTO 12000
		ELSE
			HL=VALUX
			WRITE	(HLX,'(F9.2)')HL
			CALL	SMG$PUT_CHARS(VD(6),HLX,3,1,,VIDEO)
			DELR=1
			GOTO 11000
		END IF
	END IF
C
	IF (I .EQ. 7)THEN
		CALL	OTS$CVT_TI_L(INPUT,ACPT(DELR),,%VAL(1))
		CALL	OTS$CVT_L_TU(ACPT(DELR),ACPTX(DELR))
		CALL	SMG$PUT_CHARS(VD(7),ACPTX(DELR),
	1				DELR+1,1,,VIDEO)
		DELR=DELR+1
		IF (DELR .GT. 6)THEN
			DELR=1
			GOTO 11000
		ELSE
			GOTO 12000
		END IF
	END IF
C
	IF (I .EQ. 8)THEN
		CALL	OTS$CVT_TI_L(INPUT,INTC(DELR),,%VAL(1))
		CALL	OTS$CVT_L_TU(INTC(DELR),INTCX(DELR))
		CALL	SMG$PUT_CHARS(VD(8),INTCX(DELR),
	1				DELR+1,1,,VIDEO)
		DELR=DELR+1
		IF (DELR .GT. 4)THEN
			DELR=1
			GOTO 11000
		ELSE
			GOTO 12000
		END IF
	END IF
C
	IF (I .EQ. 9)THEN
		READ	(INPUT,'(BN,F12.0)')VALUX
		RLC=VALUX
		WRITE	(RLCX,'(F12.4)')RLC
		CALL	SMG$PUT_CHARS(VD(9),RLCX,2,1,,VIDEO)
		GOTO 11000
	END IF
C
	I=I+1
	GOTO 400
C
10000	CONTINUE
C
	WRITE	(3,150,REC=POINT)PN,ED,EU
	I=I+1
	GOTO 400
C
11000	CONTINUE
C
	I=I+1
C
12000	CONTINUE
C
	WRITE	(2,REC=POINT)PT,ET,SC,RC,LL,HL,ACPT,INTC,RLC
	GOTO 400
C
99999	CONTINUE
	CALL	EXIT
	END
