CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C	CONFIDENTIAL
C	Property of United States Steel Corporation
C	Copyright 2007 United States Steel Corporation
C	All rights reserved.
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
	PROGRAM		GCP15M	!MINNTAC GCP 15 MINUTE SCANNER
C
C	GOOD COMBUSTION PLAN
C
C	FORT/ALIGN=NONE/WARN=NOALIGN/FLOAT=D_FLOAT GCP15M
C
C	LINK GCP15M,GCP15M/OPT
C
C	EACH 15 MINUTE RUN THE PROGRAM WILL SHIP A RECORD TO THE 
C	PLANT ORACLE DATABASE FOR AGGLOMERATOR LINE (3 THRU 7)
C	WHICH WILL CONSIST OF THE FOLLOWING COLUMNS:
C
C	1.	LINE NUMBER
C	3.	READING TIMESTAMP
C	4.	PROCESS VALUE BURN ZONE TEMPERATURE
C	5.	PROCESS VALUE KILN EXIT TEMPERATURE
C	5.	PROCESS VALUE BACKUP KILN EXIT TEMPERATURE (LINES 4,5 ONLY)
C	6.	EMISSION UNITS INDICATOR
C
C	REVISION HISTORY
C
C	SERVICE CENTER TICKET NUMBER:  PM735952
C	PLANVIEW ID NUMBER:            55352
C	PROGRAM AUTHOR:	BILL BABICH
C
	IMPLICIT NONE
C
	STRUCTURE /GCP_DATA/	!LINE DATA
C
		INTEGER*4	LINE		!AGGL LINE NUMBER
		CHARACTER*23   	DATEX		!PROCESS READINGS DATE
		REAL*4		BZT		!BURN ZONE TEMP
		REAL*4		KET		!KILN EXIT TEMP
		REAL*4		BKET		!BACKUP KILN EXIT TEMP
		INTEGER*4	EU_FLAG		!EU RUN INDICATION
C
	END STRUCTURE
C
	RECORD/GCP_DATA/GCPX
C
	INTEGER*4	ADRS(2),TEMP
	INTEGER*4	UFO_OPEN,I,J,K,L,M,N
	INTEGER*4	MIN,SEC,HRS
	INTEGER*4	CXSTS(100)
	INTEGER*4	CXSTS_AG3(100)
	INTEGER*4	FLAG(5),RND,RNDX
	INTEGER*4	BZ(5)/335,92,208,678,290/
	INTEGER*4	KE(5)/820,94,210,110,278/
	INTEGER*4	BKE(5)/0,371,372,0,0/
C
	REAL*4		ANVAL(1200)
	REAL*4		ANVAL_AG3(1000)
	REAL*4		VALU,SECF,RSECS
C
	CHARACTER*9	TIM/'0 0:15:00'/	!15 MIN RUN TIME
	CHARACTER*23	DATETIME
C
	EXTERNAL	UFO_OPEN
C
	COMMON/ANVALTBL/ANVAL
	COMMON/AG3_ANVALTBL/ANVAL_AG3
C
	CALL	LIB$DATE_TIME(DATETIME)	!GET CURRENT TIME
	READ	(DATETIME(16:17),'(I2)')MIN	!CONVERT MINUTES
	READ	(DATETIME(19:20),'(I2)')SEC	!CONVERT SECONDS
	I=MIN/15
	I=(I*15)-1
	IF (MIN .GE. 15)MIN=MIN-I-1
	IF (MIN .LT. 15)MIN=MIN+1
	SEC=60-SEC
	SEC=SEC+((15-MIN)*60)
	SECF=SEC	!FLOATING POINT SECONDS
	CALL	LIB$WAIT(SECF)	!WAIT TILL BEGINNING OF NEXT 15 MIN INTERVAL
C
	CALL	SYS$BINTIM(TIM,ADRS)
	CALL	SYS$SCHDWK(,,ADRS,ADRS)	!SCHEDULE 15 MINUTE WAKEUP
C
100	CONTINUE
C
	OPEN	(UNIT=1,FILE='ANA:GCP_EU.DAT',STATUS='OLD',
	1	ACCESS='DIRECT',RECL=5,RECORDTYPE='FIXED',
	1	FORM='UNFORMATTED',SHARED,USEROPEN=UFO_OPEN)
C
	READ	(1,REC=1)FLAG	!READ IN INDICATIONS FOR AGGLOMERATOR LINES
C
	CLOSE	(UNIT=1)
C
	CALL	LIB$DATE_TIME(DATETIME)	!GET CURRENT TIME
C
	OPEN	(UNIT=2,FILE='ANA:GCP15M.DAT',STATUS='OLD',
	1	ACCESS='DIRECT',RECL=11,RECORDTYPE='FIXED',
	1	FORM='UNFORMATTED',SHARED,USEROPEN=UFO_OPEN)
C
	DO 666	I=1,5 	!PROCESS ALL AGGLOMERATOR LINES
C
		READ	(2,REC=I)GCPX	!BRING IN AGGL LINE DATA
C
		GCPX.EU_FLAG=0
C
		GCPX.DATEX='                       '
C
		IF (FLAG(I) .GE. 540)THEN !HAS LINE RUN FOR 90 MIN
			GCPX.EU_FLAG=1	!YES
		END IF
C
		IF (I .LE. 3)THEN	!STEP 1/2 AGGLOMERATOR LINE
			GCPX.BZT=ANVAL(BZ(I))	!GRAB BURN ZONE TEMP
			GCPX.KET=ANVAL(KE(I))	!GRAB KILN EXIT TEMP
			IF (BKE(I) .EQ. 0)THEN
				GCPX.BKET=0	!NO BACKUP KE
			ELSE
				GCPX.BKET=ANVAL(BKE(I)) !GRAB BACKUP KE
			END IF
		END IF
C
		IF (I .GE. 4)THEN	!STEP 3 AGGLOMERATOR LINE
			GCPX.BZT=ANVAL_AG3(BZ(I))	!GRAB BURN ZONE TEMP
			GCPX.KET=ANVAL_AG3(KE(I))	!GRAB KILN EXIT TEMP
			GCPX.BKET=0		!NO BACKUP KE
		END IF
C
		GCPX.DATEX=DATETIME	!RECORD TIME AND DATE
C
		WRITE	(2,REC=I)GCPX	!SAVE AGGL LINE DATA
C
666	CONTINUE
C
	CLOSE	(UNIT=2)	!CLOSE 15 MINUTE DATA FILE
C
999	CONTINUE
C
	CALL	SYS$HIBER()	!DELAY TILL WAKEUP
C
	GOTO 100
C
	END
C
