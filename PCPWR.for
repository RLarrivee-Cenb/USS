C
	PROGRAM		PCPWR	!(PRIMARY CRUSHER POWER)
C
C	PROGRAM RUNS EVERY 10 SECONDS AND COLLECTS THE NUMBERS 
C	NEEDED TO PRINT THE PRIMARY CRUSHER POWER REPORT
C
C	LINK PCPWR,PCPWR/OPT
C
	IMPLICIT NONE
C
	INCLUDE	'($PRCDEF)'
C
	REAL*4		WHR(4),EHR(4),WHROB(4),EHROB(4),
	1		CRSHX(4),IDLEX(4),CSHOB(4),IDLOB(4),
	1		CRSH(4),IDLE(4),CRSHOB(4),IDLEOB(4),
	1		WIMBAL(4),EIMBAL(4),WIOB(4),EIOB(4),
	1		W8(4),E8(4),W8OB(4),E8OB(4),
	1		CRSH8(4),IDLE8(4),C8OB(4),I8OB(4),
	1		W24(4),E24(4),W24OB(4),E24OB(4),
	1		CRSH24(4),IDLE24(4),C24OB(4),I24OB(4)
C
	REAL*4		RSECS,ANVAL(1000),VALU
	INTEGER*4	ADRS(2),GPT(40),CXSTS(100)
	INTEGER*4	SECS,TEMP,REM,QUO,HORCNT
	INTEGER*4	I,J,K,L,N,M,MARKER(4)
	INTEGER*4	MCX(4)/277,317,177,220/
	INTEGER*4	NWPWR(4)/128,190,246,286/
	INTEGER*4	NEPWR(4)/89,159,245,285/
	INTEGER*4	IDLEXX(4)/27,36,15,18/
	INTEGER*4	BIT,BITX,CNTR,LOGQUEX(2)
	INTEGER*4	PM1030/81000/,
	1		PM2400/86400/
	INTEGER*4	UFO_OPEN
C
	CHARACTER*8	TIM/'0 0:0:10'/
	CHARACTER*12	FILEX/'GP:PCPWR.DAT'/
	CHARACTER*9	PRGNAM/'GP:PRIPWR'/	!LOG PROGRAM
	CHARACTER*6	PRCNAM/'PRIPWR'/
	CHARACTER*23	DATETIME
C
	EXTERNAL	UFO_OPEN
C
	COMMON/CRS_ANVALTBL/ANVAL
	COMMON/CRS_GOODPTTBL/GPT
	COMMON/CONTACTSCRS/CXSTS
	COMMON/CRS_LOGQUE/LOGQUEX
C
	RSECS=SECNDS(0.0)			!CALCULATE AND SET UP
	SECS=RSECS				!THE NUMBER OF 10 SEC
	TEMP=SECS-PM1030			!SCANS LEFT IN THE HOUR
	IF (TEMP .LT. 0)TEMP=TEMP+PM2400	
	REM=MOD(TEMP,3600)
	QUO=REM/10
	QUO=NOT(QUO)
	HORCNT=JIBITS(QUO,0,9)
	HORCNT=HORCNT-143
C
	CNTR=0
C
	CALL	SYS$BINTIM(TIM,ADRS)
	CALL	SYS$SCHDWK(,,ADRS,ADRS)	!SCHEDULE 10 SEC WAKEUP
C
200	CONTINUE
C
	OPEN	(UNIT=3,NAME=FILEX,FORM='UNFORMATTED',SHARED,
	1	ACCESS='DIRECT',STATUS='OLD',RECL=128,
	1	RECORDTYPE='FIXED',USEROPEN=UFO_OPEN)
C
	READ	(3,REC=1)WHR,EHR,WHROB,EHROB,
	1		CRSHX,IDLEX,CSHOB,IDLOB,
	1		CRSH,IDLE,CRSHOB,IDLEOB,
	1		WIMBAL,EIMBAL,WIOB,EIOB,
	1		W8,E8,W8OB,E8OB,
	1		CRSH8,IDLE8,C8OB,I8OB,
	1		W24,E24,W24OB,E24OB,
	1		CRSH24,IDLE24,C24OB,I24OB
C
	MARKER(1)=0
	MARKER(2)=0
	MARKER(3)=0
	MARKER(4)=0
C
	IF (HORCNT .EQ. 360)THEN	!IF NEW HOUR
C
	DO 6000	I=1,4
		WHR(I)=0	!CLEAR HOUR FILE
		EHR(I)=0
		WHROB(I)=0
		EHROB(I)=0
		CRSHX(I)=0
		IDLEX(I)=0
		CSHOB(I)=0
		IDLOB(I)=0
6000	CONTINUE
C
	BIT=JIBITS(LOGQUEX(1),1,1)	!EXTRACT SHIFT BIT
C
	IF (BIT .EQ. 1)THEN
C
	DO 7000	I=1,4
		W8(I)=0	!CLEAR SHIFT FILE
		E8(I)=0
		W8OB(I)=0
		E8OB(I)=0
		CRSH8(I)=0
		IDLE8(I)=0
		C8OB(I)=0
		I8OB(I)=0
7000	CONTINUE
C
	END IF
C
	BIT=JIBITS(LOGQUEX(1),2,1)	!EXTRACT SHIFT BIT
C
	IF (BIT .EQ. 1)THEN
C
	DO 8000	I=1,4
		W24(I)=0	!CLEAR DAY FILE
		E24(I)=0
		W24OB(I)=0
		E24OB(I)=0
		CRSH24(I)=0
		IDLE24(I)=0
		C24OB(I)=0
		I24OB(I)=0
8000	CONTINUE
C
	END IF
C
	END IF
C
	DO 800	I=1,4	!PROCESS 4 PRIMARY CRUSHERS
C
		L=(MCX(I)/32)+1	!EXTRACT CXSTS LONGWORD
		M=MOD(MCX(I),32)	!EXTRACT CORRECT BIT
		BIT=JIBITS(CXSTS(L),M,1)	!EXTRACT BIT CONDITION
C
		IF (BIT .EQ. 0)THEN		!M-CX OPEN (CRUSHER OFF)
			MARKER(I)=1		!INDICATE CRUSHER DOWN
			CRSH(I)=0
			CRSHOB(I)=0
			IDLE(I)=0
			IDLEOB(I)=0
			GOTO 800		!SKIP TO NEXT CRUSHER
		END IF
C
		K=(NWPWR(I)/32)+1		!CHECK IF WEST MOTOR
		L=MOD(NWPWR(I),32)                   !AMPS ARE OK
		BIT=JIBITS(GPT(K),L,1)
		IF (BIT .EQ. 0)GOTO 400		!SKIP TO EAST MOTOR
C
		VALU=ANVAL(NWPWR(I))		!GRAB AMPS
C
		WHR(I)=WHR(I)+VALU
		W8(I)=W8(I)+VALU
		W24(I)=W24(I)+VALU
		WHROB(I)=WHROB(I)+1
		W8OB(I)=W8OB(I)+1
		W24OB(I)=W24OB(I)+1
		WIMBAL(I)=WIMBAL(I)+VALU
		WIOB(I)=WIOB(I)+1
C
		L=(IDLEXX(I)/32)+1	!EXTRACT CXSTS LONGWORD
		M=MOD(IDLEXX(I),32)	!EXTRACT CORRECT BIT
		BITX=JIBITS(CXSTS(L),M,1)	!EXTRACT BIT CONDITION
C
		IF (BITX .EQ. 0)THEN	!IF CRUSHER IS IDLING
			IDLEOB(I)=IDLEOB(I)+1
			IF (IDLEOB(I) .GE. 7)IDLE(I)=IDLE(I)+VALU
		ELSE
			CRSHX(I)=CRSHX(I)+VALU
			CRSH8(I)=CRSH8(I)+VALU
			CRSH24(I)=CRSH24(I)+VALU
			CRSH(I)=CRSH(I)+VALU
			CSHOB(I)=CSHOB(I)+1
			C8OB(I)=C8OB(I)+1
			C24OB(I)=C24OB(I)+1
			CRSHOB(I)=CRSHOB(I)+1
			IDLE(I)=0
			IDLEOB(I)=0
		END IF
C
400		CONTINUE
C
		K=(NEPWR(I)/32)+1		!CHECK IF EAST MOTOR
		L=MOD(NEPWR(I),32)                   !AMPS ARE OK
		BIT=JIBITS(GPT(K),L,1)
		IF (BIT .EQ. 0)GOTO 800		!SKIP TO NEXT CRUSHER
C
		VALU=ANVAL(NEPWR(I))		!GRAB AMPS
C
		EHR(I)=EHR(I)+VALU
		E8(I)=E8(I)+VALU
		E24(I)=E24(I)+VALU
		EHROB(I)=EHROB(I)+1
		E8OB(I)=E8OB(I)+1
		E24OB(I)=E24OB(I)+1
		EIMBAL(I)=EIMBAL(I)+VALU
		EIOB(I)=EIOB(I)+1
C
		IF (BITX .EQ. 0)THEN	!IF CRUSHER IS IDLING
			IF (IDLEOB(I) .GE. 7)IDLE(I)=IDLE(I)+VALU
		ELSE
			CRSHX(I)=CRSHX(I)+VALU
			CRSH8(I)=CRSH8(I)+VALU
			CRSH24(I)=CRSH24(I)+VALU
			CRSH(I)=CRSH(I)+VALU
		END IF
C
800	CONTINUE
C
	DO 1000	I=1,4	!FOR EACH PRIMARY CRUSHER
C
		IF (MARKER(I) .EQ. 1)GOTO 1000	!CRUSHER IS DOWN
C
		CRSH(I)=0
		CRSHOB(I)=0
C
		IF (IDLEOB(I) .LT. 9)GOTO 1000	!NEED MINIMUM IDLE SCANS
C
		IDLEOB(I)=IDLEOB(I)-6
C
		IDLEX(I)=IDLEX(I)+IDLE(I)	!ADD ACCUM TO HOURLY ACCUM
		IDLE8(I)=IDLE8(I)+IDLE(I)	!ADD ACCUM TO SHIFT ACCUM
		IDLE24(I)=IDLE24(I)+IDLE(I)	!ADD ACCUM TO DAY ACCUM
		IDLOB(I)=IDLOB(I)+IDLEOB(I)	
		I8OB(I)=I8OB(I)+IDLEOB(I)
		I24OB(I)=I24OB(I)+IDLEOB(I)
C
		IDLE(I)=0
		IDLEOB(I)=0
C
1000	CONTINUE
C
	CNTR=CNTR+1	!BUMP IMBALANCE COUNTER
C
	IF (CNTR .GE. 30)THEN
		DO 1100	I=1,4
			WIMBAL(I)=0
			EIMBAL(I)=0
			WIOB(I)=0
			EIOB(I)=0
1100		CONTINUE
C
		CNTR=0
	END IF
C
	WRITE	(3,REC=1)WHR,EHR,WHROB,EHROB,
	1		CRSHX,IDLEX,CSHOB,IDLOB,
	1		CRSH,IDLE,CRSHOB,IDLEOB,
	1		WIMBAL,EIMBAL,WIOB,EIOB,
	1		W8,E8,W8OB,E8OB,
	1		CRSH8,IDLE8,C8OB,I8OB,
	1		W24,E24,W24OB,E24OB,
	1		CRSH24,IDLE24,C24OB,I24OB
C
	CLOSE	(UNIT=3)
C
	HORCNT=HORCNT-1
C
	IF (HORCNT .LE. 0)THEN
		HORCNT=360
C
		CALL	SYS$CREPRC(,PRGNAM,,,,,,PRCNAM,%VAL(4),,,
	1		%VAL(PRC$M_DETACH.OR.PRC$M_NOACNT))
C
	END IF
C
	CALL	SYS$HIBER()	!DELAY TILL WAKEUP
	GOTO 200
C
	END
