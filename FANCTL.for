	PROGRAM FANCTLX 
C
C	(FAN CONTROL)
C
C	THIS PROGRAM RUNS EVERY 10 SECONDS AND CONTROLS THE DAMPER
C	POSITIONS OF THE PREHEAT AND 3A FANS FOR LINES 4 AND 5 AGGL
C
C	FORT/NOOP/NOALIGN/WARN=NOALIGN/FLOAT=D_FLOAT FANCTL
C
C	LINK FANCTL,FANCTL/OPT,WESAPI_LIB:WESAPI/OPT
C
	IMPLICIT NONE
C
	INCLUDE 'WESAPI:SPD.DCL'
	INCLUDE 'WESAPI:SHC_defines.DCL'
	INCLUDE 'WESAPI:SHC_err.DCL'
C
	INTEGER*4	LIB$SYS_TRNLOG
	INTEGER*4 	status
	CHARACTER*20	highway

	CHARACTER*40    spd_filename
	INTEGER*2	spd_filename_len
	INTEGER*2	spd_fd
	INTEGER*2	access_type
	INTEGER*2	c_short_val
	CHARACTER*2	field_name
	CHARACTER*8	pt_name
	BYTE		extended_flag
	BYTE		gp_bit_num
	BYTE		inactive_flag
	BYTE		c_byte_val
	INTEGER*4	gp_sid
	INTEGER*2       get_pkd_group_val
	INTEGER*2       get_pkd_group_stat
	INTEGER*2       gp_force_stat
	INTEGER*2       gp_op_mask
	INTEGER*2       gp_val_mask
C
	REAL            put_analog_val
	INTEGER*2	PUT_DIGITAL_VAL
C
	INTEGER*4	AO(6),DO
	INTEGER*4	LEN,POINT_QUALITY
C
	CHARACTER*8	TIME/'0 0:0:10'/
C
	INTEGER*4	CONTRL(2)
	REAL*4		GT1XPN(4),PHFPPN(2),IT1XPN(4),FA1XPN(4),CTEMPN(4),
	1		GT1XPO(4),PHFPPO(2),IT1XPO(4),FA1XPO(4),CTEMPO(4),
	1		GTPSET(2),OBGT1X(4),GTPRES(2),GTGAIN(2),GTLO(2),
	1		GTHI(2),COUTPT(4),CSETPT(2),CGAIN(2),GTPDER(2),
	1		CLO(2),CHI(2),PFPSET(2),OBFP1X(4),PFPRES(2),
	1		FPGAIN(2),PFPDER(2),PHFPLO(2),PHFPHI(2),INTSET(2),
	1		OBIT1X(4),INTRES(2),ITGAIN(2),INTDER(2),INTLO(2),
	1		INTHI(2),FASET(2),OBFA1X(4),FARES(2),FAGAIN(2),
	1		FADER(2),FALO(2),FAHI(2),ASETPT(2),LTPHPN(2),
	1		OBAS1X(4),OUT1X(4)
C
	REAL*4		ANVAL(1200)
	REAL*4		OB,NB,PN,PO,RESET,GAIN,DERIV,LO,HI,SET,
	1		BASES(4),OUTPTS(4),NEWC,COBAS,TEMP,THERK/0.4/,
	1		SAVE,OUTX,CUTBCK/20/,RSTART/0/,CUT/5/,
	1		SLOPE(2)/0.08,0.08/,YINTER(2)/15,15/,SETSAV
C
	INTEGER*4	TEMPX,UFO_OPEN
	INTEGER*4	GPT(50)
	INTEGER*4	I,J,K,L,M,N,ADDRESS(2),BIT,BITX
C
	INTEGER*4	ONOFCX(2)/1610,1613/,	!ON/OFF COMP CXS
	1		GRATCX(2)/362,569/,	!GRATE DRIVE CXS
	1		FN1ACX(2)/366,573/,	!FAN DRIVE CXS
	1		FN1BCX(2)/367,574/,	!FAN DRIVE CXS
	1		SETPTX(2)/580,614/,	!CONTROLLER SETPTS
	1		GT1AA(2)/97,213/,	!GRATE TEMPS
	1		GT1BA(2)/95,211/,	!GRATE TEMPS
	1		PHFPA(2)/124,241/,	!FURNACE PRESSURE
	1		FA1AA(2)/101,217/,	!FAN AMPS
	1		FA1BA(2)/113,229/,	!FAN AMPS
	1		IT1AA(2)/105,221/,	!INLET TEMPS
	1		IT1BA(2)/116,233/,	!INLET TEMPS
	1		LTPHA(2)/1036,1037/,	!TONS AT GRATE
	1		DP1AA(2)/424,458/,	!DAMPER POSITIONS
	1		DP1BA(2)/428,461/,	!DAMPER POSITIONS
	1		DRUMS(2)/1038,1039/	!DRUMS RUNNING
C
	INTEGER*4	NFFCX(2)/1612,1615/,	!3A ON/OFF COMPUTER
	1		SPT3AX(2)/577,611/,	!CONTROLLER SETPOINTS
	1		FN3A(2)/139,255/,	!3A FAN AMPS ANVAL
	1		HD3A(2)/90,206/,	!HOOD DRAFT ANVAL
	1		DP3A(2)/421,455/,	!3A CNTLLR OUTPUT ANVAL
	1		MWT(2)/77,193/,		!3A MOTOR WIND TEMP ANVAL
	1		F3ACX(2)/375,582/,	!3A DRIVE ON/OFF
	1		STKCAP(2)/274,275/	!STACK CAP POS ANVAL
C
	INTEGER*4	CNTRLX(2)
	REAL*4		FANPN(2),HDPN(2),FANPO(2),HDPO(2),
	1		FMWT(2),MWTSET(2),MWTRES(2),HAMPS(2),ASOB(2),
	1		FANSET(2),OBFAN(2),FANRES(2),FNGAIN(2),FNDER(2),
	1		FANLO(2),FANHI(2),
	1		HDSET(2),OBHD(2),HDRES(2),HDGAIN(2),HDDER(2),
	1		HDLO(2),HDHI(2),
	1		DPSET(2),OBDP(2),DPRES(2),DPGAIN(2),DPLO(2),DPHI(2),
	1		DPCALC(2),CONSTC(2),AHDSET(2),OBAS3A(2),OUT3A(2)
C
	REAL*4		L4TONS(30),L5TONS(30),ACCUM,OBS,TEMPSP
C
	CHARACTER*8	AOP(6)/'AD600006','AD600007','AD600008',
	1		       'AD600009','AD600010','AD600012'/
C
	CHARACTER*8	DOP/'DD600005'/
C
	COMMON/ANVALTBL/ANVAL
	COMMON/PHFANFILE/CONTRL,GT1XPN,PHFPPN,IT1XPN,FA1XPN,
	1		CTEMPN,GT1XPO,PHFPPO,IT1XPO,FA1XPO,
	1		CTEMPO,GTPSET,OBGT1X,GTPRES,GTGAIN,
	1		GTLO,GTHI,COUTPT,CSETPT,CGAIN,GTPDER,
	1		CLO,CHI,PFPSET,OBFP1X,PFPRES,FPGAIN,
	1		PFPDER,PHFPLO,PHFPHI,INTSET,OBIT1X,
	1		INTRES,ITGAIN,INTDER,INTLO,INTHI,
	1		FASET,OBFA1X,FARES,FAGAIN,FADER,FALO,
	1		FAHI,ASETPT,LTPHPN,OBAS1X,OUT1X
C
	COMMON/F3AFANFILE/CNTRLX,FANPN,HDPN,FANPO,HDPO,
	1		FMWT,MWTSET,MWTRES,HAMPS,ASOB,
	1		FANSET,OBFAN,FANRES,FNGAIN,FNDER,FANLO,FANHI,
	1		HDSET,OBHD,HDRES,HDGAIN,HDDER,HDLO,HDHI,
	1		DPSET,OBDP,DPRES,DPGAIN,DPLO,DPHI,DPCALC,
	1		CONSTC,AHDSET,OBAS3A,OUT3A
C
	COMMON/GOODPTTBL/GPT
	EXTERNAL	UFO_OPEN
C
5	CONTINUE
C
C	OPEN CONNECTION TO AGGL 1-2 POINT DIRECTORY
C
        status = LIB$SYS_TRNLOG('WESAPI_PDIR_0',
     1			spd_filename_len,spd_filename,,,)
C
	spd_filename(spd_filename_len+1:) = CHAR(0)
C
        access_type = RUNTIME
C
        spd_fd = SPD_open_file(%ref(spd_filename), access_type)
C
	DO 15	J=1,6	!GET THE 6 AGGL 1-2 ANALOG OUTPUT SIDS
C
	        status = SPD_get_sid(spd_fd,%ref(AOP(J)//char(0)),AO(J),
     x		             gp_sid, gp_bit_num, extended_flag, inactive_flag)

C
15	CONTINUE
C
C		GET COMPUTER ACKNOWLEDGE DO
C
	status = SPD_get_sid(spd_fd,%ref(DOP//char(0)),DO,
	1		gp_sid, gp_bit_num, extended_flag, inactive_flag)

C
C  Close the point directory connection
C
	status = SPD_close_file(spd_fd)
C
C  Open the connection to SHC memory for WESAPI access
	status = SHC_open_memory()
C
	IF (status .NE. SHC_OK) THEN
		IF (status .EQ. SHC_E_MEMOPEN)GOTO 88
C
		OPEN	(UNIT=22,FILE='CTL:FAN.ERR',STATUS='UNKNOWN',
	1		 ACCESS='APPEND')
C
		WRITE	(22,77)status
77		FORMAT	(' SHC open memory failure - error =',I4)
C
		CLOSE	(UNIT=22)
C
	   GO TO 99999
C
	ENDIF
C
88	CONTINUE
C
	OPEN	(UNIT=1,FILE='CTL:PHFAN.DAT',STATUS='OLD',
	1	RECL=126,RECORDTYPE='FIXED',FORM='UNFORMATTED',
	1	SHARED,ACCESS='DIRECT',USEROPEN=UFO_OPEN)
C
	READ	(1,REC=1)CONTRL,GT1XPN,PHFPPN,IT1XPN,FA1XPN,
	1		CTEMPN,GT1XPO,PHFPPO,IT1XPO,FA1XPO,
	1		CTEMPO,GTPSET,OBGT1X,GTPRES,GTGAIN,
	1		GTLO,GTHI,COUTPT,CSETPT,CGAIN,GTPDER,
	1		CLO,CHI,PFPSET,OBFP1X,PFPRES,FPGAIN,
	1		PFPDER,PHFPLO,PHFPHI,INTSET,OBIT1X,
	1		INTRES,ITGAIN,INTDER,INTLO,INTHI,
	1		FASET,OBFA1X,FARES,FAGAIN,FADER,FALO,
	1		FAHI,ASETPT,LTPHPN,OBAS1X,OUT1X
C
	CLOSE	(UNIT=1)
C
C
	OPEN	(UNIT=1,FILE='CTL:F3AFAN.DAT',STATUS='OLD',
	1	RECL=70,RECORDTYPE='FIXED',FORM='UNFORMATTED',
	1	SHARED,ACCESS='DIRECT',USEROPEN=UFO_OPEN)
C
	READ	(1,REC=1)CNTRLX,FANPN,HDPN,FANPO,HDPO,
	1		FMWT,MWTSET,MWTRES,HAMPS,ASOB,
	1		FANSET,OBFAN,FANRES,FNGAIN,FNDER,FANLO,FANHI,
	1		HDSET,OBHD,HDRES,HDGAIN,HDDER,HDLO,HDHI,
	1		DPSET,OBDP,DPRES,DPGAIN,DPLO,DPHI,
	1		DPCALC,CONSTC,AHDSET,OBAS3A,OUT3A
C
	CLOSE	(UNIT=1)
C
	CALL	SYS$BINTIM(TIME,ADDRESS)
	CALL	SYS$SCHDWK(,,ADDRESS,ADDRESS)
C
100	CONTINUE
C
	DO 2000	I=1,2		!LINE INDEX 1=L4...2=L5
		CALL	CHKCX(ONOFCX(I),BIT)	!SEE IF ON COMP
		IF (BIT .EQ. 1)THEN
			CONTRL(I)=JIBCLR(CONTRL(I),23)	!ON COMP
		ELSE
			CONTRL(I)=JIBSET(CONTRL(I),23)	!OFF COMP
		END IF
C
		GTPSET(I)=ANVAL(SETPTX(I))	!GET CNTRLLR SETPNT
		IF ((GTPSET(I) .LT. 850).OR.(GTPSET(I) .GT. 1000))THEN
			CONTRL(I)=JIBSET(CONTRL(I),8)	!SETPNT IS BAD
		ELSE
			CONTRL(I)=JIBCLR(CONTRL(I),8)	!SETPNT IS GOOD
		END IF
C
		IF (ANVAL(DRUMS(I)) .EQ. 4)GTPSET(I)=GTPSET(I)-10
		IF (ANVAL(DRUMS(I)) .EQ. 3)GTPSET(I)=GTPSET(I)-20
		IF (ANVAL(DRUMS(I)) .EQ. 2)GTPSET(I)=GTPSET(I)-20
		IF (ANVAL(DRUMS(I)) .EQ. 1)GTPSET(I)=GTPSET(I)-20
C
		GT1XPO(I)=GT1XPN(I)		!SWAP NEW AND OLD 
		GT1XPO(I+2)=GT1XPN(I+2)		!PROCESS READINGS
		PHFPPO(I)=PHFPPN(I)
		FA1XPO(I)=FA1XPN(I)
		FA1XPO(I+2)=FA1XPN(I+2)
		IT1XPO(I)=IT1XPN(I)
  		IT1XPO(I+2)=IT1XPN(I+2)
		CTEMPO(I)=CTEMPN(I)
		CTEMPO(I+2)=CTEMPN(I+2)
C
		GT1XPN(I)=ANVAL(GT1AA(I))	!READ NEW PROCESS VALUES
		GT1XPN(I+2)=ANVAL(GT1BA(I))
		PHFPPN(I)=ANVAL(PHFPA(I))
		FA1XPN(I)=ANVAL(FA1AA(I))
		FA1XPN(I+2)=ANVAL(FA1BA(I))	
		IT1XPN(I)=ANVAL(IT1AA(I))
		IT1XPN(I+2)=ANVAL(IT1BA(I))
		LTPHPN(I)=ANVAL(LTPHA(I))
C
		DO 200 N=30,2,-1
			IF (I .EQ. 1)L4TONS(N)=L4TONS(N-1)
			IF (I .EQ. 2)L5TONS(N)=L5TONS(N-1)
200		CONTINUE
C
		IF (I .EQ. 1)L4TONS(1)=LTPHPN(I)
		IF (I .EQ. 2)L5TONS(1)=LTPHPN(I)
C
		ACCUM=0
		OBS=0
C
		DO 300 N=1,30
			IF (I .EQ. 1)THEN
				IF (L4TONS(N) .EQ. 0)GOTO 300
				ACCUM=ACCUM+L4TONS(N)
				OBS=OBS+1
			ELSE
				IF (L5TONS(N) .EQ. 0)GOTO 300
				ACCUM=ACCUM+L5TONS(N)
				OBS=OBS+1
			END IF
C
300		CONTINUE
C
		IF (OBS .EQ. 0)THEN
			TEMPSP=0
		ELSE
			TEMPSP=(ACCUM/OBS)
		END IF
C
		IF (TEMPSP .LT. 0)TEMPSP=0
		CSETPT(I)=TEMPSP		!FLOATING TONNAGE SETPOINT
C
		CALLCHKCX(GRATCX(I),BIT)	!CHECK IF GRATE RUNNING
		IF (BIT .EQ. 1) THEN
			CONTRL(I)=JIBCLR(CONTRL(I),22)!GRATE ON	
		ELSE	
			CONTRL(I)=JIBSET(CONTRL(I),22)!GRATE OFF
		END IF
C
		BIT=JIBITS(CONTRL(I),23,1)	!IS LINE OFF CONTROL
		IF (BIT .EQ. 1)CONTRL(I)=JIBSET(CONTRL(I),21)	!YES...
C				USE DAMPER POSITIONS WHEN WE COME ON

		OBAS1X(I)=ANVAL(DP1AA(I))	!LOAD DAMPER POSOTIONS
		OUT1X(I)=ANVAL(DP1AA(I))
		OBAS1X(I+2)=ANVAL(DP1BA(I))
		OUT1X(I+2)=ANVAL(DP1BA(I))
C
	DO 500 J=1,2
		K=(J-1)*2+I
		TEMP=GT1XPN(K)-GT1XPO(K)	!PERFORM THERMOCOUPLE CALC
		TEMP=TEMP*TEMP			!ON GRATE TEMPERATURES
		IF (GT1XPN(K) .GT. GT1XPO(K))THEN
			CTEMPN(K)=GT1XPN(K)+(TEMP*THERK)
		ELSE
			CTEMPN(K)=GT1XPN(K)-(TEMP*THERK)
		END IF
500 	CONTINUE
C
		CALL	CHKCX(FN1ACX(I),BIT)	!SEE IF 1A FAN RUNNING
		IF (BIT .EQ. 1)THEN
			CONTRL(I)=JIBCLR(CONTRL(I),11)
		ELSE
			CONTRL(I)=JIBSET(CONTRL(I),11)
		END IF
C
		CALL	CHKCX(FN1BCX(I),BIT)	!SEE IF 1B FAN RUNNING
		IF (BIT .EQ. 1)THEN
			CONTRL(I)=JIBCLR(CONTRL(I),12)
		ELSE	
			CONTRL(I)=JIBSET(CONTRL(I),12)
		END IF
C
		DO 550 N=1,8
			CONTRL(I)=JIBCLR(CONTRL(I),N-1)	!CLEAR OUTPUT INDIC BITS
550		CONTINUE
C
	DO 1000 J=1,2
		K=(J-1)*2+I
		BIT=JIBITS(CONTRL(I),23,1)	!ARE WE OFF COMPUTER
		IF (BIT .EQ. 1)GOTO 1000	!YES SKIP CALC
C
		BIT=JIBITS(CONTRL(I),22,1)	!IS GRATE OFF
600		CONTINUE
C
		IF (BIT .EQ. 1)THEN
			OUT1X(K)=OUT1X(K)-CUTBCK	!YES CLOSE DAMPER
			IF (OUT1X(K) .LT. 0)OUT1X(K)=0	!UNTIL ZERO
			OBGT1X(K)=RSTART	!RESET BASES
			OBIT1X(K)=RSTART
			OBFA1X(K)=RSTART
			OBFP1X(K)=RSTART
			GOTO 1000
		END IF
C
		BIT=JIBITS(CONTRL(I),J+10,1)	!IS FAN RUNNING
		IF (BIT .EQ. 0)THEN
			GOTO 700		!YES CONTINUE TO CALC
		ELSE
			GOTO 600		!NO GO CLOSE DAMPER
		END IF
C
700		CONTINUE
		IF (LTPHPN(I) .LT.200)THEN	!IF TONS < 200 
			OUT1X(K)=OUT1X(K)-CUT	!CUT OUTPUT BY 5%
			IF (OUT1X(K) .LT. 0)OUT1X(K)=0
			GOTO 1000
		END IF
C
C	CALCULATE CORRECTION TO GRATE TEMP OUTPUT BASED ON TONS
C
		BITX=JIBITS(CONTRL(I),21,1)	!IS BASE USABLE
C
		IF (BITX .EQ. 1)THEN
			NB=0
		ELSE
			NB=COUTPT(K)
		END IF
C
		PN=CSETPT(I)
		SET=LTPHPN(I)
		PO=0
		GAIN=CGAIN(I)
		DERIV=0
		HI=CHI(I)
		LO=CLO(I)
C
		CALL	NOUTPT(NB,SET,PN,PO,GAIN,DERIV,OUTX,HI,LO)
C
		COUTPT(K)=OUTX	!NEW OUTPUT FOR TONNAGE CORRECTION
C
C	CALCULATE GRATE TEMP BASE
C
		BIT=JIBITS(CONTRL(I),8,1)	!CHECK WHICH TEMP
		IF (BIT .EQ. 1) THEN		!SETPOINT TO USE
			SET=ASETPT(I)
		ELSE
			SET=GTPSET(I)
		END IF
C
		PN=CTEMPN(K)
		RESET=GTPRES(I)
		LO=GTLO(I)
		HI=GTHI(I)
C
		IF (BITX .EQ. 1)THEN
			OB=OBAS1X(K)
		ELSE	
			OB=OBGT1X(K)
		END IF
C
		CALL	NBASE(OB,SET,PN,RESET,NB,HI,LO)
		BASES(1)=NB
C
C	CALCULATE FURNACE PRESSURE BASE 
C
		SET=PFPSET(I)
		PN=PHFPPN(I)
		RESET=PFPRES(I)
C
		IF (BITX .EQ. 1)THEN
			OB=OBAS1X(K)
		ELSE	
			OB=OBFP1X(K)
		END IF
C
		LO=PHFPLO(I)
		HI=PHFPHI(I)
		CALL	NBASE(OB,SET,PN,RESET,NB,HI,LO)
		BASES(2)=NB
C
C	CALCULATE INLET TEMPERATURE BASE
C
		SET=INTSET(I)
		PN=IT1XPN(K)
		RESET=INTRES(I)
C
		IF (BITX .EQ. 1)THEN
			OB=OBAS1X(K)
		ELSE
			OB=OBIT1X(K)
		END IF
C
		LO=INTLO(I)
		HI=INTHI(I)
		CALL	NBASE(OB,SET,PN,RESET,NB,HI,LO)
		BASES(3)=NB
C
C	CALCULATE FAN AMPS BASE
C
		SET=FASET(I)
		RESET=FARES(I)
		PN=FA1XPN(K)
C
		IF (BITX .EQ. 1)THEN
			OB=OBAS1X(K)
		ELSE
			OB=OBFA1X(K)
		END IF
C
		LO=FALO(I)
		HI=FAHI(I)
		CALL	NBASE(OB,SET,PN,RESET,NB,HI,LO)
		BASES(4)=NB
C
C	CALCULATE GRATE TEMPERATURE OUTPUT
C
		PN=CTEMPN(K)
		PO=CTEMPO(K)
		SET=GTPSET(I)
		GAIN=GTGAIN(I)
		DERIV=GTPDER(I)
		NB=BASES(1)
		LO=GTLO(I)
		HI=GTHI(I)
		CALL	NOUTPT(NB,SET,PN,PO,GAIN,DERIV,OUTX,HI,LO)
		OUTPTS(1)=OUTX
C
C	CALCULATE FURNACE PRESSURE OUTPUT
C
		PN=PHFPPN(I)
		PO=PHFPPO(I)
		SET=PFPSET(I)
		GAIN=FPGAIN(I)
		DERIV=PFPDER(I)
		NB=BASES(2)
		LO=PHFPLO(I)
		HI=PHFPHI(I)
		CALL	NOUTPT(NB,SET,PN,PO,GAIN,DERIV,OUTX,HI,LO)
C
		IF (PHFPPN(I) .LT. PFPSET(I))THEN
			OUTPTS(2)=110.0
		ELSE
			OUTPTS(2)=OUTX
		END IF
C
C	CALCULATE INLET TEMPERATURE OUTPUT
C
		PN=IT1XPN(K)
		PO=IT1XPO(K)
		SET=INTSET(I)
		GAIN=ITGAIN(I)
		DERIV=INTDER(I)
		NB=BASES(3)
		LO=INTLO(I)
		HI=INTHI(I)
		CALL	NOUTPT(NB,SET,PN,PO,GAIN,DERIV,OUTX,HI,LO)
C
		IF (IT1XPN(K) .LT. INTSET(I))THEN
			OUTPTS(3)=110.0
		ELSE
			OUTPTS(3)=OUTX
		END IF
C
C	CALCULATE FAN AMPS OUTPUT
C
		PN=FA1XPN(K)
		PO=FA1XPO(K)
		SET=FASET(I)
		GAIN=FAGAIN(I)
		DERIV=FADER(I)
		NB=BASES(4)
		LO=FALO(I)
		HI=FAHI(I)
		CALL	NOUTPT(NB,SET,PN,PO,GAIN,DERIV,OUTX,HI,LO)
C
		IF (FA1XPN(K) .LT. FASET(I))THEN
			OUTPTS(4)=110.0
		ELSE
			OUTPTS(4)=OUTX
		END IF
C
C	LOCATE THE LOWEST OF THE FOUR OUTPUTS
C
		TEMP=AMIN1(OUTPTS(1),OUTPTS(2),OUTPTS(3),OUTPTS(4))
		DO 800 L=1,4
			IF (TEMP .EQ. OUTPTS(L))GOTO 900
800		CONTINUE
900		CONTINUE
C
		OUTPTS(1)=OUTPTS(1)+COUTPT(K)	!ADD TONNAGE CORRECTION
		IF (OUTPTS(1) .GT. GTHI(I))OUTPTS(1)=GTHI(I)
		IF (OUTPTS(1) .LT. GTLO(I))OUTPTS(1)=GTLO(I)
C

		OUT1X(K)=OUTPTS(L)	!OUTPUT THIS VALUE TO DAMPER
		M=((J-1)*4+L)-1
		CONTRL(I)=JIBSET(CONTRL(I),M)	!INDICATE INPUT RESP.
		OBGT1X(K)=BASES(L)	!RESET BASES FOR ALL LOOPS
		OBFP1X(K)=BASES(L)
		OBIT1X(K)=BASES(L)
		OBFA1X(K)=BASES(L)
C
1000	CONTINUE
		BIT=JIBITS(CONTRL(I),23,1)
		IF (BIT .EQ. 0)CONTRL(I)=JIBCLR(CONTRL(I),21)
C
2000	CONTINUE
C
C	SECTION FOR CONTROL OF LINE 4 AND 5 3A FAN DAMPERS
C
	DO 3000	I=1,2		!INDEX TO LINE ...I=1=L4...I=2=L5
		CALL	CHKCX(NFFCX(I),BIT)	!SEE IF ON COMP
		IF (BIT .EQ. 1)THEN
			CNTRLX(I)=JIBCLR(CNTRLX(I),23)	!ON COMP
		ELSE
			CNTRLX(I)=JIBSET(CNTRLX(I),23)	!OFF COMP
		END IF
C
		HDSET(I)=ANVAL(SPT3AX(I))	!GET CONTROLLER SETPT
		IF ((HDSET(I) .LT. -0.5).OR.(HDSET(I) .GT. 0.5))THEN
			CNTRLX(I)=JIBSET(CNTRLX(I),8)	!SP IS BAD
		ELSE
			CNTRLX(I)=JIBCLR(CNTRLX(I),8)	!SP IS GOOD
		END IF
C
		BIT=JIBITS(CONTRL(I),22,1)	!SEE IF GRATE RUNNING
		IF (BIT .EQ. 1)HDSET(I)=HDSET(I)-0.2
C		DECREASE HOOD DRAFT SP WHILE GRATE IS DOWN
C
		GOTO 2200
C
C		TEMP=ANVAL(STKCAP(I))	!LOAD STACK CAP POSITION
C		IF (TEMP .LT. 15.0)GOTO 2200	!CAP IS CLOSED
C
		OUT3A(I)=5		!PUT 3A FAN AT 5% WHILE CAP OPEN
2050		CONTINUE
C
		OBFAN(I)=0
		OBHD(I)=0
		OBDP(I)=0
		DO 2100	N=1,2
			CNTRLX(I)=JIBCLR(CNTRLX(I),N-1)	!CLR OUTPUT BITS
2100		CONTINUE
		GOTO 2900
C
2200		CONTINUE
		FANPO(I)=FANPN(I)	!SAVE OLD SCAN VALUES
		HDPO(I)=HDPN(I)
C
		FANPN(I)=ANVAL(FN3A(I))	!FAN AMPS THIS SCAN
		HDPN(I)=ANVAL(HD3A(I))	!HOOD DRAFT THIS SCAN
		OBAS3A(I)=ANVAL(DP3A(I))	!OUTPUT THIS SCAN
		FMWT(I)=ANVAL(MWT(I))	!3A MTR WIND TEMP THIS SCAN
C
		BIT=JIBITS(CNTRLX(I),23,1)	!IS LINE OFF CTL
		IF (BIT .EQ. 1)THEN		!YES USE DAMPER POS
			CNTRLX(I)=JIBSET(CNTRLX(I),21)	!WHEN WE COME ON
			ASOB(I)=HAMPS(I)	!RESET AMP SETPT BASE
			IF (OBAS3A(I) .GT. 100)THEN
				OUT3A(I)=100.0
			ELSE
				OUT3A(I)=OBAS3A(I)
			END IF
		END IF
C
		DO 2300 N=1,2
			CNTRLX(I)=JIBCLR(CNTRLX(I),N-1)
2300		CONTINUE
C
		CALL	CHKCX(F3ACX(I),BIT)	!CHECK IF 3A FAN IS RUNNING
		IF (BIT .EQ. 1)THEN
			CNTRLX(I)=JIBCLR(CNTRLX(I),6)	!FAN IS ON
		ELSE
			CNTRLX(I)=JIBSET(CNTRLX(I),6)	!FAN IS OFF
		END IF
C
		BIT=JIBITS(CNTRLX(I),23,1)
		IF (BIT .EQ. 1)GOTO 2900	!LINE OFF CONTROL
		BIT=JIBITS(CNTRLX(I),6,1)
		IF (BIT .EQ. 1)THEN
			OUT3A(I)=0		!FAN IS NOT RUNNING
			GOTO 2050
		END IF
C
C	HERE WE BEGIN OUTPUT CALCULATIONS
C
		SETSAV=(MWTSET(I)-FMWT(I))*MWTRES(I)+ASOB(I)
		IF (SETSAV .LT. 0)SETSAV=0
		IF (SETSAV .GT. HAMPS(I))SETSAV=HAMPS(I)
		FANSET(I)=SETSAV	!NEW 3A FAN AMP SETPOINT
		ASOB(I)=SETSAV
		BITX=JIBITS(CNTRLX(I),21,1)	!IS BASE USABLE INDICATION
C
C	CALCULATE NEW BASE FOR FAN AMPS
C
		SET=FANSET(I)
		PN=FANPN(I)
		RESET=FANRES(I)
C
		IF (BITX .EQ. 1)THEN
			OB=OBAS3A(I)
		ELSE
			OB=OBFAN(I)
		END IF
C
		LO=FANLO(I)
		HI=FANHI(I)
		CALL	NBASE(OB,SET,PN,RESET,NB,HI,LO)
		BASES(1)=NB
		OBFAN(I)=NB
C
		DPSET(I)=(OUT1X(I)+OUT1X(I+2))/2	!PH DAMPER SETPT
C
C	CALCULATE NEW BASE FOR HOOD DRAFT
C
		BIT=JIBITS(CNTRLX(I),8,1)
		IF (BIT .EQ. 1)THEN
			SET=AHDSET(I)
		ELSE
			SET=HDSET(I)
		END IF
C
		PN=HDPN(I)
		RESET=HDRES(I)
		TEMP=CONSTC(I)*DPSET(I)
C
		IF (BITX .EQ. 1)THEN
			OB=OBAS3A(I)-TEMP
		ELSE
			OB=OBHD(I)
		END IF
C
		LO=HDLO(I)
		HI=HDHI(I)
		CALL	NBASE(OB,SET,PN,RESET,NB,HI,LO)
		BASES(2)=NB
		OBHD(I)=NB
C
C	CALCULATE FAN AMPS OUTPUT
C
		PN=FANPN(I)
		PO=FANPO(I)
		SET=FANSET(I)
		GAIN=FNGAIN(I)
		DERIV=FNDER(I)
		NB=BASES(1)
		LO=FANLO(I)
		HI=FANHI(I)
		CALL	NOUTPT(NB,SET,PN,PO,GAIN,DERIV,OUTX,HI,LO)
		OUTPTS(1)=OUTX
C
C	CALCULATE HOOD DRAFT OUTPUT
C
		PN=HDPN(I)
		PO=HDPO(I)
		SET=HDSET(I)
		GAIN=HDGAIN(I)
		DERIV=HDDER(I)
		NB=BASES(2)
		LO=HDLO(I)
		HI=HDHI(I)
		CALL	NOUTPT(NB,SET,PN,PO,GAIN,DERIV,OUTX,HI,LO)
		OUTPTS(2)=OUTX
C
		IF (BITX .EQ. 1)THEN
			DPCALC(I)=DPSET(I)
			OBDP(I)=DPSET(I)
		END IF
C
		TEMP=DPSET(I)-DPCALC(I)	!PERCENT ERROR
		BASES(3)=TEMP*DPRES(I)+OBDP(I)
		IF (BASES(3) .LT. DPLO(I))BASES(3)=DPLO(I)
		IF (BASES(3) .GT. DPHI(I))BASES(3)=DPHI(I)
		OUTPTS(3)=TEMP*DPGAIN(I)+BASES(3)
		IF (OUTPTS(3) .GT. DPHI(I))OUTPTS(3)=DPHI(I)
		DPCALC(I)=OUTPTS(3)
C
		TEMP=CONSTC(I)*OUTPTS(3)
		OUTPTS(2)=OUTPTS(2)+TEMP
C
		IF (OUTPTS(2) .GT. DPHI(I))THEN
			OUTPTS(2)=DPHI(I)
			OBHD(I)=DPHI(I)-TEMP
		END IF
C
		IF (OUTPTS(2) .LT. DPLO(I))THEN
			OUTPTS(2)=DPLO(I)
			OBHD(I)=DPLO(I)-TEMP
		END IF
C
		N=2	!ASSUME OUTPTS(2) LESS THAN OUTPTS(1)
		IF (OUTPTS(1) .LT. OUTPTS(2))N=1	!ELSE OPPOSITE
		OUT3A(I)=OUTPTS(N)	!OUTPUT FOR THIS FAN
		CNTRLX(I)=JIBSET(CNTRLX(I),N-1)	!SAVE INDICATION
C
		IF (N .EQ. 2)THEN	!IF CONTROLLING FROM DAMPERS
			OBDP(I)=BASES(3)	!REPLACE DAMPER BASE
			OBFAN(I)=OUTPTS(2)	!CORRECT FAN AMP BASE
			GOTO 2900
		END IF
C
		OBHD(I)=OBFAN(I)-(BASES(3)*CONSTC(I))	!CORRECT HOOD DRAFT BASE
C
2900		CONTINUE
C
		BIT=JIBITS(CNTRLX(I),23,1)
		IF (BIT .EQ. 0)CNTRLX(I)=JIBCLR(CNTRLX(I),21)
		IF (N .EQ. 1)ASOB(I)=SETSAV	!IF CONTROLLING FROM
C						FAN AMPS RESTORE SETPT BASE
C
3000	CONTINUE
C
	IF (put_digital_val .EQ. 1)THEN	!PERFORM COMPUTER ACKNOWLEDGE
		put_digital_val=0		!IF RELAY OPEN THEN CLOSE IT
	ELSE
		put_digital_val=1		!IF RELAY CLOSED THEN OPEN IT
	END IF
C
	status = SHC_put_point_quality(DO, point_quality)
	status = SHC_put_digital_val( DO, put_digital_val)
C
4000	CONTINUE
C
	put_analog_val=OUT1X(1)	!LINE 4 1A DAMPER POSITION
	status = SHC_put_point_quality(AO(1), point_quality)
	status = SHC_put_analog_val(AO(1), put_analog_val)
C
	put_analog_val=OUT1X(3)	!LINE 4 1B DAMPER POSITION
	status = SHC_put_point_quality(AO(2), point_quality)
	status = SHC_put_analog_val(AO(2), put_analog_val)
C
	put_analog_val=OUT1X(2)	!LINE 5 1A DAMPER POSITION
	status = SHC_put_point_quality(AO(3), point_quality)
	status = SHC_put_analog_val(AO(3), put_analog_val)
C
	put_analog_val=OUT1X(4)	!LINE 5 1B DAMPER POSITION
	status = SHC_put_point_quality(AO(4), point_quality)
	status = SHC_put_analog_val(AO(4), put_analog_val)
C
	put_analog_val=OUT3A(1)	!LINE 4 3A DAMPER POSITION
	status = SHC_put_point_quality(AO(5), point_quality)
	status = SHC_put_analog_val(AO(5), put_analog_val)
C
	put_analog_val=OUT3A(2)	!LINE 5 3A DAMPER POSITION
	status = SHC_put_point_quality(AO(6), point_quality)
	status = SHC_put_analog_val(AO(6), put_analog_val)
C
5000	CONTINUE
C
	CALL	SYS$HIBER()
	GOTO 100
C
99999	CONTINUE
C
	CALL	EXIT
C
	END
C
	SUBROUTINE	CHKCX(POINT,BIT)
C
C	SUBROUTINE WILL RETURN THE STATUS OF A CONTACT (0 OR 1)
C
	INTEGER*4	BIT,POINT,I,J,CXSTS(100)
C
	COMMON/CONTACTS/CXSTS
C
	I=(POINT/32)+1		!WORD IN TABLE
	J=MOD(POINT,32)		!BIT IN WORD
	BIT=JIBITS(CXSTS(I),J,1)!EXTRACT BIT
	RETURN
	END
C
	SUBROUTINE	CHKGPT(POINT,BIT)
C
C	SUBROUTINE WILL RETURN GOODPT STATUS OF SELECTED ANALOG
C
	INTEGER*4	BIT,POINT,I,J,GPT(50)
C
	COMMON/GOODPTTBL/GPT
C
	I=(POINT/32)+1		!WORD IN TABLE
	J=MOD(POINT,32)		!BIT IN WORD
	BIT=JIBITS(GPT(I),J,1)	!EXTRACT BIT
	RETURN
	END
C
	SUBROUTINE	NBASE(OB,SET,PN,RESET,NB,HI,LO)
C
C	SUBROUTINE WILL CALCULATE NEW BASE FOR PROCESS VARIABLE
C
	IMPLICIT NONE
C
	REAL*4		OB,SET,PN,RESET,NB,HI,LO
C
	NB=(SET-PN)*RESET+OB
  	IF (NB .GT. HI)NB=HI
	IF (NB .LT. LO)NB=LO
C
	RETURN
	END
C
	SUBROUTINE	NOUTPT(NB,SET,PN,PO,GAIN,DERIV,OUTX,HI,LO)
C
C	SUBROUTINE WILL CALCULATE NEW OUTPUT FOR PROCESS VARIABLE
C
	IMPLICIT NONE
C
	REAL*4		NB,SET,PN,PO,GAIN,DERIV,OUTX,HI,LO
C
	OUTX=((SET-PN)*GAIN)+((PO-PN)*DERIV)+NB
	IF (OUTX .GT. HI)OUTX=HI
	IF (OUTX .LT. LO)OUTX=LO
C
	RETURN
	END
