C
C	***********************************************************
C
C		          HRAGGL2
C
C	  PROGRAM TO MAINTAINT THE STEP II AGGL EQUIPMENT
C	  RUNNING TIME.  THE DIGITAL CONTACTS ARE SCANNED
C	  EVERY FIVE MINUTES AND IF THE CONTACT IS CLOSED
C	  ONE COUNT IS ADDED TO THE COUNT TOTAL IN THE
C	  AGGL_EQUIP_2.DAT FILE.  IF THE CONTACT IS OPEN
C	  NO COUNT IS ADDED.  THE CONTACT NUMBERS SCANNED
C	  ARE THOSE LISTED IN THE AGGL_EQUIP_2.DAT FILE
C         FOR STEP I & II AND IN THE ARRAY AG3_FILTER(12)
C	  FOR THE STEP III FILTERS.
C
C	  EACH TIME THE PROGRAM IS RESTARTED THE CURRENT 
C	  BINARY DATE-TIME NUMBER IS STORED IN THE FIRST
C	  RECORD.  THE PREVIOUS START DATE IS TRANSFERRED
C	  FROM THE FIRST TO THE SECOND RECORD.
C
C	26-JAN-1995	ALPHA VERSION
C
C	**********************************************************
C	31-MAY-1989: ADDED AN OPEN FAILURE ERROR HANDLING TO TAKE CARE
C		OF CONFLICTS WITH THE DAYHRS PROGRAM
C	3-SEP-1992	Added step three filters.
C	18-APR-1995	HAD TO RELINK BECAUSE OF NEW DIGCOM
C	20-MAR-2000  NEW VERSION FOR A7
C	****************************************************************
C	LINK	HRAGGL2,FILTER/OPT
C	****************************************************************
	IMPLICIT	INTEGER (A-Z)
	EXTERNAL	UFO_OPEN
C	INCLUDE		'($SSDEF)'
C
	STRUCTURE/DIGFILE/
		INTEGER		POINT_REF  !POINT REFERENCE NO.
		INTEGER		COUNT	   !ACCUMULATED TIME COUNTS
		INTEGER		COUNT_1    !RESET COUNTER
		INTEGER		COUNT_2    !REFERENCE COUNTER
		INTEGER		COUNT_3
		CHARACTER*11	POINT_NO   !POINT NUMBER
		CHARACTER*32	DESCR      !ENGLISH DESCRIPTION
	END STRUCTURE
	RECORD/DIGFILE/POINT
C
	INTEGER			TIME_1(2),REGISTER 
	INTEGER			CXSTS(100)
	CHARACTER*7		WAKE/'0 00:05'/	!5 MINUTE WAKEUP
	CHARACTER*40		FILE_1/'AGGL_HRS:
	1AGGL_EQUIP_2.DAT'/
C	*** STEP THREE FILTERS ****
	INTEGER*4	AG3STS(100) !STEP3 CONTACTS
C	*** POINT NUMBERS OF STEP THREE FILTERS ***
	INTEGER*4	AG3_FILTERS(12)/214,	!208-06-1
	1				215,	!208-06-2
	1				218,	!208-06-3
	1				219,	!208-06-4
	1				222,	!208-06-5
	1				224,	!208-06-6
	1				225,	!208-07-1
	1				227,	!208-07-2
	1				229,	!208-07-3
	1				232,	!208-07-4
	1				234,	!208-07-5
	1				236/	!208-07-6
C
	COMMON/CONTACTS/CXSTS   !STEP I &II
	COMMON/CONTACTSAG3/AG3STS    !STEP III
C
10	OPEN(1,FILE=FILE_1,STATUS='OLD',ERR=400,
	1    ORGANIZATION='SEQUENTIAL',
	1    ACCESS='DIRECT',
	1    RECORDTYPE='FIXED',
	1    RECL=16,
	1    FORM='UNFORMATTED',
	1    USEROPEN=UFO_OPEN,SHARED)
C
C	***** GET CURRENT TIME *******************
	READ(1,REC=1)POINT	!EXCHANGE DATES
	WRITE(1,REC=2)POINT	
	STATUS=SYS$GETTIM(%REF(TIME_1))  !CURRENT TIME
	IF( .NOT. STATUS) CALL LIB$SIGNAL(%VAL(STATUS))
C
	POINT.POINT_REF=TIME_1(1)
	POINT.COUNT=TIME_1(2)       
	WRITE(1,REC=1) POINT
C
C	****** SCHEDULE WAKEUP EVERY 5 MINUTES *******
C
	STATUS=SYS$BINTIM(%DESCR(WAKE),%REF(TIME_1))
	IF( .NOT. STATUS) CALL LIB$SIGNAL(%VAL(STATUS))
C
	STATUS=SYS$SCHDWK(,,%REF(TIME_1),%REF(TIME_1))
	IF( .NOT. STATUS) CALL LIB$SIGNAL(%VAL(STATUS))
C
C	NOTE: THE REC # STARTS AT 3, TO SKIP THE THE 2 RECORDS
C	THAT CONTAIN THE TIME. RECORDS 3 THRU 14 CONTAIN THE STEP3
C	FILTERS.  
C	NOTE ** IN THE PROGRAM THAT MAINTAINS THE FILTERS THE OLD POINT
C	NUMBERS ARE MAINTAINED.  THE NUMBERS IN AG3_FILTERS(12) ARE THE
C	ACTUAL POINT NUMBERS IN THE STEP 3 COMMON.
C
C	*** PROCESS STEP III POINTS *****************************
1000	DO REGISTER=3,14
	  READ(1,REC=REGISTER ,ERR=110)POINT
	  POINT.COUNT_2=POINT.COUNT_2+1  !INCREMENT THE REFERENCE COUNTER
	  K=(AG3_FILTERS(REGISTER-2)/32)+1
	  L=MOD(AG3_FILTERS(REGISTER-2),32)
	  IF(BJTEST(AG3STS(K),L)) THEN
	     POINT.COUNT=POINT.COUNT+1      !INCREMENT ACCUMULATOR
	     POINT.COUNT_3=POINT.COUNT_3+1  !INCREMENT ALTERNATE ACCUM. 
	     POINT.COUNT_1=POINT.COUNT_1-1  !DECREMENT COUNT DOWN
	  END IF
	  WRITE(1,REC=REGISTER )POINT
	END DO
C	****** PROCESS STEP I & II POINTS ***********************
110	REGISTER =15
100	READ(1,REC=REGISTER ,ERR=300)POINT
	POINT.COUNT_2=POINT.COUNT_2+1  !INCREMENT THE REFERENCE COUNTER
	K=(POINT.POINT_REF/32)+1
	L=MOD(POINT.POINT_REF,32)
	IF(BJTEST(CXSTS(K),L)) THEN
	   POINT.COUNT=POINT.COUNT+1      !INCREMENT ACCUMULATOR
	   POINT.COUNT_3=POINT.COUNT_3+1  !INCREMENT ALTERNATE ACCUM. 
	   POINT.COUNT_1=POINT.COUNT_1-1  !DECREMENT COUNT DOWN
	END IF
	WRITE(1,REC=REGISTER )POINT
	REGISTER =REGISTER +1
	GOTO 100
C
C	**** HIBERNATE FOR 5 MINUTES *****************
300	CLOSE(1)
	STATUS=SYS$HIBER()
	IF( .NOT. STATUS) CALL LIB$SIGNAL(%VAL(STATUS))
C
302	OPEN(1,FILE=FILE_1,STATUS='OLD',ERR=500,
	1    ORGANIZATION='SEQUENTIAL',
	1    ACCESS='DIRECT',
	1    RECORDTYPE='FIXED',
	1    RECL=16,
	1    FORM='UNFORMATTED',
	1    USEROPEN=UFO_OPEN,SHARED)
C
	GOTO 1000
C	*** OPEN ERROR SHOULD HAPPEN IF DAHYRS PROGRAM HAS THE FILE OPEN
C	*** FOR EXCLUSIVE USE WHILE DOING ITS UPDATE ***
400	CALL LIB$WAIT(2.)
	GOTO 10  !TRY AGAIN  (INITIAL OPEN WHEN PROGRAM FIRST RUN)
C
500	CALL LIB$WAIT(2.)
	GOTO 302  !TRY AGAIN
	END
